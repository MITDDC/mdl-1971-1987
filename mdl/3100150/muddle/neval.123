TITLE EVAL -- MUDDLE EVALUATOR

RELOCATABLE

; GERALD JAY SUSSMAN, 1971

.GLOBAL PROCID,LPROG,IDVAL,CALER1,SPECBIND,SPECSTORE,FINIS,SPTIME

.INSRT MUDDLE >

	MFUNCTION EVAL,SUBR
	INTGO
	HLRZ A,AB	;GET NUMBER OF ARGS
	CAIE A,-2	;EXACTLY 1?
	JRST AEVAL	;EVAL WITH AN ALIST
	HLRZ	A,(AB)	;GET TYPE OF ARG
	CAILE A,NUMPRI	;PRIMITIVE?
	JRST NONEVT	;NO
	JRST @EVTYPT(A)	;YES-DISPATCH

SELF:	MOVE A,(AB)	;TYPES WHICH EVALUATE 
	MOVE B,1(AB)
	JRST FINIS		;TO SELF-EG NUMBERS

;EVALUATES A IDENTIFIER -- GETS LOCAL VALUE IF THERE IS ONE, OTHERWISE GLOBAL.

MFUNCTION VALUE,SUBR
	ENTRY 1
	HLLZ A,(AB)
	CAME A,$TATOM
	JRST NONATM
	MOVE B,1(AB)	;TO PASS TO THE
	PUSHJ P,IDVAL
	JRST FINIS

IDVAL:	PUSHJ P,IVAL	;LOCAL VALUE FINDER
	CAME A,$TUNBOUND	;IF NOT UNBOUND OR UNASSIGNED
	POPJ P,	;DONE
	JUMPN B,UNAS	;IF UNASSIGNED - ERROR
	MOVE B,1(AB)	;GET POINTER TO ATOM
	HLRZ A,(B)	;GET TYPE IN VALUE CELL
	CAIE A,TUNBOUND	;IF UNBOUND
	CAIN A,TLOCI
	JRST UNBOU	;ERROR -- UNBOUND VARIABLE
	MOVE A,(B)	;OTHERWISE GET GLOBAL VALUE
	MOVE B,1(B)
	POPJ P,

; GETS A LOCATIVE TO THE LOCAL VALUE OF AN IDENTIFIER.

MFUNCTION LOCA,SUBR,LOC
	ENTRY 1
	HLLZ A,(AB)
	CAME A,$TATOM
	JRST NONATM
	MOVE B,1(AB)
	PUSHJ P,ILOC
	CAMN A,$TUNBOUND
	JRST UNBOU
	JRST FINIS

;EVALUATE A FORM. IF CAR IS AN ATOM USE GLOBAL VALUE OVER LOCAL ONE.

EVFORM:	SKIPN C,1(AB)	;NIL?
	JRST IFALSE
	HLLZ A,(C)	;GET CAR TYPE
	CAME A, $TATOM	;ATOMIC?
	JRST EV0	;NO -- CALCULATE IT
	MOVE B,1(C)	;GET PTR TO ATOM
	HLRZ D,(B)	;GET TYPE IN VALUE CELL
	CAIE D,TUNBOUND	;DOES IT HAVE A
	CAIN D,TLOCI	;GLOBAL VALUE?
	JRST GLOC	;DOES IT HAVE A LOCAL VALUE?
	PUSH AP,(B)	;YES -- IT IS THE FUNCTION
	PUSH AP,1(B)
	JRST IAPPLY	;APPLY IT
EV0:	PUSH AP,A	;SET UP CAR OF FORM AND
	PUSH AP,1(C)
	MCALL 1,EVAL	;EVALUATE IT
	PUSH AP,A	;APPLY THE RESULT
	PUSH AP,B	;AS A FUNCTION
	JRST IAPPLY

GLOC:	PUSH AP,A
	PUSH AP,B
	MCALL 1,VALUE
	PUSH AP,A
	PUSH AP,B
	JRST IAPPLY

;DISPATCH TABLE FOR EVAL
DISTBL EVTYPT,NONEVT,[[TFIX,SELF],[TFLOT,SELF],[TCHRS,SELF],[TCHSTR,SELF]
[TATOM,SELF],[TLIST,EVLIST],[TLMNT,EVFORM],[TVEC,EVECT],[TTIME,SELF]]

AEVAL:
	CAIE A,-4	;EXACTLY 2 ARGS?
	JRST WNA"	;NO-ERROR
	HLRZ A,2(AB)	;CHECK THAT
	CAIE A,TENV	;THE SECOND ARG
	JRST TRYSP	;IS AN ENVIRONMENT
	MOVE B,3(AB)	;GET POINTER TO IT
	MOVE A,3(B)	;GET SKIP POINTER
	MOVE B,1(B)	;GET TIME
	CAME B,(A)	;IS THE PTR LEGIT?
	JRST BADENV	;NO
AEVAL1:
	PUSHJ P,MUNGSP	;YES -- MUNG IT
	PUSH SP,$TSKIP	;PUSH ON SKHP PTR
	PUSH SP,A
	PUSH AP,(AB)
	PUSH AP,1(AB)
	MCALL 1,EVAL	;EVAL TH GOODIE WITH HACKED VALUES
	PUSH TP,A
	PUSH TP,B
	MOVE A,(SP)	;GET SKIP PTR
	SUB SP,[2,,2]	;UNWIND IT
	PUSHJ P,MUNGSP
	POP TP,B
	POP TP,A
	JRST FINIS


TRYSP:	CAIE A,TSP	;IS IT AN SP (FROM DEBUGGER)?
	JRST BADENV
	MOVE A,3(AB)	;YES -- GET THE PTR
	JRST AEVAL1
MUNGSP:
	MOVE B,SP	;GET CURRENT SP
MUNGLP:
	CAMN B,A	;ARE WE DONE?
	POPJ P,	;YES
	HLRZ C,-1(B)	;GET TYPE OF NEXT GOODIE
	CAIE C,TTIME	;IS IT A CONTROL WORD?
	CAIN C,TSKIP
	JRST MG1	;YES
	MOVE C,-4(B)	;NO GET ATOM
	HLRZ D,(C)	;GET ITS VALUE
	CAIN D,TLOCI	;IS IT LOCAL?
	HLLZS 2(C)	;YES -- CLOBBER ITS PROCID
	SUB B,[6,,6]	;IN ANY CASE CONTINUE
	JRST MUNGLP
MG1:
	SKIPN (B)	;ARE WE AT THE END OF SP?
	JRST BADENV	;YES -- LOSE
	SUB B,[2,,2]	;NO -- CONTINUE
	JRST MUNGLP
IAPPLY:
	HLRZ A,2(AB)	;GET TYPE OF FUNCTION
	CAIN A,TSUBR	;SUBR?
	JRST APSUBR	;YES
	CAIN A,TFSUBR	;NO -- FSUBR?
	JRST APFSUBR	;YES
	CAIN A,TEXPR	;NO -- EXPR?
	JRST APEXPR	;YES
	CAIN A,TFIX	;NO -- CALL TO NTH?
	JRST APNUM	;YES
	CAIN A,TFUNARG	;NO -- FUNARG?
	JRST APFUNARG	;YES
	JRST NAPT	;NONE OF THE ABOVE


;APFSUBR CALLS FSUBRS

APFSUBR:	HRRZ A,@1(AB)	;GET THE ARGUMENT LIST
	PUSH AP,$TLIST
	HRRZ B,AP
	PUSH AP,A	;PUSH THE ARGLIST
	HRLI B,-2	;MAKE AB POINTER
	JRST CALSUB

;APSUBR CALLS SUBRS

APSUBR:	PUSHJ P,EVLTUP	;EVALUATE THE ARGS AND SPREAD THEM OUT

CALSUB:	JSR SAVCAL"	;MAKE SAVE BLOCK
	MOVE A,3(AB)	;GET SUBROUTINE LOCATION
	MOVE AB,B	;CLOBBER ARGUMENT POINTER
	HRLI A,TENTRY	;SET UP CALL WORD
	PUSH TP,A	;AND SAVE IT
	PUSHJ TP,(A)	;GO TO SUBROUTINE
	JSR RESCAL	;RESTORE THE WORLD
	JRST FINIS

;APNUM INTERPRETS NUMBERS AS CALL TO FUNCTION GET

APNUM:
	HRRZ A,@1(AB)	;GET ARGLIST
	JUMPE A,ERRTFA	;NO ARGUMENT
	PUSH AP,(A)	;GET CAR OF ARGL
	HLLZS (AP)	
	PUSH AP,1(A)
	HRRZ A,(A)	;MAKE SURE ONLY ONE ARG
	JUMPN A,ERRTMA
	MCALL 1,EVAL
	PUSH AP,A
	PUSH AP,B
	PUSH AP,2(AB)
	PUSH AP,3(AB)
	MCALL 2,GET"
	JRST FINIS

;APEXPR APPLIES EXPRS
;EXPRESSION IS IN 0(AB),  FUNCTION IS IN 2(AB).

APEXPR:
	SKIPN A,3(AB)	;IS FUNCTION NIL?
	JRST NOBODY	;YES -- ERROR
	PUSH P,[0]	;TNE NUMBER OF VARIABLES BOUND WILL ACCUMULATE HERE
	PUSH TP,$TLIST	;MAKE A SLOT FOR LIST OF AUXILIARY VARIABLES
	PUSH TP,[0]
	PUSH TP,(A)	;MAKE A SLOT FOR THE VARIABLE DECLARATIONS
	HLLZS (TP)
	PUSH TP,1(A)	;AND FILL IT WITH CAR OF THE FUNCTION
	PUSH TP,$TLIST	;NEED ONE MORE SLOT FOR TEMPORARY STUFF
	PUSH TP, [0]

;NUMVARS = 0(P)
;AUXLIST = 0(TB)
;VARDEC = 2(TB)
;TEM = 4(TB)

	HLRZ A,2(TB)	;GET VARDEC TYPE
	CAIE A,TLIST	;IS IT A LIST
	JRST MPD	;NO -- LOSSAGE
	SKIPN A,3(TB)	;YES -- IS IT NIL?
	JRST RUNIT	;YES -- DONE, GO RUN PROGRAM
	HLRZ B,(A)	;GET TYPE OF CAR OF VARDEC
	CAIE B,TCHRS	;IS IT A CHARACTER STRING?
	JRST VBIND	;NO -- NORMAL VARIABLE BINDING LIST
	MOVE B,1(A)	;GET STRING
	CAME B,[ASCIZ /BIND/]	;IS IT A BINDING DECLARATION?
	JRST V1	;NO -- TRY SOMETHING ELSE
	HRRZ A,(A)	;GET CDR OF BINDING DECLARATION
	JUMPE A,MPD	;CHECK NON NIL
	MOVEM A,5(TB)	;SAVE IT IN TEM
	HLLZ B,(A)	;GET CAR TYPE
	CAME B,$TATOM	;CHECK ATOMIC
	JRST MPD	;NO -- ERROR
	PUSH TP,B	;OK -- SET UP THE ATOM
	PUSH TP,1(A)	;FOR BINDING TO
	PUSHJ P,AMAKE	;THE OUTER ENVIRONMENT
	PUSH AP,A	
	PUSH AP,B
	AOS (P)	;BUMP NUMVARS
	HRRZ A,@5(TB)	;GET CDDR OF VARDEC
	JUMPE A,MPD	;CHECK NON-NIL
	HLRZ B,(A)	;GET CADDR VARDEC
	CAIE B,TLIST	;LIST?
	JRST MPD	;NO -- ERROR
	SKIPN A,1(A)	;IS IT NIL?
	JRST DOBIND	;YES -- NO MORE VARIABLES
	MOVEM A,3(TB)	;STORE IN VARDEC
	HLRZ B,(A)	;GET TYPE OF CAR
	CAIE B,TCHRS	;CHARACTERS?
	JRST VBIND	;NO -- NORMAL VARIABLE LIST
	MOVE B,1(A)	;GET STRING
V1:	CAME B,[ASCIZ /AUX/]	;IS IT AN AUX DECLARATION?
	JRST V2	;NO -- TRY SOMETHING ELSE
	HRRZ A,(A)	;YES -- GET CDR
	JUMPE A,MPD	;CHECK NON-NIL
	HRRZ B,(A)	;GET CDDR
	MOVEM B,1(TB)	;STORE IN AUXLIST SLOT
	HLRZ B,(A)	;GET TYPE OF CADR
	CAIE B,TLIST	;LIST?
	JRST MPD	;NO -- ERROR
	SKIPN A,1(A)	;IS IT NIL?
	JRST DOBIND	;YES -- NO MORE VARIABLES
	MOVEM A,3(TB)	;NO -- SAVE
	HLRZ B,(A)	;GET TYPE OF CAR
	CAIE B,TCHRS	;CHARACTERS?
	JRST VBIND	;NO -- GO BIND
	MOVE B,1(A)	;GET STRING
V2:	CAME B,[ASCIZ /CALL/]	;IS IT A CALLSITE DECLARAITION?
	JRST V3		;NO -- TRY TUPLE
	HRRZ A,(A)	;YES -- GET CDR
	JUMPE A,MPD	;CHECK NON-NIL
	HLLZ B,(A)	;GET TYPE OF CAR
	CAME B,$TATOM	;ATOMIC?
	JRST MPD	;NO -- BAD VARIABLE SPECIFICATION
	PUSH TP,B	;OK -- BIND THE VARIABLE
	PUSH TP,1(A)	;TO
	PUSH AP,$TLIST	;THE ARGUMENT LIST
	PUSH AP,@1(AB)
	AOS (P)	;BUMP THE ARGCOUNT
	JRST DOBIND	;AND BIND IT UP
V3:	CAME B,[ASCIZ /TUPLE/]	;IS IT A TUPLE DECLARATION?
	JRST MPD	;NO-- LOSE
	HRRZ A,(A)	;YES -- GET CDR
	JUMPE A,MPD	;CHECK NON-NIL
	HLLZ B,(A)	;GET TYPE OF CADR
	CAME B,$TATOM	;ATOMIC?
	JRST MPD	;NO -- BAD VARIABLE SPECIFICATION
	PUSH TP,B	;OK -- BIND THE VARIABLE
	PUSH TP,1(A)	;TO THE EVALUATED
	PUSHJ P,EVLTUP	;TUPLE OF ARGS
	PUSH AP,A
	PUSH AP,B
	AOS (P)
	JRST DOBIND

;VBIND HANDLES NORMAL VARIABLE LISTS

VBIND:	HRRZ A,@1(AB)	;GET ARGLIST
	MOVEM A,5(TB)	;AND STORE IT IN TEM
VLUP:	INTGO	;CHECK OUT INTERRUPTS
	MOVE A,3(TB)	;GET THE VARLIST
	SKIPN B,5(TB)	;GET THE ARGLIST
	JUMPE A,DOBIND	;ARE WE DONE?
	JUMPE B,ERRTFA	;TOO FEW ARGS
	JUMPE A,ERRTMA	;TOO MANY ARGS
	AOS (P)	;BUMP ARGCNT
	HLLZ C,(A)	;GET TYPE OF VAR
	CAME C,$TATOM	;IS IT AN ATOM?
	JRST QVAR	;PROBABLY A QUOTED VARIABLE
	PUSH TP,C	;SET UP BINDING OF VARIABLE
	PUSH TP,1(A)	;TO
	PUSH AP,(B)
	HLLZS (AP)	;EVAL
	PUSH AP,1(B)	;OF THE MATCHING
	MCALL 1,EVAL	;ARGUMENT
	PUSH AP,A
	PUSH AP,B
	JRST NEXTV
QVAR:	CAMN C,$TLIST	;IS IT A LIST
	SKIPN A,1(A)	;NON-NIL?
	JRST MPD	;NO -- LOSE
	HLRZ C,(A)	;IS CAR OF THE GOODIE
	CAIE C,TATOM	;THE ATOM QUOTE?
	JRST MPD
	MOVE C,1(A)	
	CAME C,MQUOTE QUOTE
	JRST MPD	;NO -- LOSE
	HRRZ A,(A)	;CDR IT
	JUMPE A,MPD	;NIL CHECK
	HLLZ C,(A)	;GET TYPE OF VAR
	CAME C,$TATOM	;ATOM?
	JRST MPD	;NO -- LOSE
	PUSH TP,C
	PUSH TP,1(A)	;SETUP BIND OF ATOM TO
	PUSH AP,(B)	;THE LITERAL
	HLLZS (AP)	;ARGUMENT
	PUSH AP,1(B)	
NEXTV:	HRRZ A,@3(TB)	;SET THE VARLIST TO CDR OF
	MOVEM A,3(TB)	;THE VARLIST
	HRRZ B,@5(TB)	;SET THE ARGLIST TO
	MOVEM B, 5(TB)	;CDR OF THE ARGLIST
	JRST VLUP
DOBIND:	PUSHJ P,SPECBIND	;BIND THEM UP
	SUB TP,[4,,4]	;POP OFF TEM AND VARDEC
AUXLP:	INTGO
	SKIPN A,1(TB)	;IS THE AUXLIST NIL?
	JRST RUNIT	;YES -- DONE
	SETZM (P)	;ZERO THE ARGCNT
	HLLZ B,(A)	;GET TYPE OF CAR
	CAME B,$TATOM	;ATOMIC?
	JRST AUXIN	;IT IS AN INITIALIZED AUXILLIARY
	PUSH TP,B	;SET UP VARIABLE
	PUSH TP,1(A)	;AND BIND IT TO
	PUSH AP,$TUNBOU	;'UNASSIGNED
	PUSH AP,[-1]	
	AOS (P)
	PUSHJ P,SPECBIND	;BIND IT
	JRST NXTAUX
AUXIN:	CAMN B,$TLIST	;IS IT A LIST?
	SKIPN A,1(A)	;WHICH IS NON-NIL?
	JRST MPD	;NO ERROR
	HLLZ B,(A)	;GET TYPE OF CAR
	CAME B,$TATOM	;IS IT AN ATOM?
	JRST MPD	;NO -- ERROR
	PUSH TP,B	;SAVE UP
	PUSH TP,1(A)	;VARIABLE
	HRRZ A,(A)	;GET CDR
	JUMPE A,MPD	;CHECK NON-NIL
	PUSH AP,(A)	;EVAL CADR
	HLLZS (AP)
	PUSH AP,1(A)
	MCALL 1,EVAL
	PUSH AP,A
	PUSH AP,B
	AOS (P)
	PUSHJ P,SPECBIND	;BIND IT UP
NXTAUX:	HRRZ A,@1(TB)	;SET THE AUXLIST TO
	MOVEM A,1(TB)	;CDR OF THE AUXLIST
	JRST AUXLP
RUNIT:	SUB P,[1,,1]	;UNSCREW P
	HRRZ C,@3(AB)	;GET BODY OF FUNCTION
	JUMPE C,NOBODY	;IF NIL - ERROR
	MOVEM C,1(TB)	;PUT IT IN TEM
;FALL INTO IPROG

;IPROG RUNS THROUGH THE LIST OF EXPRESSIONS IN 0(TB) EVALUATING EACH
;IT RETURNS THE VALUE OF THE LAST EVALUATED IN A AND B.

IPROG:
	SKIPN C,1(TB)	;ARE WE DONE?
	JRST FINIS
	PUSH AP,(C)	;EVAL THE
	HLLZS (AP)	;CAR OF
	PUSH AP,1(C)	;THE BODY
	MCALL 1,EVAL
	HRRZ C,@1(TB)	;SET THE BODY
	MOVEM C,1(TB)	;TO CDR OF THE BODY
	JRST IPROG


AMAKE:
	AOS SPTIME
	PUSH SP,$TTIME
	PUSH SP,SPTIME
	PUSH AP,$TTIME
	PUSH AP,SPTIME
	PUSH AP,$TSKIP
	PUSH AP,SP
	MCALL 2,EVECTOR
	HRLZI A,TENV
	POPJ P,


EVLIST:	PUSH P,[0]	;NEED TEMP FOR COUNT
	PUSH TP,(AB)	;MAKE UP COPY OF ARG
	PUSH TP,1(AB)	
EVLST1:	INTGO
	SKIPN A,1(TB)	;ANY MORE ELEMENTS?
	JRST LSTUP	;NO
	HLLZ B,(A)	;YES -- GET NEXT GOODIE
	CAMN B,$TSEG
	MOVSI B,TLMNT
	PUSH AP,B
	PUSH AP,1(A)
	MCALL 1,EVAL
	HLRZ C,@1(TB)	;GET TYPE OF GOODIE
	CAIN C,TSEG	;IF A SEGMENT -- DO HAIR
	JRST LDOSEG
	AOS (P)	;COUNT NEW ELEMENT
	PUSH AP,A	;AND STACK IT
	PUSH AP,B
LNXTEL:	HRRZ A,@1(TB)	;SET LIST TO CDR OF LIST
	MOVEM A,1(TB)
	JRST EVLST1

LSTUP:	MOVSI A,TLIST	;GET A NIL
	MOVEI B,0
	JRST LCLUP	;GO CONS UP A RESULT

LDOSEG:	PUSH P,A	;SAVE UP A
	HLRZ A,A	;MOVE TYPE TO RIGHT HALF
	PUSHJ P,SAT	;GET STORAGE ALLOCATION TYPE
	CAIE A,2WORD	;LIST STRUCTURE?
	JRST LSPV	;NO -- TRY VECTOR
	POP P,A	;RESTRE TYPE FIELD
	HRRZ C,@1(TB)	;IS IT THE LAST GOODIE?
	JUMPN C,LSPL	;IF NOT -- SPREAD IT
LCLUP:	SOSGE (P)	;ANY MORE?
	JRST FINIS	;;NOPE
	PUSH AP,A	;PUSH ACCUMULATED LIST
	PUSH AP,B	
	MCALL 2,CONS	;CONS IT ONTO NEXT GOODIE
	JRST LCLUP

LSPV:	CAIE A,2NWORD	;IS IT A VECTOR?
	JRST ILLSEG	;NOPE -- LOSE
	POP P,BSTO (PVP)	;NEED TO USE AC B FOR POINTER

LSPVL:	INTGO	
	JUMPGE B,LSPD	;ANUY MORE ELEMENTS?
	AOS (P)	;BUMP COUNT
	PUSH AP,(B)	;STACK ELEMENT
	PUSH AP,1(B)	;
	ADD B,[2,,2]
	JRST LSPVL

LSPL:	MOVEM A,BSTO(PVP)
LSPLL:	INTGO
	JUMPE B,LSPD	;ANY MORE?
	PUSH AP,(B)	;STACK IT
	HLLZS (AP)
	PUSH AP,1(B)
	AOS (P)		;BUMP COUNT
	HRRZ B,(B)	;CDR LIST
	JRST LSPLL
	
LSPD:	SETZM BSTO (PVP)
	JRST LNXTEL

EVECT:	PUSH P,[0]	;NEED COUNT OF RESULTING ELEMENTS
	SKIPL C,1(AB)	;ANYTHING TO DO?
	JRST VUP	;NO -- DO IT
	PUSH TP,(AB)	;YES, NEED COPY OF ARG PTR
	PUSH TP,C

EVECT1:	INTGO
	JUMPGE C,VUP	;ANY MORE?
	MOVEM C,1(TB)	;CLOBBER IT AWAY
	HLLZ B,(C)	;GET TYPE OF NEXT GOODIE
	CAMN B,$TSEG	;SEGMENT CALL?
	MOVSI B,TLMNT	;YES -- FAKE OUT EVAL
	PUSH AP,B	;EVALUATE GOODIE
	PUSH AP,1(C)
	MCALL 1,EVAL
	MOVE C,1(TB)	;WAS THE GOODIE
	HLRZ D,(C)	;
	CAIN D,TSEG	;A SEGMENT CALL?
	JRST VDOSEG	;YES -- HACK IT
	AOS (P)			;NO-- COUNT NEW ELEMENT
	PUSH AP,A	;AND STACK IT
	PUSH AP,B
	ADD C,[2,,2]	;BUMP ARG POINTER
	JRST EVECT1	;NEXT

VUP:	HRRZI B,1(AP)	;GET AP
	MOVE A,(P)	;GET NUMARGS
	LSH A,1	;DOUBLE IT
	HRL A,A	;COPY INTO LEFT HALF
	SUB B,A	;MAKE POINTER
	JSR SAVCAL	;SAVE STATE
	MOVE A,[TENTRY,,EVECTOR"]
	MOVE AB,B
	PUSH TP,A
	PUSHJ TP,EVECTOR
	JSR RESCAL
	JRST FINIS

VDOSEG:	PUSH P,A	;SAVE TYPE
	HLRZ A,A	;GET SAT
	PUSHJ P,SAT
	CAIE A,2WORD	;LIST?
	JRST VSPV	;NO -- MAY BE A VECTOR

VSPL:	POP P,BSTO(PVP)	;WILL BE INTERRUPTED WITH GOODIES IN B
VSPLL:	INTGO
	JUMPE B,VSPD	;ANY MORE?
	PUSH AP,(B)	;PUSH IT
	HLLZS (AP)	;
	PUSH AP,1(B)
	AOS (P)		;BUMP COUNT
	HRRZ B,(B)	;CDR SEGMENT
	JRST VSPLL	;NEXT
VSPV:	CAIE A,2NWORD	;IS IT A VECTOR?
	JRST ILLSEG	;NOPE -- LOSEY LOSEY!
	POP P,BSTO(PVP)	;YES -- WILL NEED B AT INTERRUPT TIME
VSPVL:	INTGO	;CHECK INTERRUPTS
	JUMPGE B,VSPD	;ANY MORE?
	AOS (P)		;BUMP COUNT
	PUSH AP,(B)	;STACK IT
	PUSH AP,1(B)
	ADD B,[2,,2]	;REST THE SEGMENT VECTOR
	JRST VSPVL	;NEXT
VSPD:	SETZM BSTO(PVP)
	MOVE C,1(TB)
	ADD C,[2,,2]
	JRST EVECT1
;EVLTUP EVALUATES THE ARGUMENTS OF THE FORM IN 0(AB), SPREADS THEM OUT ON THE
;AP AND RETURNS THE TUPLE POINTER IN B WITH $TAB IN A. IT IS CALLED BY PUSHJ P,

EVLTUP:
	HRRZ A,@1(AB)	;GET CDR OF FORM -- ARGLIST
	PUSH TP,$TLIST	;SAVE THE ARGLIST ON
	PUSH TP,A	;THE TP
	PUSH P,[0]	;MAKE SLOT FOR ARGCNT
TUPLUP:
	SKIPN A,(TP)	;IS IT NIL?
	JRST MAKPTR	;YES -- DONE
	PUSH AP,(A)	;NO -- GET CAR OF THE
	HLLZS (AP)	;ARGLIST
	PUSH AP,1(A)
	MCALL 1,EVAL	;AND EVAL IT.
	PUSH AP,A	;SAVE THE RESULT IN
	PUSH AP,B	;THE GROWING TUPLE
	AOS (P)	;BUMP THE ARGCNT
	HRRZ A,@(TP)	;SET THE ARGLIST TO 
	MOVEM A,(TP)	;CDR OF THE ARGLIST
	JRST TUPLUP
MAKPTR:
	HRRZI B,1(AP)	;GET AP POINTER
	MOVE A,(P)	;GET #OF ARGS
	LSH A,1	;DOUBLE IT
	HRL A,A	;COPY IT INTO LEFT HALF
	SUB B,A	;MAKE POINTER
	MOVE A,ABSTO(PVP)	;GET TYPE POINTER
	SUB TP,[2,,2]	;FLUSH TEMPORARY
	SUB P,[1,,1]	;FLUSH COUNT
	POPJ P,

;APFUNARG APPLIES OBJECTS OF TYPE FUNARG

APFUNARG:
	HRRZ A,@3(AB)	;GET CDR OF FUNARG
	JUMPE A,FUNERR	;NON -- NIL
	HLLZ B,(A)	;GET TYPE OF CADR
	CAME B,$TLIST	;BETTR BE LIST
	JRST FUNERR
	PUSH TP,B	;SAVE IT UP
	PUSH TP,1(A)
	HRRZ A,(A)	;TAKE CDDR
	JUMPE A,FUNERR	;BETTER NOT BE NIL
	HLLZ B,(A)	;GET TYPE OF CADDR
	CAME B,$TLIST
	JRST FUNERR
	PUSH TP,B	;MAKE SLOT FOR CADDR
	PUSH TP,1(A)
	HRRZ A,(A)	;CHECK TO MAKE SURE NO MORE
	JUMPN A,FUNERR
	PUSH P,[0]	;NEED SLOT FOR COUNT
FUNLP:
	INTGO
	MOVE A,1(TB)	;HAVE WE RUN OUT
	SKIPN B,3(TB)
	JUMPE A,DOF	;YES -- GO DO IT
	JUMPE B,FUNERR	;WE LOSE!
	JUMPE A,FUNERR
 	AOS (P)	;BUMP COUNT
	HLLZ C,(A)	;GET FIRST VAR
	CAME C,$TATOM	;MAKE SURE IT IS ATOMIC
	JRST FUNERR
	PUSH TP,C	;SET IT UP
	PUSH TP,1(A)
	HLLZ C,(B)	;GET TYPE OF VALUE
	PUSH AP,C	;SET IT UP
	PUSH AP,1(B)
	HRRZ A,(A)	;CDR THE VARLIST
	MOVEM A,1(TB)
	HRRZ B,(B)	;CDR THE VALUELIST
	MOVEM B,3(TB)
	JRST FUNLP
DOF:
	PUSHJ P,SPECBIND	;BIND THEM
	SUB P,[1,,1]	;STRAIGHTEN OUT THE 
	SUB TP, [4,,4]	;PDLS
	MOVE A,3(AB)	;GET GOODIE
	HLLZ B,(A)
	MOVEM B,2(AB)	;SET THE FUNCTION TO ITS CAR
	MOVE B,1(A)	
	MOVEM B,3(AB)
	JRST IAPPLY	;AND APPLY IT

;ILOC RETURNS IN A AND B A LOCATIVE TO THE LOCAL VALUE OF THE IDENTIFIER PASSED TO IT
;IN A AND B.  IF THE IDENTIFIER IS UNBOUND IT RETURNS $TUNBOUND IN A AND 0 IN B,
;AND THE LOCATION OF THE ZERO SKIP PTR IN C.  IT IS CALLED BY PUSHJ P,ILOC.

ILOC:
	HLRZ C,(B)	;GET TYPE OF DATUM IN VALUE CELL
	CAIE C,TLOCI	;IS IT A LOCATIVE POINTER?
	JRST SCHSP	;NO  -- SEARCH THE BINDING STACK (SP).
	HRRZ C,2(B)	;GET PROCESS ID OF CURRENT OWNER OF CELL
	CAME C,PROCID+1 (PVP)	;IS IT THE CURRENT PROCESS?
	JRST SCHSP	;NO -- SEARCH THE SP.
	MOVE A,(B)	;YES -- GOBBLE THE LOCATIVE FROM THE VALUE CELL
	MOVE B,1(B)
	POPJ P,

SCHSP:
	MOVE C,SP	;GET SP POINTER
SLOOP:
	HLRZ D,-1(C)	;GET TYPE OF TOPMOST ITEM
	CAIN D,TSKIP	;IS IT A SKIP POINTER?
	JRST SKIP1	;YES
	CAIN D,TTIME	;IS IT A TIME
	JRST TIME1	;YES
	CAMN B,-4(C)	;HAVE WE FOUND THE IDENTIFIER'S HOME?
	JRST FOUND	;YES
	SUB C,[6,,6]	;NO SKIP THIS ITEM
	JRST SLOOP	;AND TRY AGAIN
SKIP1:
	SKIPN A,(C)	;ARE WE AT THE END?
	JRST NOTBOU	;YES -- UNBOUND
	MOVE C,A
	JRST SLOOP
TIME1:
	SUB C,[2,,2]	;SKIP OVER TIME KLUDGE
	JRST SLOOP	;TRY AGAIN

NOTBOU:	MOVSI A,TUNBOUND	;RETURN FAILURE MESSAGE
	MOVEI B,0
	POPJ P,

FOUND:
	MOVE D,B	;SAVE ATOM POINTER
	MOVSI A,TLOCI	;MAKE LOCATIVE POINTER
	MOVE B,C	;FOR THE HOME WE 
	SUB B,[3,,3]	;JUST FOUND
	HLRZ C,(D)	;GET TYPE IN VALUE CELL
	CAIN C,TLOCI	;IS IT A LOCATIVE?
	JRST LINK	;YES -- LINK IT
	CAIE C,TUNBOUND	;IS IT UNBOUND?
	POPJ P,	;NO -- DON'T MUNG IT AS IT HAS A GLOBAL VALUE
LINK:
	MOVEM A,(D)	;CLOBBER THE VALUE CELL
	MOVEM B,1(D)	;TO THE NEW LOCATIVE
	MOVE C,PROCID+1(PVP)	;GET  THE PROCESS ID
	HRRM C,2(D)	;SIGN THE VALUE CELL
	POPJ P,

;IVAL RETURNS IN A AND B THE LOCAL VALUE OF THE IDENTIFIER PASSED TO IT IN A AND B
;IF THE IDENTIFIER IS UNBOUND ITS VALUE IS $TUNBOUND IN A AND 0 IN B. IF
;IT IS UNASSIGNED ITS VALUE IS $TUNBOUND IN A AND -1 IN B.  CALL - PUSHJ P,IVAL

IVAL:
	PUSHJ P,ILOC	;GET LOCATIVE TO VALUE
	CAMN A,$TUNBOUND	;BOUND
	POPJ P,	;NO -- RETURN
	MOVE A,(B)	;GET THE TYPE OF THE VALUE
	MOVE B,1(B)	;GET DATUM
	POPJ P,

;SPECBIND  BINDS THE IDENTIFIERS ON THE TP TO THE MATCHING VALUES ON THE AP.
;-1(P) CONTAINS THE NUMBER OF SUCH PAIRS TO BE BOUND.  IT IS CALLED BY PUSHJ P,SPECBIND
;IT RETURNS AFTER POPPING OFF THE GOODIES ON TP AND AP BUT NOT THE ONE ON P.

SPECBIND:
BINDLP:
	INTGO	;CHECK PENDING INTERRUPTS
	SOSGE -1(P)	;ANY MORE PAIRS?
	POPJ P,	;NO -- DONE
	MOVE A,-1(TP	;GET THE ATOM TO BE BOUND
	MOVE B,(TP)
	PUSHJ P,ILOC	;GET LOCATIVE TO ITS VALUE
	PUSH SP,-1(TP)	;PUSH THE ATOM TO BE BOUND ON THE BINDING STACK
	PUSH SP,(TP)
	PUSH SP,-1(AP)	;PUSH ITS NEW VALUE
	MOVE C,SP	;(MAKE NEW LOCATIVE POINTER)
	PUSH SP,(AP)	;IN THE SECOND SLOT
	PUSH SP,A	;SAVE THE OLD LOCATIVE
	PUSH SP,B	;IN THE THIRD SLOT
	MOVE D,(TP)	;GET THE VALUE CELL
	HLRZ A,(D)	;GET ITS TYPE
	CAIE A,TLOCI	;IS IT LOCAL
	CAIN A,TUNBOUND	;OR UNBOUND?
	JRST CLBVC	;YES -- CLOBBER IT
	JRST NXTPR	;NO -- HAS GLOBAL DEFINITION
CLBVC:
	MOVSI E,TLOCI	;MAKE TYPE WORD
	MOVEM E,(D)	;CLOBBER VALUE CELL
	MOVEM C,1(D)	;TO NEW LOCATIVE
	MOVE C,PROCID+1 (PVP)	;GET CURRENT PROCESS NUMBER
	HRRM C,2(D)	;SIGN THE VALUE CELLî
NXTPR:
	SUB TP,[2,,2]	;POP OFF THE ATOM
	SUB AP,[2,,2]	;AND THE VALUE
	JRST BINDLP

;SPECSTORE RESTORES THE BINDING STACK  SP TO THE ENVIRONMENT POINTER IN 
;ACCUMULATOR E.  IT IS CALLED BY PUSHJ P,SPECSTORE.

SPECSTORE":
STLOOP:
	CAMN SP,E	;ARE WE DONE?
	POPJ P,
	HLRZ C,-1(SP)	;GET THE LAST GOODIE
	CAIE C,TTIME	;IS IT A CONTROL WORD?
	CAIN C,TSKIP
	JRST IGST
	MOVE C,-4(SP)	;NO -- GET ATOM POINTER
	HLRZ D,(C)	;GET TYPE OF VALUE CELL
	CAIE D,TLOCI	;IS IT A LOCATIVE?
	CAIN D,TUNBOUND	;OR UNBOUND?
	JRST SLINK	;YES -- LINK IT
	SUB SP,[6,,6]	;CAN'T LINK BECAUSE GLOBAL
	JRST STLOOP	;SO POP AND TRY AGAIN
SLINK:
	POP SP,1(C)	;CLOBBER VALUE CELL
	POP SP,(C)	;TO OLD CRUFT
	MOVE D,PROCID+1(PVP)	;AND SIGN IT
	HRRM C,2(D)	;WITH CURRENT PROCESS NAME
	SUB SP,[4,,4]	;FLUSH AND TRY MORE
	JRST STLOOP
IGST:
	SKIPN (SP)	;ARE WE ABOUT TO LOSE?
	.VALUE [ASCIZ /SPOVERPOP/]	;YES -- ERROR CONDITION
	SETZM (SP)	;INVALIDATE CONTROL WORD
	SUB SP,[2,,2]	;POP IT OFF
	JRST STLOOP

MFUNCTION REP,FSUBR,REPEAT
	JRST PROG
MFUNCTION PROG,FSUBR
	ENTRY 1
	HLRZ A,(AB)	;GET ARG TYPE
	CAIE A,TLIST	;IS IT A LIST?
	JRST WTYP	;WRONG TYPE
	SKIPN A,1(AB)	;GET AND CHECK ARGUMENT
	JRST ERRTFA	;NOT ENOUGH ARGS
	HLLZ B,(A)	;GET TYPE OF ARGL
	CAME B,$TLIST	;CHECK TYPE
	JRST WTYP	;BAD
	PUSH TP,B	;SAVE IT
	PUSH TP,1(A)
	PUSH P,[0]	;NEED SLOT FOR BINDCOUNT
PROGV:
	SKIPN A,1(TB)	;ANY MORE?
	JRST VDONE	;NO
	AOS (P)	;BUMP BINDCNT
	HLLZ B,(A)	;GET TYPE OF VARIABLE SPEC
	CAME B,$TATOM	;ATOMIC? -- UNASSIGNED VARIABLES
	JRST PROGIN	;NO-- INITIALIZED VARIABLE
	PUSH TP,B	;SET UP ATOM
	PUSH TP,1(A)	;FOR BINDING
	PUSH AP,$TUNBOUND	;TO THE UNASSIGNED VALUE
	PUSH AP,[-1]
	JRST NXPVG	;GET NEXT ONE
PROGIN:
	CAMN B,$TLIST	;IS IT A LIST?
	SKIPN A,1(A)	;NON-NIL?
	JRST MPD	;NO LOSE
	HLLZ B,(A)	;GET TYPE OF VAR
	CAME B,$TATOM	;MUST BE ATOMIC
	JRST MPD	;OTHERWISE -- ERROR
	PUSH TP,B	;SET UP ATOM
	PUSH TP,1(A)	;FOR BINDING
	HRRZ A,(A)	;GET CDR
	JUMPE B,MPD	;FOR INITIAL VALUE
	PUSH AP,(A)	;BIND IT TO THE
	HLLZS (AP)	;VALUE OF
	PUSH AP,1(A)	;THE CADR OF THE SPEC
	MCALL 1,EVAL
	PUSH AP,A
	PUSH AP,B
NXPVG:
	HRRZ A,@1(TB)	;SET THE VLIST 
	MOVEM A,1(TB)	;TO ITS CDR
	JRST PROGV
VDONE:	PUSHJ P,SPECBIND	;BIND VARIABLES
	SUB P,[1,,1]
	MOVE A,1(AB)	;GET PROG
	HRRZ B,(A)	;GET BODY OF PROG
	JUMPE B,NOBODY
	MOVEM B,1(TB)	;SAVE UP BODY IN 1(TB) FOR FINDING TAGS
	PUSH TP,$TLIST
	PUSH TP,B	;AND IN 3(TB) -- CURRENT RUNNING BODY
	PUSH TP,LPROG(PVP)	;SAVE UP LAST PROG POINTER
	PUSH TP,LPROG+1(PVP)	;IN 5(TB)
	MOVEM TB,LPROG+1(PVP)	;SET UP CURRENT PROG
	PUSH TP,$TSP	;SAVE THE STATES OF THE PDLS
	PUSH TP,SP	;IN 7(TB)
	PUSH TP,$TAP
	PUSH TP,AP	;11(TB)
	PUSH TP,$TPDL
	PUSH TP,P	;13(TB)
DOPROG:
	SKIPN C,3(TB)	;IS IT NIL?
	JRST ENDPROG	;YES -- DONE
	PUSH AP,(C)	;EVALUATE THE
	HLLZS (AP)
	PUSH AP,1(C)	;STATEMENT
	MCALL 1,EVAL	
	HRRZ C,@3(TB)	;SET THE BODY TO
	HRRZM C,3(TB)	;CDR OF THE BODY
	JRST DOPROG	;GO ON

MFUNCTION RETURN,SUBR
	ENTRY 1
	SKIPN LPROG+1(PVP)	;IS THIS RETURN LEGAL? (IN A PROG)
	JRST NXPRG	;NO -- ERROR
	MOVE B,(AP)
	MOVE A,-1(AP)	;FINISH ARG
	MOVE TB,LPROG+1(PVP)	;GET THE LAST EXECUTED PROG
FINPROG:
	MOVE C,5(TB)
	MOVEM C,LPROG+1(PVP)
	JRST FINIS

ENDPROG:
	HRRZ C,-2(TB)
	MOVE C,@-1(C)
	CAME C,MQUOTE REP,REPEAT
	JRST FINPROG
	SKIPN C,1(TB)
	JRST FINPROG
	MOVEM C,3(TB)
	JRST DOPROG
MFUNCTION AGAIN,SUBR
	ENTRY 0
	SKIPN C,LPROG+1(PVP)
	JRST NXPRG
	MOVE B,1(C)
	JRST FNDGO

MFUNCTION GO,SUBR
	ENTRY 1
	SKIPN C,LPROG+1(PVP)	;ARE WE IN A PROG?
	JRST NXTAG	;NO -- ERROR
	PUSH AP,(AB)
	PUSH AP,1(AB)
	PUSH AP,(C)
	PUSH AP,1(C)
	MCALL 2,MEMQ"	;DOES IT HAVE THIS TAG?
	JUMPE B,NXTAG	;NO -- ERROR
FNDGO:	MOVE TB,LPROG+1(PVP)	;FOUND IT
	MOVEM B,3(TB)
	MOVE AP,11(TB)	;RESTORE THE SAVED STATES OF THE PDLS
	MOVE P,13(TB)
	MOVE E,7(TB)
	PUSHJ P,SPECSTORE
	MOVE TP,[13,,13]
	ADD TP,TB
	JRST DOPROG

MFUNCTION COND,FSUBR
CLSLUP:	SKIPN B,1(AB)	;IS THE CLAUSELIST NIL?
	JRST IFALSE	;YES -- RETURN NIL
	HLRZ A,(B)	;NO -- GET TYPE OF CAR
	CAIE A,TLIST	;IS IT A LIST?
	JRST BADCLS	;
	MOVE A,1(B)	;YES -- GET CLAUSE
	JUMPE A,BADCLS
	PUSH AP,(A)	;EVALUATION OF
	HLLZS (AP)
	PUSH AP,1(A)	;THE PREDICATE
	MCALL 1,EVAL
	CAMN A,$TFALSE	;IF THE RESULT IS
	JRST NXTCLS	;FALSE TRY NEXT CLAUSE
	MOVE C,1(AB)	;IF NOT, GET
	MOVE C,1(C)	;THE CLAUSE
	PUSH TP,$TLIST	;EXECUTE ITS
	HRRZ C,(C)	;CDR
	PUSH TP,C	;IF NOTHING TO DO IPROG RETURNS THE
	JRST IPROG	;CONTENTS OF A AND B
NXTCLS:	HRRZ A,@1(AB)	;SET THE CLAUSLIST
	HRRZM A,1(AB)	;TO CDR OF THE CLAUSLIST
	JRST CLSLUP
	
IFALSE:
	MOVSI A,TFALSE	;RETURN FALSE
	MOVEI B,0
	JRST FINIS


;SETG IS USED TO SET THE GLOBAL VALUE OF ITS FIRST ARGUMENT,
;AN IDENTIFIER, TO THE VALUE OF ITS SECOND ARGUMENT.  ITS VALUE IS
; ITS SECOND ARGUMENT.

MFUNCTION SETG,SUBR
	ENTRY 2
	HLRZ C,(AB)	;GET TYPE OF FIRST ARGUMENT
	CAIE C,TATOM	;CHECK THAT IT IS AN ATOM
	JRST NONATM	;IF NOT -- ERROR
	MOVE C,1(AB)	;GET POINTER TO ATOM
	MOVE A,2(AB)	;GET SECOND ARGUMENT
	MOVE B,3(AB)	;INTO THE RETURN POSITION
	MOVEM A,(C)	;DEPOSIT INTO THE 
	MOVEM B,1(C)	;INDICATED VALUE CELL
	MOVE D,PROCID+1(PVP)	;GET PROCESS ID
	HRRM D,2(C)	;SIGN VALUE CELL
	JRST FINIS

;SET CLOBBERS THE LOCAL VALUE OF THE IDENTIFIER GIVEN BY ITS
;FIRST ARGUMENT TO THE SECOND ARG.  ITS VALUE IS ITS SECOND ARGUMENT.

MFUNCTION SET,SUBR
	ENTRY 2
	HLLZ A,(AB)	;GET TYPE OF FIRST
	CAME A,$TATOM	;ARGUMENT -- 
	JRST WTYP	;BETTER BE AN ATOM
	MOVE B,1(AB)	;GET PTR TO IT
	PUSHJ P,ILOC	;GET LOCATIVE TO VALUE
	CAMN A,$TUNBOUND	;BOUND?
	JRST BSET	;BIND IT
	MOVE C,B	;SAVE PTR
	POP AP,B	;GET SECOND ARG
	POP AP,A	;INTO RETURN VALUE
	MOVEM A,(C)	;CLOBBER IDENTIFIER
	MOVEM B,1(C)
	JRST FINIS
BSET:
	HLRE A,C	;GET # OF WORDS TO DOPE WORD
	MOVMS A	;GET ABS VALUE  OF IT
	MOVE B,A	;SAVE IT
	ADDI A,(C)	;GET POINTER TO DOPE WORD
	HLRZ A,(A)	;EXTRACT STACK LENGTH
	SUB A,B	;GET # OF FREE WORDS
	CAIGE A,8	;ARE THERE ENOUGH?
	JRST UNBOU	;NO!
	MOVSI A,TSKIP	;YES -- MAKE NEW
	MOVEM A,-7(C)	;STACK FENCE
	SETZM -6(C)
	POP AP,-2(C)	;GENERATE NEW BINDING
	POP AP,-3(C)
	POP AP,-4(C)
	POP AP,-5(C)
	MOVSI A,TUNBOUND
	MOVEM A,-1(C)
	MOVE A,-3(C)
	MOVE B,-2(C)
	JRST FINIS

MFUNCTION NOT,SUBR
	ENTRY 1
	HLRZ A,(AB)	; GET TYPE
	CAIE A,TFALSE	;IS IT FALSE?
	JRST IFALSE	;NO -- RETURN FALSE

TRUTH:
	MOVSI A,TATOM	;RETURN T (VERITAS) 
	MOVE B,MQUOTE T
	JRST FINIS

MFUNCTION ANDA,FSUBR,AND
	ENTRY 1
	HLRZ A,(AB)
	CAIE A,TLIST
	JRST WTYP	;IF ARG DOESN'T CHECK OUT
	SKIPN C,1(AB)	;IF NIL
	JRST TRUTH	;RETURN TRUTH
ANDLP:
	JUMPE C,FINIS	;ANY MORE ARGS?
	MOVEM C,1(AB)	;STORE CRUFT
	PUSH AP,(C)	;EVALUATE THE
	HLLZS (AP)	;FIRST REMAINING
	PUSH AP,1(C)	;ARGUMENT
	MCALL 1,EVAL
	CAMN A,$TFALSE	
	JRST FINIS	;IF FALSE -- RETURN
	HRRZ C,@1(AB)	;GET CDR OF ARGLIST
	JRST ANDLP

MFUNCTION OR,FSUBR
	ENTRY 1
	HLRZ A,(AB)
	CAIE A,TLIST	;CHECK OUT ARGUMENT
	JRST WTYP
	MOVE C,1(AB)	;PICK IT UP TO ENTER LOOP
ORLP:
	JUMPE C,IFALSE	;IF NO MORE OPTIONS -- FALSE
	MOVEM C,1(AB)	;CLOBBER IT AWAY
	PUSH AP,(C)	
	HLLZS (AP)
	PUSH AP,1(C)	;EVALUATE THE FIRST REMAINING
	MCALL 1,EVAL	;ARGUMENT
	CAME A,$TFALSE	;IF NON-FALSE RETURN
	JRST FINIS
	HRRZ C,@1(AB)	;IF FALSE -- TRY AGAIN
	JRST ORLP

MFUNCTION EXPR,FSUBR
	PUSH AP,$TATOM
	PUSH AP,MQUOTE EXPR
	HRLI AB,-4
	JRST CHTYPE"

MFUNCTION FUNARG,FSUBR
	ENTRY 1
	HLRZ A,(AB)	;GET TYPE OF ARG
	CAIE A,TLIST	;MAKE SURE IT IS A LIST
	JRST WTYP
	SKIPN A,1(AB)	;MAKE SURE IT ISN'T NIL
	JRST ERRTFA
	HLLZ B,(A)	;GET TYPE OF CAR
	PUSH AP,B
	PUSH AP,1(A)	;EVALUATE CAR
	MCALL 1,EVAL
	PUSH AP,A
	PUSH AP,B
	HRRZ A,@1(AB)	;GET CDR
	PUSH AP,$TLIST
	PUSH AP,A
	PUSH TP,$TAP
	PUSH TP,AP	;SAVE CURRENT AP
	PUSHJ P,EVLTUP	;EVALUATE THE FUNARG VARIABLES
	JSR SAVCAL	;SAVE STATE
	MOVE A,[TENTRY,,LIST"]
	MOVE AB,B
	PUSH TP,A
	PUSHJ TP,LIST	;MAKE UP LIST
	JSR RESCAL
	MOVE AP,1(TB)	;RESTORE AP
	SUB TP,[2,,2]	;AND TP
	PUSH AP,A	;SAVE THE RESULT
	PUSH AP,B
	MCALL 3,LIST
	HRLZI A,TFUNARG
	JRST FINIS

MFUNCTION FALSE,SUBR
	ENTRY
	JUMPGE AB,IFALSE
	HLRZ A,(AB)
	CAIE A,TLIST
	JRST WTYP
	MOVSI A,TFALSE
	MOVE B,1(AB)
	JRST FINIS
;ERROR COMMENTS FOR EVAL

UNBOU:	PUSH AP,$TATOM
	PUSH AP,MQUOTE UNBOUND-VARIABLE
	JRST ER1ARG

UNAS:	PUSH AP,$TATOM
	PUSH AP,MQUOTE UNASSIGNED-VARIABLE
	JRST ER1ARG

ERRTFA:	PUSH AP,$TATOM
	PUSH AP,MQUOTE TOO-FEW-ARGUMENTS-SUPPLIED
	JRST CALER1

ERRTMA:	PUSH AP,$TATOM
	PUSH AP,MQUOTE TOO-MANY-ARGUMENTS-SUPPLIED
	JRST CALER1

BADENV:
	PUSH AP,$TATOM
	PUSH AP,MQUOTE BAD-ENVIRONMENT
	JRST CALER1

FUNERR:
	PUSH AP,$TATOM
	PUSH AP,MQUOTE BAD-FUNARG
	JRST CALER1

WTYP:	PUSH AP,$TATOM
	PUSH AP,MQUOTE WRONG-TYPE
	JRST CALER1

MPD:	PUSH AP,$TATOM
	PUSH AP,MQUOTE MEANINGLESS-PARAMETER-DECLARATION
	JRST CALER1

NOBODY:	PUSH AP,$TATOM
	PUSH AP,MQUOTE HAS-EMPTY-BODY
	JRST CALER1

BADCLS:	PUSH AP,$TATOM
	PUSH AP,MQUOTE BAD-CLAUSE
	JRST CALER1

NXTAG:	PUSH AP,$TATOM
	PUSH AP,MQUOTE NON-EXISTENT-TAG
	JRST CALER1

NXPRG:	PUSH AP,$TATOM
	PUSH AP,MQUOTE NOT-IN-PROG
	JRST CALER1

NAPT:	PUSH AP,$TATOM
	PUSH AP,MQUOTE NON-APPLICABLE-TYPE
	JRST CALER1

NONEVT:	PUSH AP,$TATOM
	PUSH AP,MQUOTE NON-EVALUATEABLE-TYPE
	JRST CALER1


NONATM:	PUSH AP,$TATOM
	PUSH AP,MQUOTE NON-ATOMIC-ARGUMENT
	JRST CALER1


ILLSEG:	PUSH AP,$TATOM
	PUSH AP,MQUOTE ILLEGAL-SEGMENT
	JRST CALER1

ER1ARG":	PUSH AP,(AB)
	PUSH AP,1(AB)
	MOVEI A,2
	JRST CALER
CALER1":	MOVEI A,1
CALER":
	HRRZ C,-2(TB)
	PUSH AP,$TATOM
	PUSH AP,@-1(C)
	ADDI A,1
	JSR SAVCAL
	HRL A,A
	LSH A,1
	MOVEI AB,1(AP)
	SUB AB,A
	MOVE B,[TENTRY,,ERROR"]
	PUSH TP,B
	PUSHJ TP,ERROR"
	JSR RESCAL
	JRST FINIS


END
TURN FALSE
	MOVEI B,0
	JRST FINIS


;SETG IS USED TO SET THE GLOBAL VALUE OF ITS FIRST ARGUMENT,
;AN IDENTIFIER, TO THE VALUE OF ITS SECOND ARGUMENT.  ITS VALUE IS
; ITS SECOND ARGUMENT.

MFUNCTION SETG,SUBR
	ENTRY 2
	HLRZ C,(AB)	;GET TYPE OF FIRST ARGUMENT
	CAIE C,TATOM	;CHECK THAT IT IS AN ATOM
	JRST NONATM	;IF NOT -- ERROR
	MOVE C,1(AB)	;GET POINTER TO ATOM
	MOVE A,2(AB)	;GET SECOND ARGUMENT
	MOVE B,3(AB)	;INTO THE RETURN POSITION
	MOVEM A,(C)	;DEPOSIT INTO THE 
	MOVEM B,1(C)	;INDICATED VALUE CELL
	MOVE D,PROCID+1(PVP)	;GET PROCESS ID
	HRRM D,2(C)	;SIGN VALUE CELL
	JRST FINIS

;SET CLOBBERS THE LOCAL VALUE OF THE IDENTIFIER GIVEN BY ITS
;FIRST ARGUMENT TO THE SECOND ARG.  ITS VALUE IS ITS SECOND ARGUMENT.

MFUNCTION SET,SUBR
	ENTRY 2
	HLLZ A,(AB)	;GET TYPE OF FIRST
	CAME A,$TATOM	;ARGUMENT -- 
	JRST WTYP	;BETTER BE AN ATOM
	MOVE B,1(AB)	;GET PTR TO IT
	PUSHJ P,ILOC	;GET LOCATIVE TO VALUE
	CAMN A,$TUNBOUND	;BOUND?
	JRST BSET	;BIND IT
	MOVE C,B	;SAVE PTR
	POP AP,B	;GET SECOND ARG
	POP AP,A	;INTO RETURN VALUE
	MOVEM A,(C)	;CLOBBER IDENTIFIER
	MOVEM B,1(C)
	JRST FINIS
BSET:
	HLRE A,C	;GET # OF WORDS TO DOPE WORD
	MOVMS A	;GET ABS VALUE  OF IT
	MOVE B,A	;SAVE IT
	ADDI A,(C)	;GET POINTER TO DOPE WORD
	HLRZ A,(A)	;EXTRACT STACK LENGTH
	SUB A,B	;GET # OF FREE WORDS
	CAIGE A,8	;ARE THERE ENOUGH?
	JRST UNBOU	;NO!
	MOVSI A,TSKIP	;YES -- MAKE NEW
	MOVEM A,-7(C)	;STACK FENCE
	SETZM -6(C)
	POP AP,-2(C)	;GENERATE NEW BINDING
	POP AP,-3(C)
	POP AP,-4(C)
	POP AP,-5(C)
	MOVSI A,TUNBOUND
	MOVEM A,-1(C)
	MOVE A,-3(C)
	MOVE B,-2(C)
	JRST FINIS

MFUNCTION NOT,SUBR
	ENTRY 1
	HLRZ A,(AB)	; GET TYPE
	CAIE A,TFALSE	;IS IT FALSE?
	JRST IFALSE	;NO -- RETURN FALSE

TRUTH:
	MOVSI A,TATOM	;RETURN T (VERITAS) 
	MOVE B,MQUOTE T
	JRST FINIS

MFUNCTION ANDA,FSUBR,AND
	ENTRY 1
	HLRZ A,(AB)
	CAIE A,TLIST
	JRST WTYP	;IF ARG DOESN'T CHECK OUT
	SKIPN C,1(AB)	;IF NIL
	JRST TRUTH	;RETURN TRUTH
ANDLP:
	JUMPE C,FINIS	;ANY MORE ARGS?
	MOVEM C,1(AB)	;STORE CRUFT
	PUSH AP,(C)	;EVALUATE THE
	HLLZS (AP)	;FIRST REMAINING
	PUSH AP,1(C)	;ARGUMENT
	MCALL 1,EVAL
	CAMN A,$TFALSE	
	JRST FINIS	;IF FALSE -- RETURN
	HRRZ 