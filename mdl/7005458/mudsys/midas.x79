
TITLE MIDAS 46 RVSD 12 SEP 73 (JFH)

.MLOLIT==0
ITS==0
IFE ITS, .INSRT MUDSYS;STENEX >
TS==1
EANDS==0
IFN EANDS,PRINTC /"MIDASD"
/
KERNEL==0	;KERNEL ASSEMBLY MIDAS
IFN KERNEL,PRINTC /"MIDASK"
/
MACG==0		;MACRO GROWTH SWITCH
IMLAC==0	;IMLAC SWITCH
IFN IMLAC,PRINTC /"MIDASI"
/

SLOWLY==0
SLOW==TS&SLOWLY

IFN SLOW,PRINTC /"SMIDAS"
/

MACGRO==TS&MACG

PURFLG=1
IFE ITS,PURFLG==0
HPUR=2000
HIMPUR=400000
PURE=0
IMPURE=1

DEFINE INFORM A,B
IFN PURFLG,[	IF1,[PRINTX \A = B
\]]TERMIN


DEFINE PUR
	IFN PURFLG,[ HIMPUR=.
	IFE PURE, LOC HPUR
	PURE=1
	IMPURE=0]
	TERMIN

DEFINE IMPUR
	IFN PURFLG,[ HPUR=.
	IFE IMPURE, LOC HIMPUR
	IMPURE=1
	PURE=0]
	TERMIN




IFN PURFLG,[	DEFINE PAGMAP LOTOP,HITOP,MIDDLE
	RIM
	IF2,[%HALF==0
	WORD 0
%LO==<LOTOP+1777>/2000
%HI==<HITOP+1777>/2000
%MID==<MIDDLE+1777>/2000
IFSE MIDDLE,,%MID==128.
REPEAT %LO,HAFWRD 600000
IFG %MID-%LO,REPEAT %MID-%LO,HAFWRD 0
IFL %MID-%LO,%MID==%LO
IFG %HI-%MID,REPEAT %HI-%MID,HAFWRD 400000
IFL %HI-%MID,%HI==%MID
REPEAT 256.-%HI,HAFWRD 0
	.SLDR
	EXPUNGE %LO,%MID,%HI,%HALF
]
	TERMIN

	DEFINE HAFWRD X
IFE %HALF&(1),%HALF==%HALF&(777777)+X
IFN %HALF&(1),WORD %HALF,,X
%HALF==%HALF+(1)
	TERMIN
]





IF1,[
IFN EANDS,[.INSRT COM:ESDEFS >
SPXXXX=SP
CMXXXX=CM
STXXXX=ST
EXPUNGE CM SP ST
]]


IFE TS,1PASS
FOO==.

ERRUUO==01000,,0
TYPUUO==02000,,0
TYP2UU==03000,,0
TYP3UU==04000,,0
TYP4UU==05000,,0
TYP5UU==06000,,0
TYP6UU==07000,,0
TYP7UU==10000,,0

IFE ITS,TYPR==37000,,0

LOC 41
	JSR ERROR
IFN TS,JSR TSINT	;PDL OVERFLOW AND/OR OTHER TS PROBLEMS

A1PSW==TS	;FOR 1PASS END-OF-PROGRAM AUTO-RECSSEMBLY
TSSYMS==1	;.UAI, .UAO, .BAI, .BAO, .UII, .UIO, .BII, .BIO - EVER USE THEM?
LISTSW==0&TS	;LISTING FEATURE FGR MAINT PROGS
IFN TS,MAXIND==20	;MAXIMUM NUMBER IF INDIRECT DEVICE SPECIFICATIONS FOR .INSRT

LOC FOO


;INDICATOR REGISTER

;LEFT HALF
GLI==1	;SET ON " CLEARED EACH SYL
VAR==2	;SET ON '    "!    "   "
FLO==4	;SET ON .   "     "    "
DECP==10	;DECIMAL PREFER
UARI==20	;UP  AR INDICATOR
LSRET==40	;RETURN FROM <
MNSFLG==100	;SET IF LAST OP WAS MINUS
WRDF==400	;SET IF CURRENT WORD IS NOT NULL RETURNED BY GETWORD
NPRC==1000	;GTSYL
UNRCHF==2000	;GETSYL
MWRD==4000	;SET ON MULTIPLE WORD
NREP==10000	;NOT 1 PASSABLE
MWRD1==20000

;RIGHT HALF

FLD==1	;SET IF FLD NOT NULL
SYL==2	;SET IF L-N SEEN IN CURRENT SYL
LET==4	;SET IF LET SEEN IN CURRENT SYL
DEF==10	;SET IF CURRENT EXPR DEFINED
LLET==20	;SET IF LAST CHAR WAS LET

COM==40	;SET IF CURRENT QUAN IS COMMON
PERI==100	;SET IF PERIOD SEEN
EQLF==200
AIOWD==400	;CURRENT WORD CONTAINS IO INST
CONT==1000	;SET IF NOT OK TO END BLOCK
PSEUDF==4000	;SET IF ERROR COMMENTS WILL COME FROM PSEUDO
GMINF==20000	;SET IF UARI OR BAKARI HAS GOBBLED MINUS
OPFLD==200000	;SET IF OP SEEN IN CURRENT FIELD

;AC DEFS

FF"=0
P=1
I=2
AA=3
A=4
B=5
C=6
D=7
T=10
TT=11
SYM=12
LINK=13
F=14
CH1=15
CH2=16
TM=17	;SUPER TEMPORARY
IFN IMLAC,[
E=10
G=11
H=12
J=13
K=15
L=16
M=17
N=3
ARGP=3
]


;FF FLAGS NOT PUSHED
;LEFT HALF
INDEFF==200000	;SET IF LOC IS INDEF
PPSS"=400000	;ONE IF PUNCHING PASS MUST BE SIGN
SKILF==100000	;ONE IF SYM TO BE SEMI KILLED IN DDT
VOT"=40000	;ALL RCH S MUST GO THRU RCH
	; IE TYPCTL .NE. POPJ P,
PTPF==20000	;SET IF (TIME SHARING) OUTPUT DEVICE IS PTP
OUTF==10000	;ONE IF OUTPUT HAS OCCURED IN CURRENT MODE (USED BY TS NED LOGIC)


;RIGHT HALF FLAGS

BITF==400	;SET IF CURRENT SPEC IS 111
INVTF==4000
NLIKF==2000	;TEMP SUPPRESS ADR LINKING
PSS"=20000	;ONE ON PASS 2
NPSS"=40000	;ONE IF TWO PASS ASSEMBLY
GLOLOC==1000
IPSYMS==200000	;ONE IF SYM PUNCH DESIRED
FIRWD==400000	;ONE FOR FIRST WORD OF BLOCK
LOCF==100000	;ONE BETWEEN ABS LOC ASSIGN AND
	;FIRST BLOCK OUTPUT THEREAFTER
MRSW=200	;MACRO PROC TO RETURN TO .GO HACKER

MACRCH==10000	;ONE IF CHARS FROM MAC PROCESSOR
TTYRCH==100	;ONE IF CHARS FROM TTY (WINS OVER MACRCH)
GLOFFS==40

IFE TS,[.LIFS .NMAC"
MACL=.NMAC"
.ELDC
.LIFS -.NMAC"
MACL==2000*2	;LENGTH OF MACTBL
.ELDC
]IFN TS,IFN SLOW,MACL==600
IFN TS,IFE SLOW,MACL==6000

STRL==20	;LENGTH OF STRING STORAGE
DMDEFL==20	;MAX NO OF DMY ARGS IN DEFINE

DMYAGL==400	;MAX NO COMBINED DMYARGS ALL MACROS CURRENTLY
;EXPANDING OR PUSHED
MPDLL==300	;MAC PDL LENGTH
DSSIZ==40
MIDVRS"=.FNAM2
MAXGC==3	; NUMBER OF GCS PRIOR TO CORE GROWTH

	LOC HIMPUR

;VARIABLE STORAGE

FUNPDL:	0	;NON-ZERO, SIMULATING BACK BLT WITH POP
CDISP:	0	;CURRENT DISPATCH CODE
PPRIME:	0	;PUSH DOWN LIST MARKER  (GETFD)
GLSP1:	0	;POINTER TO BOT OF LINKAGE TABLE IN USE HIGH ADR
GLSP2:	0	;POINTER TO TOP OF LINKAGE TABLE IN USE LOW ADR
FORMAT:	0	;ACCUMULATES FORMAT WORD
FORPNR:	0	;POINTER INTO FORMAT WORD
WRD:	0	;ACCUMULATES VALUE OF WORD
WRDRLC:	0	;RELOC OF WRD
T1:	0	;TEMP
T2:	0	;TEMP
LSYL:	0
LSYLR:	0
FLDCNT:	0	;NUMBER OF FIELDS PUSHED DOWN IN CURRENT WORD
PBITS1:	0	;CURRENT CODE BITS
PBITS2:	0	;NO OF SPECS LEFT IN CURRENT WORD
PBITS4:	0	;POINTER TO WHERE CURRENT CODE BITS WILL GO
LIMBO:	0	;TEMP STG FOR INVERT WORD
CLOC:	0	;PUNCHING LOC
CRLOC:	0	;PUNCHING RELOC
OFLOC:	0	;OFSET VAL
OFRLOC:	0	;OFSET RELOC
;VAL OF PT=CLOC+OFLOC,CRLOC+OFLOC
OPT1:	0	;POINTER TO BKBUF TAB
OPTT1:	0	;TEMP
OPTT2:	0	;TEMP
ESL1:	0	;LEVEL OF CURRENT SYM
ESL2:	0	;3RD WORD CURRENT SYM
SADR:	0	;SYM TAB ADR
CONTRL":	0	;FLAG REG FOR IO CONTROL ETC
LEV:	0	;CURRENT BEGIN END LEVEL
CDATBC:	0	;CURRENT DATA BLOCK CODE TYPE
VARCNT:	0	;NO OF VAR IN CURRENT VAR AREA SO FAR
SCKSUM:	0	;CKSUM FOR SIMPLE BLOCK FORMAT
IFN MACGRO,NGCS:	MAXGC
IFN A1PSW,[
PRGC":	-1	;ONE LESS THAN # TIMES END HAS BEEN INCOUNTERED
OUTN1":	-1	;.GE. 0 => OUTPTT HAS OCCURED IN OTHER THAN 1PASS MODE (NOT INITIALIZED)
OUTC":	-1	;.GE. 0 => OUTPUT HAS OCCURED DURING CURRENT ASSEMBLY
]

LIMBO1:	0	;UNRCH TEMP
SCONT1:	0	;SAVE CONT FLAG
PBCON:	0	;POINTER TO CURRENT PLACE IN CONST AREA INFO TABLE
PLIM:	0	;TOP OF CURRENT AREA
PBCONL:	0	;POINTER TO ABS TOP OF CONST AREA INFO TABLE
CSQZ:	0	;CONST SQUOZE COUNTER
LINKL:	0	;SAVE LIMIT OF GLOTB  GETWRD
CONCNT:	0	;NUMBER OF TIMES CONSTANTS CAN APPEAR (DECREMENTED BY CONSTA)
CONGOL:	0	;POINTER TO CONST GLO TAB
NREPC:	0	;COUNT OF CONST UNREPS
SYLOC:	0	;VAL OF LAST TAG
SYSYM:	0	;LAST TAG
SYLOC1:	0	;VALUE OF NEXT TO LAST TAG
SYSYM1:	0	;NEXT TO LAST TAG
CONDEP:	0	;DEPTH IN CONSTANTS (0 TOP LEVEL)
VARPNT:	0	;POINTER TO CURRENT PLACE IN VARTAB
VARCNR:	0	;NO OF TIMES VARIABLES MAY APPEAR
GETCNR:	105	;PRIORITY OF UNARY OPS
ISYMF:	-1	;-1IF ISYMS HAVE NOT BEEN SPREAD
PRGNM:	0	;PROG NAME
GLOBT:	0	;GLSP1 PUSHED DOWN ONE LEVEL AT GETFLD
BITP:	0	;BYTE PNTR TO CODE BITS IN CURRENT (RELOC) BLOCK
SCINST:	0	;STRING COND INST
SCONDF:	0	;STRING COND FLAG, -1 STRINGS IDENTICAL, 0 DIFFERENT
BLOCKF:	0	;BLOCK OR LOC
AIRPT2:	0
LDCCC:	0	;DEPTH IN LOADTIME CONDS
PARBIT:	0	;0 OR 4 FOR : OR =
STGSW:	0	;NON ZERO GIVES ERROR PRINT ON STORAGE WORDS
VCLOC:	0	;TEM FOR VARIAB
STARTA:	0	;STARTING ADDRESS FOR SBLK, RIM
BYTM:	0	;-1 FOR IN BYTE MODE
BYTMC:	0	;COUNT CORRESP WITH BYTMP
BYTMP:	0	;POINTER TO BYTE DESC TABLE
BYTMT:	0	;TOTAL ACTIVE BYTES IN TABLE
CPGN:	0	;CURRENT PAGE # IN INPUT FILE
CLNN:	0	;CURRENT LINE # IN INPUT FILE

;FORMAT OF BYTE DESC TABLE
;SEVEN BIT BYTES
;1.7=0 ASSEMBLE =1 BLANK
;1.1 - 1.6 NUMBER OF BITS
BYTWP:	440000,,BYTW	;POINTER TO BYTW IDPB TO DEPOSIT CURRENT BYTE
BYTW:	0	;WORD BEING ASSEMBLED IN BYTE MODE
NBYTS:	0	;NUMBER BYTES ASSEMBLED
ISAV:	0	;I FROM FLD AT AGETFLD

IFN LISTSW,[
PNTBP:	0	;POINTER TO LISTING LINE BUFFER
LISTF:	0	;-1 IF LISTING ON
PNTSW:	0	;-1 IF LAST CHR CR OR LF
LISTBF:	BLOCK 50.
LISTAD:	0	;ADDRESS OR -1 NONE 3.1 RELOC
LISTWD:	0	;WORD
LSTRLC:	0	;RELOCATION
LISTPF:	0	;-1 OTHERS CONTAIN SOMETHING
LISTBC:	0	;BREAK CHR CR LF OR FF OR -1 IF NONE SINCE LAST PNTR
LISTTM:	0	;TEMP AT AEND
]

;CONTROL FLAGS
;LEFT HALF
TRIV==400000	;1 IF OUT FORM IS FOR TRIVIAL LOADER (RIM OR RIM1)
;RIGHT HALF
ARIM==2	;IF ONE OUT FOR IS RIM
ARIM1==4	;IF ONE OUT FORM IS RIM1
SBLKS==10	;IF ONE OUT FORM IS SIMPLE BLOCKS
ARIM10==20	;PDP-10 RIM

;LINK TABLE FLAGS

ACF==40000
HFWDF==100000
SWAPF==200000
MINF==20000	;NEGATIVE OF GLOBAL

BSIZE==37	;PREFERRED SIZE BLOCK  MAX SIZE-3
LPDL"==500	;LENGTH OF PDL
IFE TS,[.LIFS GMOBY"
MOBY=GMOBY"
.ELDC
.LIFS -GMOBY"
MOBY==0
.ELDC
]

IFN TS,MOBY==1

IFN TS,[IFN MOBY,LCONTB==500*5	;LENGTH OF CONSTANTS TABLE
IFE MOBY,LCONTB==500
]
IFE TS,[.LIFE MOBY
LCONTB==500
.ELDC
.LIFN MOBY
LCONTB==500*5
.ELDC
]
LCNGLO==200*3	;LENGTH OF CONST GLO TAB
NCONS==10
NVARS==10

IFE TS,[.LOP IDIV LCONTB 18.
.LCN18==.LVAL1"
]

IFN TS,.LCN18==LCONTB/18.


LBRKT=="[
RBRKT=="]

IFE TS,[.LIFS .NSYMS"
SMK=.NSYMS"
.ELDC
.LIFS -.NSYMS"
SMK=3177*2	;MAX NO ENTRIES IN SYM TAB
.ELDC
]IFN TS,IFN MOBY,SMK=6177*2
IFN TS,IFE MOBY,SMK==2177*2



IFN ITS,TYPR=(77000)

;CODE BIT DEFS
CMMN==0	;COMMON
PSUDO==40000	;PSEUDO OR MACRO
SYMC==100000	;SYM
LCUDF==140000	;LOCAL UNDEF
DEFLVR==200000	;DEF LOC VAR
UDEFLV==240000	;UNDEF LOC VAR
DEFGVR==300000	;DEF GLO VAR
UDEFGV==340000	;UNDEF GLO VAR
GLOETY==400000	;GLO ENTRY
GLOEXT==440000	;GLO EXIT

;LOADER BLOCK TYPES LINK
LLDCM==1	;LOADER COMMAND BLOCK
LABS==2	;ABSOLUTE
LREL==3	;RELOCATABLE
LPRGN==4	;PROG NAME
LLIB==5	;LIBRARY BLOCK
LCOMLOD==6	;LOAD INTO COMMON
LGPA==7	;GLOBAL PRARMETER ASSIGN
LDDSYM==10	;LOCAL SYMS
LTCP==11	;LOAD TIME COND ON PRESENCE
ELTCB==12	;END LOAD TIME COND
LDHLFK==13	;HALF KILL SYMBOLS (SET BY LOADER)
LPTRMN==14	; TERMINATE FOR PROG
LENTRY==15	;ENTRY BLOCK, NOTE 14 IS RESERVED FOR LIBRARY CREATOR
LEXTERN==16	;EXTERN BLOCK
LTCN==17	;LOAD TIME CONDITIONAL ON NEED
;LOADER COMMANDS
;IN ADR OF LDCMD BLK
LCJMP==1	;JUMP
LCGLO==2	;GLOBAL LOC ASSIGN
LCCMST==3	;SET COMMON BENCHMARK
LCEGLO==4	;END OF GLOBAL BLOCK
LDCV==5	;LOAD TIME COND ON VALUE
LDOFS==6	;LOADER SET GLOBAL OFFSET
LD.OP==7	;LOADER .OP
LDROFS==10	;RESET GLOBAL OFFSET

;LOADER CODEBITS SECOND SPEC AFTER 7
CDEF==0	;DEF
CCOMN==1	;COMMON REL
CLGLO==2	;LOC-GLO REC
CLIBQ==3	;LIBREQ
CRDF==4	;GLO REDEF
CRPT==5	;REPEAT GLOBAL VALUE
CDEFPT==6	;DEFINE SYM AS $.

;3RDWRD SYM TAB BITS

3REL==600000	;RELOC BITS
3RLR==200000	;R(RH)
3RLL==400000	;R(LH)
3RLNK==100000	;R(LINK)
3INI==40000	;INITIAL SYM
3LLV==4000	;LL MUST INSERT VAL
3VP==20000	;VALUE PUNCHED
3VCNT==1000	;USED IN CONSTANT
3SKILL==10000	;SEMI KILL IN DDT
3VAS2==2000	;VAR SEEN ON PSS TWO WITH '
3MAS==400	;MULTIPLE SYMS SAME NAME AT DIFFERENT LEVELS (I.E. KEEP LOOKING
		;AT SYMBOL LOOKUP

;CONST TAB BITS
CGBAL==100000	;CONST AREA GLOBAL
CTRL==200000	;R(CONST AREA)
CTDEF==400000	;CONST AREA DEF

;MACRO PROC FLAGS

SCEND==200	;SCAN END
GENF==400	;GENERATED ARGS
LNSCN==1000	;ACTIVATE LINE SCAN ON LAST LINE
ALNSCN==2000	;LINE SCAN ACTIVE
;400000 SAYS COMMAS DO NOT BREAK ARG
RDRPTF==4000	;REPEAT
LCRIND=10000	;CR SEEN AFTER ]
;MAC PDL STRUCTURE IRP,IRPC,IRPS
;FIRST ENTRY STANDARD RETURN==USED FOR .IRPCNT
;SECOND ENTRY  4.9==1 IF IRPC 4.8 = 1 IF AIRPS
;REST L H NO GROUPS
;RH POINTER TO BODY
;LH(WD1)=DMYAGT POINTER  RH==RETURN AIRP4

	;MACRO PROCESSOR INFO,VARIABLES,AND PARAMETER ASSIGNS.

TOPP:	0	;TOP OF CURRENT DUMMY ARG TABLE PTS TO FREE REG
BBASE:	0	;BOT  " "    "        "    "
PTAB:	(341000+CH1)MACTAB	;BYTE TABLE
	(241000+CH1)MACTAB
	(141000+CH1)MACTAB
	(41000+CH1)MACTAB
	(341000+CH1)MACTAB+1

PTAB1:	341000,,MACTAB
	241000,,MACTAB
	141000,,MACTAB
	41000,,MACTAB

MACP:	0	;MAC PDL POINTER
FREEPT:	0	;MACRO STG PNTR
GENSM:	0	;GENERATED SYM COUNT
STRCNT:	0	;NO OF CHARS IN S S
STRPNT:	0	;POINTER TO S S
CPTRB:	-1	;CPTR IN BYTE PNTR FORM OR -1 IF NOT COMPUTED
RDWRDP:	DSTG
MACNM:	0	;NAME OF MACRO BEING DEFINED
DCNT:	0	;USED BY WRQOTE CURRENT DEPTH IN DEFINE-IRP-TERMIN LEVEL 
PRCALP:	PRCAL
DMYTOP:	0
PRSTG:
PRSCND:	0	;CHARACTER ADDRESS OF CURRENT LOCATION IN FIRST STRING OF IFSE,IFSN WHILE COMPARING WITH SECOND
PRSCN1:	0	;CHAR ADDR BEG OF FIRST STRING IFSE, IFSN
PRREPT:	0	;CHAR ADR BEG OF BODY OF REPT
PRIRP:	0	;CHAR ADR BEG OF IRP BODY
PRDEF:	0	;CHAR ADR BEG OF MACRO BEING DEFINED
CPTR:	0	;CURRENT POINTER FOR MACRO,IRP,REPEAT ETC CHAR ADR ONE BEFORE NEXT CHR TO BE READ
PRCAL:	REPEAT 10,0
EPRSTT:
SYMSTR:	0	;PNTR TO CHAIN OF MACRO PNTRS IN SYM TABLE (DURING GC)
REDPT:	0
COFST:	0
SVF:	0
FREPTS:	0
GCENDF:	0
REDPTB:	0
FREPTB:	0
FRPTBS:	0
MDEPTH:	0	;DEPTH IN MACRO (NOT IRP OR REPEAT) EXPANSIONS
PUTCNT:	0
AIRPT:	0	;TYPE OF IRP (400000) IRPC (200000) IRPS 0 IRP
IRPCR:	0	;COUNT OF A,B,[LIST] GROUPS IN IRP IRPC IRPS
GCHI:	0
A.QOT2:	0
AIRPT1:	0
CRPTCT:	0	;COUNT THROUGH CURRENT REPEAT
CIRPCT:	-1	;COUNT THOUGH CURRENT IRP

DEFINE BYB A,B,C
ZZZ=C
ZZ=43
REPEAT 35.,[IFGE ZZZ,[ ZZZ=ZZZ_1
	ZZ=ZZ-1
]]
A,[ZZ*10000+100,,B
TERMIN


DEFINE 3GET A,B
	HRRZ TM,B
	ROT TM,-2
	IFE SLOW,HLLZ A,3RDWRD(TM)
	IFN SLOW,HLLZ A,@3RDWRD
	SKIPGE TM
	IFE SLOW,HRLZ A,3RDWRD(TM)
	IFN SLOW,HRLZ A,@3RDWRD
	TERMIN

DEFINE 3GET1 A,B
	IFE SLOW,MOVEI TM,-ST(B)
	IFN SLOW,[MOVE TM,B
	SUB TM,SYMPTR
	]
	ROT TM,-2
	IFE SLOW,HLLZ A,3RDWRD(TM)
	IFN SLOW,HLLZ A,@3RDWRD
	SKIPGE TM
	IFE SLOW,HRLZ A,3RDWRD(TM)
	IFN SLOW,HRLZ A,@3RDWRD
	TERMIN

DEFINE 3PUT A,B
	HRRZ TM,B
	ROT TM,-2
	SKIPGE TM
	IFE SLOW,HLRM A,3RDWRD(TM)
	IFN SLOW,HLRM A,@3RDWRD
	SKIPL TM
	IFE SLOW,HLLM A,3RDWRD(TM)
	IFN SLOW,HLLM A,@3RDWRD
	TERMIN

DEFINE 3PUT1 A,B
	IFE SLOW,MOVEI TM,-ST(B)
	IFN SLOW,[MOVE TM,B
	SUB TM,SYMPTR
	]
	ROT TM,-2
	SKIPGE TM
	IFE SLOW,HLRM A,3RDWRD(TM)
	IFN SLOW,HLRM A,@3RDWRD
	SKIPL TM
	IFE SLOW,HLLM A,3RDWRD(TM)
	IFN SLOW,HLLM A,@3RDWRD
	TERMIN

	PUR


IFN SLOW,[


;SUBROUTINES TO SEE IF POINTER POINTS TO SYMTAB OR CONST TAB

CNSTCH:	CAIL T,PCNTB	;SKIPS IF NOT POINING TO CONSTANTS
	CAILE T,CONGLO
	AOS (P)	;CAUSE SKIP
	POPJ P,

CONSTC:	PUSH P,T	;OPPOSITE OF ABOVE
	MOVEI T,(TM)
	PUSHJ P,CNSTCH
	AOS -1(P)
	POP P,T
	POPJ P,

]
LNKTZ:	TDZA C,C
LNKTC1:	MOVE T,GLSP2
LINKTC:	CAML T,GLSP1
	POPJ P,
	SKIPL 1(T)
	XORM B,1(T)
	SKIPL 1(T)
	IORM C,1(T)
	AOJA T,LINKTC
	IMPUR


HFWDAD:	HRRM A,.+1	;ADD A TO B
	HRRI A,(B)
	MOVSS B
	HLRM A,.+1
	HRLI A,(B)
	POPJ P,

	PUR


SGTSY:	PUSH P,I
	PUSH P,AA
	PUSH P,A
	PUSH P,B
	PUSHJ P,(LINK)
SGTSY1:

PERIOD:	TLO I,DECP
	TROE I,PERI
	TRO I,LET
	ADD SYM,%.SQ(D)
	TRO I,SYL
	SOJGE D,RRL2
	AOJA D,RRL2

GETFLD:	PUSH P,GLSP2
	PUSH P,GLSP2
	PUSH P,PPRIME
	PUSH P,GLOBT
	MOVE A,GLSP1
	MOVEM A,GLOBT
	MOVEM P,PPRIME
	TRZ I,FLD+OPFLD
GETFD1:	TLNE I,MWRD
	JRST .+3
	PUSHJ P,GETSYL
	TRNE I,LET
	PUSHJ P,GETVAL
GETFD6:	MOVE C,CDISP
	TLNE C,DFLD
	JRST (C)
GETFD:	MOVEI TT,0	;NO DISP MEANS FD TERM
	TRNE I,SYL
	TRO I,FLD
	JSP LINK,GETFD2
	SUB P,[(4)4
	POP P,GLOBT
	POP P,PPRIME
	SUB P,[(2)2
	POPJ P,
GETFDA:	TRO I,FLD+OPFLD
	TRNN I,SYL
	AOS TT,GETCNR	;UNARY
GETFD2:	CAMN P,PPRIME
	JRST GETFD3	;TOP OF LIST
	HLRZ T,(P)	;GET LEVEL
	CAMLE TT,T	;COMPARE TO CURRENT
	JRST GETFD3	;WAIT UNTIL LATER
	HRRZ T,(P)
	JRST (T)	;GO DO IT NOW
GETFD4:	SUB P,[(4)4
	JRST GETFD2
GETFD3:	PUSH P,GLSP1
	PUSH P,B
	PUSH P,A
	HRL C,TT
	PUSH P,C
	JRST (LINK)


PLS:	MOVEI C,PLS1
	MOVEI TT,10
	JSP LINK,GETFDA
	JRST GETFD1
MINUS1:	MOVNS A
	EQVI B,0
	ADD B,[1000000
	HRRI B,1(B)
	MOVE T,-3(P)
	PUSH P,B
	HRLZI B,MINF
	PUSH P,C
	PUSHJ P,LNKTZ
	POP P,C
	POP P,B
PLS1:	ADD A,-1(P)
	PUSH P,A
	MOVE A,-3(P)
	PUSHJ P,HFWDAD
	MOVE B,A
	POP P,A
	JRST GETFD4


MINUS:	MOVEI C,MINUS1
	JRST PLS+1

MULTP:	MOVEI C,MULTP1
	MOVEI TT,20
CBAK2:	JSP LINK,GETFDA
	JRST GETFD1

MULTP1:	JUMPE B,MULTP3
	SKIPE -2(P)
	JRST MULTP4
	MOVE T,-1(P)
	JRST .+3
MULTP3:	MOVE T,A
	MOVE B,-2(P)
	MOVE D,-3(P)
	CAME D,-7(P)
	JRST GMUL1
	CAME D,GLSP1
	JRST GMUL2
GMUL4:	HRLZ D,B
	IMUL D,T
	HLRM D,B
	HLLZ D,B
	IMUL D,T
	HLLM D,B
	IMUL A,-1(P)
	JRST GETFD4
MULTP4:	TYPUUO (SIXBIT /IRL/)	;ILLEGAL RELOCATION * /
	JRST GETFD4

DIVID:	MOVEI C, DIVID1
	JRST MULTP+1

GMUL1:	TLNE FF,PPSS
	CAMN D,GLSP1
	SKIPA CH1,A
	TYPUUO (SIXBIT /IMY/)	;ILLEGAL MPY
	SKIPA D,-7(P)
GMUL2:	MOVE CH1,-1(P)
GMUL3:	CAML D,GLSP1
	JRST GMUL4
	SKIPGE 1(D)
	AOJA D,GMUL3
	JUMPE CH1,GMUL5
	LDB CH2,[(221200+D)1
	SKIPN CH2
	MOVEI CH2,1
	IMUL CH2,CH1
	DPB CH2,[(221200+D)1
	AOJA D,GMUL3


GMUL5:	CLEARM 1(D)
	AOJA D,GMUL3

DIVID1:	JUMPN B,MULTP4
	SKIPE -2(P)
	JRST MULTP4
	EXCH A,-1(P)
	IDIV A,-1(P)
	MOVEI B,0
	JUMPGE FF,GETFD4
	MOVE D,-7(P)
	CAME D,GLSP1
	TYPUUO (SIXBIT /IDV/)
	JRST GETFD4

IORF:	MOVEI C, IORF1
LOGIC3:	MOVEI TT, 30
LOGIC:	JRST CBAK2


XORF:	MOVEI C,XORF1
	TRNN I,SYL	;IF OP ABOUT TO BE UNARY
	MOVNI A,1	;THEN TURN IT INTO LOGICAL NOT
	JRST  LOGIC3
ANDF:	MOVEI C,ANDF1
	MOVEI TT,40
	JRST  LOGIC
XORF1:	MOVEI D,(XOR A,(P))
	JRST LOGIC1
IORF1:	MOVEI D,(IOR A,(P))
	JRST LOGIC1
ANDF1:	MOVEI D,(AND A,(P))
LOGIC1:	HRLM D,LOGIC2
	JUMPN B,MULTP4
	SKIPE -2(P)
	JRST  MULTP4
	JRST LOGIC2

	IMPUR

LOGIC2:	,-1
	JRST GETFD4

	PUR




CBAKAR:	MOVEI C,CBAK1
	MOVEI TT,100
	JRST CBAK2

CBAK1:	JUMPN B,MULTP4
	MOVE T,A
	MOVE A,-1(P)
	LSH A,(T)
	JRST GETFD4

SEMIC:	PUSHJ P,RCH
	CAIE A,15
	JRST .-2
RRU3:	PUSH P,[RRL2A]
	JRST RRU1


RBRAK:	SKIPN CONDEP
	JRST RRL2
	JRST RR10

;COMPUTE CPTR IN BYTE PNTR FORM
BCOMP:	SKIPE CH1,CPTR	;GET CPTR, IF NOT ZERO
	SOS CH1	;THEN DECREMENT ONE (FOR ILDB)
	IDIVI CH1,4	;DIVIDE FOR QUOTIENT AND REMAINDER (WDS AND CHR POS IN WD)
	MOVE CH2,PTAB1(CH2)	;GET BYTE PNTR FROM TABLE
	ADD CH2,CH1	;ADD TO ADDRESS PART
	MOVEM CH2,CPTRB	;SAVE AWAY
	POPJ P,

RRU:	SKIPA A,LIMBO1
RRLM2:	PUSHJ P,MRCH2C
RRU1:	XCT RRR1
	JRST RRL1B
	SKIPG CPTRB
	PUSHJ P,BCOMP
	JRST RRLM4

RR4A:	AOS CPGN
	CLEARM CLNN
	AOS CLNN
	JRST RR4

GETSYL:	TDZA I,[(UARI+NPRC+DECP)PERI
GTSL1:	TDZ I,[(DECP)PERI
	CLEARB SYM,NUMTAB
	MOVE AA,[(NUMTAB)NUMTAB+1
	AOSN NTCLF
	BLT AA,NUMTAB+10
	MOVEI D,6
	TDZ I,[(FLO+VAR+GLI+LSRET)LET+SYL
RRL2:	PUSHJ P,RR
RRL2A:	MOVEM A,LIMBO1
	TRNE FF,MACRCH
	JRST RR4
	CAIN A,12
	AOS CLNN
	CAIN A,14
	JRST RR4A
RR4:	HRRZ A,GDTAB(A)
	MOVE C,DTB-40(A)
	MOVEM C,CDISP
RR8:	TLNE C,DSYL
	JRST (C)
RR10:	TRNN I,SYL
	JRST CABPOP
	TRNE I,LET
	POPJ P,
	CAMN SYM,[SQUOZE 0,.
	JRST PT1



RR5:	TLNN I,NPRC
	PUSHJ P,NUMSL
RR6:	TLNN I,FLO
	JRST RR9
	MOVE A,B	;IF FLOATING HIGH IN AA,LOW IN A,EXP IN B
	ADDI A,306	;201+105
	ADDI AA,200	;HIGHEST ORDER TO BE FLUSHED
	JUMPGE AA,.+3
	LSH AA,-1
	AOS A
	EXCH A,AA
	ASHC AA,-10
	SKIPE AA
	TYP2UU (SIXBIT /EPO/);EXPONENT OVERFLOW
RR9:	TLZ I,GLI+VAR
	JRST CLBPOP

;SUBROUTINE TO FLUSH LOWER CASE ASCII  TO UPPER CASE

LOWRCS:	CAIG A,172	;IF GREATER THAN Z
	CAIG A,140	;OR LESS THAN A
	POPJ P,	;DONT CAHNGE
	SUBI A,40	;OTHERWISE MAKE LOWER CASE TO UPPER
	POPJ P,





ATSGN:	MOVSI A,20
	IORM A,WRD
	TRO I,SYL
	JRST RRL2
	JRST RRL1

	IMPUR


RRL1":	PUSHJ P,RCH 	;OR ILDB A,UREDP"
RRL1B:	PUSHJ P,LOWRCS	;UP LOWER CASE
	XCT GDTAB(A)
	TROA I,LET+SYL+LLET	;LETTERS
	TRO I,SYL+LLET	;NUMBERS RETURN
RRL1A:	SOJGE D,RRL1
	AOJA D,RRL1

RR:	TLZE I,UNRCHF
	JRST RRU
RRR1":	TRNN FF,MACRCH"	;OR JFCL IF IN DISPLAY OUTPUT
	JRST RRL1

RRLM1A:	SKIPG CPTRB
	PUSHJ P,BCOMP
RRLM1:	ILDB A,CPTRB
	AOS CPTR
	TRZE A,200
	JRST RRLM2
RRLM4:	PUSHJ P,LOWRCS	;MAKE LOWER CASE GOODIES
	XCT GDTAB(A)
	TROA I,LET+SYL+LLET
	TRO I,SYL+LLET
RRLM4A:	SOJGE D,RRLM1
	AOJA D,RRLM1

	PUR



UT141:	PUSHJ P,INCHR3"
	JRST RRU1

MAKNUM:	SETOM NTCLF
	MOVEI AA,2
	SKIPGE HIGHPT(AA)
	JRST MAKNM3
	MOVE T,LOWPT(AA)
	MUL T,ARADIX(AA)
	MOVEM TT,CH1
	MOVE TT,HIGHPT(AA)
	ADD CH1,A
	TLZE CH1,400000
	AOS T
	JFCL 17,.+1
	IMUL TT,ARADIX(AA)
	ADD TT,T
	JFCL 10,MAKNM2
	TLNE I,FLO
	SOS NUMTAB(AA)
	MOVEM TT,HIGHPT(AA)
	MOVEM CH1,LOWPT(AA)
MAKNM4:	SOJGE AA,MAKNUM+2
	POPJ P,
MAKNM2:	MOVSI B,400000
	IORM B,HIGHPT(AA)
MAKNM3:	TLNN I,FLO
	AOS NUMTAB(AA)
	JRST MAKNM4

	IMPUR
NUMTAB:	0
	0
	0
HIGHPT:	0
	0
	0
LOWPT:	0
	0
	0
ARADIX:	10	;CURRENT RADIX
	12
	10

NTCLF:	0
	PUR

NUMSL:	CLEARB TT,D
	PUSHJ P,RDXSEL
	MOVE AA,HIGHPT(D)
	MOVE B,LOWPT(D)
	MOVE T,NUMTAB(D)
	TLNN I,FLO
	JRST FIX
	TLZ AA,400000
NUMC1:	JUMPN AA,.+2
	JUMPE B,FIX-1
	JUMPL T,NUMSL1
	JUMPE T,NUMSL2
NUMSL5:	MUL B,ARADIX(D)
	MUL AA,ARADIX(D)
	ADD A,B
	TLZE A,400000
	ADDI AA,1
	MOVE B,C
NUMSL3:	JUMPE AA,NUMSL4
	ASHC A,-1
	ASH A,1
	ASHC AA,-1
	AOJA TT,NUMSL3
NUMSL4:	MOVE AA,A
	SOJG T,NUMSL5

NUMSL2:	SKIPA A,B
NUMSL7:	ASHC AA,1
	TLNN AA,200000
	SOJA TT,NUMSL7
	SKIPA B,TT
PT1:	TRO I,LET
	POPJ P,

	TLZ I,FLO
FIX:	LSHC A,45
	LSHC AA,-1
	JUMPE AA,.+2
	TYP2UU (SIXBIT /XSG/)	;SIG CHECK
	POPJ P,

RDXS1:	MOVEI D,0
RDXSEL:	TLNE I,DECP
	MOVEI D,1
	TLNE I,VAR
	MOVEI D,2
	TLNE I,GLI
	MOVEI D,0
	POPJ P,

NUMSL8:	LSH B,1
	ASH AA,1
	TLZE B,400000
	TRO AA,1
NUMSL1:	TLNN AA,200000
	SOJA TT,NUMSL8
	IDIV AA,ARADIX(D)
	ADD B,A
	MOVEI A,0
	TLZE B,400000
	MOVEI A,1
	DIV A,ARADIX(D)
	MOVE B,A
	AOJL T,NUMSL1
	JRST NUMSL7+1

UPARR:	TRON I,SYL
	JRST UPCTRC
	TRNE I,LET
	TYP2UU (SIXBIT /ILF/)	;ILF
	PUSHJ P,NUMSL
	TLO I,UARI
	PUSHJ P,UA3
	TLZ I,UARI
	MOVE TT,B
	MOVE B,A
	PUSHJ P,RDXS1
	PUSHJ P,NUMC1	;T EXP HIGH IN AA LOW IN B TT BIN EXP
	TLNE I,FLO
	JRST RR7
	ASHC AA,(B)
	JUMPE AA,.+2
	TYP2UU (SIXBIT /XSG/)	;SIG CHECK
	MOVEI B,0
RR7:	MOVE C,CDISP
	TLO I,NPRC
	JRST RR8


UA3:	JSP LINK,SGTSY	;PUSH I,AA,A,B
	PUSHJ P,RCH
	CAIN A,"-
	TROA I,GMINF
	TLO I,UNRCHF
	PUSHJ P,RCH
	CAIN A,"<
	JRST UAR1
	TLO I,UNRCHF
	PUSHJ P,GTSL1
	TRNN I,LET
	TLNE I,FLO
	TYP2UU (SIXBIT /ILF/)	;ILF
UAR2:	TRZN I,GMINF
	SKIPA T,A
	MOVN T,A
	MOVEI TT,SGTSY1
	JSP LINK,POPLIS
	POPJ P,

UAR1:	TLO I,LSRET
	PUSHJ P,LSSTH
	PUSH P,A
	PUSHJ P,RCH
	CAIN A,"!
	JRST .-2
	HLRZ T,GDTAB(A)
	CAIE A,".
	CAIE T,(POPJ P,)
	TYP2UU (SIXBIT /ILF/)
	HRRZ A,GDTAB(A)
	MOVE A,DTB-40(A)
	MOVEM A,CDISP
	POP P,A
	JUMPN B,RLCERR	;RELOC ERR
	JRST UAR2

BAKAR:	TLNE I,UARI
	JRST RR5
	TRNE I,SYL
	TRNE I,LET
	JRST BAK1
	CAMN SYM,[SQUOZE 0,.
	JRST BAK1
	TLZN I,NPRC
	PUSHJ P,NUMSL
	PUSHJ P,UA3
	ADD B,T
	ASHC AA,(B)
	LSH A,1
	LSHC AA,-1
	CLEARB B,AA
	TLZ I,FLO
	POPJ P,


BAK1:	MOVE TT,[(DFLD)CBAKAR
	MOVEM TT,CDISP
	JRST RR10

DQUOTE:	TRON I,SYL
	JRST DQ1A
	TLO I,GLI
	JRST RRL2
UPCTRC:	PUSHJ P,RCH
	TRZA A,100
DQ1A:	PUSHJ P,RCH
DQ1:	PUSH P,A
	PUSHJ P,GETSYL
	TROE I,SYL
	TYP2UU (SIXBIT /ILF/)
	JRST POPAJ

SQUOTE:	TRON I,SYL
	JRST .+3
	TLO I,VAR
	JRST RRL2
	PUSHJ P,RCH
	SUBI A,40
	TRNN A,100
	JUMPGE A,DQ1
	TYP2UU (SIXBIT /N6B/)
	JRST DQ1


RR2:	SUBI A,60
	TRNE I,LET
	JRST RR2A
	TRZE I,PERI
	TLO I,FLO
	PUSHJ P,MAKNUM
RR2A:	XCT NSQTB(A)
	JRST 1(CH2)

NSQTB:	IRPC Q,,0123456789
	ADD SYM,%!Q!SQ(D)
TERMIN


SAVWD1:	PUSH P,LSYL
	PUSH P,LSYLR

SAVWLD:	PUSH P,FORMAT
	PUSH P,FORPNR
	PUSH P,FLDCNT
	PUSH P,GLSP2
	PUSH P,I
	PUSH P,WRD
	PUSH P,WRDRLC
	PUSH P,SYM
	PUSHJ P,(LINK)
SAVL1:
POPLIS:	POP P,F
	CAIE TT,(F)
	ERRUUO (SIXBIT /IAE/)	;IAE

POPLS1:	HLRZ F,-2(TT)
	CAIE F,(PUSH P,)
	JRST (LINK)
	POP P,@-2(TT)
	SOJA TT,POPLS1



USVWLD:	POP P,SYM
	HRRZS SYM
	CAIE SYM,SAVL1
	ERRUUO (SIXBIT /IAE/)
	POP P,SYM
	POP P,WRDRLC
	POP P,WRD
	TDZ I,[-1-(WRDF)
	IOR I,(P)
	POP P,1(P)
	POP P,GLSP2
	POP P,FLDCNT
	POP P,FORPNR
	POP P,FORMAT
	JRST (LINK)

PUSHEM:	PUSH P,A	;BBASE  CPTR
	PUSH P,F	;FLAGS LIMBO1 RETN
	MOVE F,MACP
	HRR A,CPTR
	HRL A,BBASE
	PUSH F,A
	HRL B,LIMBO1
	TLZE I,UNRCHF
IFE LISTSW,	TLO B,400000
IFN LISTSW,[
	JRST LSTUN1
LSTUN2:
]	TROE FF,MACRCH
	TLO B,200000
	TRZE FF,TTYRCH	;IF CHARS COMING FROM TTY,
	TLO B,100000	;THEN DOCUMENT SAME
	PUSH F,B
	MOVE A,[JRST MRCH
	MOVEM A,GETCHR
	JRST PSHM1

POPEM:	PUSH P,A
	PUSH P,F
	MOVE F,MACP
	POP F,A
	TLZ I,UNRCHF
	TLZE A,400000
IFE LISTSW,	TLO I,UNRCHF
IFN LISTSW,[
	JRST LSTUN3
LSTUN4:
]	TRZ FF,MACRCH\TTYRCH
	TLZE A,200000
	TRO FF,MACRCH
IFE TS,	HLRZM A,LIMBO1
IFN TS,[
	TLZE A,100000
	TRO FF,TTYRCH
	HLRZM A,LIMBO1
	TRNN FF,TTYRCH
	JRST POPEM2
	MOVE A,[PUSHJ P,RCHA]
	MOVEM A,GETCHR
	JRST POPEM1
POPEM2:]
	MOVE A,[PUSHJ P,RPA"]
	TRNN FF,MACRCH
	MOVEM A,GETCHR
POPEM1:	POP F,B
	HRRM B,CPTR
PSHM1:	SETOM CPTRB
	MOVEM F,MACP
POPFAJ:	POP P,F
POPAJ:	POP P,A
	POPJ P,

IFN LISTSW,[
LSTUN1:	AOSN PNTSW
	JRST LSTUN5	;LAST WAS BREAK CHR
	REPEAT 4,IBP PNTBP
	SOSA PNTBP
LSTUN5:	SETOM LISTBC
	TLO B,400000
	JRST LSTUN2

LSTUN3:	TLO I,UNRCHF
	MOVSS A
	IDPB A,PNTBP
	MOVSS A
	JRST LSTUN4
]


	IMPUR

ARCH":
RCH:	TLZE I,UNRCHF
	JRST RCH1
GETCHR:	PUSHJ P,RPA"	;OR JRST MRCH
	JRST AFTGCH

	PUR

AFTGCH:	CAIN A,12
	AOS CLNN
	CAIN A,14
	JRST RCH3
RCH4:
RCH2:	MOVEM A,LIMBO1
IFN LISTSW,[
	AOSN PNTSW
	PUSHJ P,PNTR
	CAIN A,14
	JRST RCHL1
	CAIE A,15
	CAIN A,12
	JRST RCHL1
	IDPB A,PNTBP
]
TYPCTL":	POPJ P,	;MUNGED WHEN OUT OF TS

IFN LISTSW,[
RCHL1:	MOVEM A,LISTBC
	SETOM PNTSW
	JRST TYPCTL
]

RCH3:	CLEARM CLNN
	AOS CLNN
	AOS CPGN
	JRST RCH4

RCH1:	MOVE A,LIMBO1
IFN LISTSW,[
	CAIN A,14
	JRST RCHL1
	CAIE A,15
	CAIN A,12
	JRST RCHL1
]	POPJ P,

MRCHR:	POP P,B
MRCH:	SKIPG CPTRB
	PUSHJ P,BCOMP
MRCH2B:	ILDB A,CPTRB
	AOS CPTR
	TRZN A,200
	JRST RCH2
MRCH2C:	PUSH P,B
	CAIN A,176
	JRST MRCHR
	CAIE A,177
	CAIN A,175
	JRST MRCH1
	MOVE B,A
	ADD B,BBASE
	MOVE A,(B)
	MOVEI B,RCHSAV
	PUSHJ P,PUSHEM
	HRRM A,CPTR
	MOVE A,TOPP
	MOVEM A,BBASE
	JRST MRCHR

MRCH1:	MOVE B,MACP
	POPJ B,	;RETURN AT END OF STRING EXPANSION

RCHSV1:	SOS MDEPTH
	PUSH P,A
	MOVE B,TOPP
RCHSV3:	CAMG B,BBASE
	JRST RCHSV2
	HLRZ A,-1(B)
	ADD A,-1(B)
	MOVEI A,1(A)
	CAME A,FREEPT
	JRST RCHSV2
	HRRZ A,-1(B)
	MOVEM A,FREEPT
	SOJA B,RCHSV3
RCHSV2:	POP P,A


RCHSAV:	MOVE B,BBASE
	MOVEM B,TOPP
	PUSHJ P,POPEM
	HLRM B,BBASE
REPT6:	TRZE FF,MRSW
	POPJ P,	;RETURN TO .GO
	POP P,B
	JRST RCH

REDINC:	MOVE CH1,A	;GET IN B THE CHAR WHOSE ADDR IS IN A, INCREMENT A
	IDIVI CH1,4
	LDB B,PTAB(CH2)
	AOJA A,CPOPJ

RDWRDA:	PUSHJ P,ADDTR1
	PUSHJ P,RCH	;ENTRY FROM STRING COND, AREPEAT ETC
	TLNE LINK,ALNSCN
	JRST RDWR3	;DONT QUIT UNTIL CR OR LF
	CAIN A,LBRKT
	JRST RDWR1	;IF FIRST CHAR LBRACK, READ UNTIL MATCHING RBRACK
	CAIN A,"\	;IF FIRST CHAR \
	JUMPGE LINK,RDWR6	;AND NORM ARG READ, THEN PROCESS FIELD
RDWR3:	CAIE A,15	;ON CR
	CAIN A,12	;OR LF
	JRST RDWR2C	;EXIT
	CAIN A,";	;ON SEMI
	JUMPGE LINK,RDWR2D	;ON NORMAL SCAN CAUSE SEMI TO BE REINPUT AND RET
REPT5:	CAIN A,",	;IF COMMA
	JUMPGE LINK,RDWR2	;END THIS ARG
RDWR5:	PUSHJ P,PUTREL	;NOTA, DEPOSIT THIS CHR
	PUSHJ P,RCH	;GET NEXT CHR
	JRST RDWR3	;AND LOOP BACK,NOT CHECKING FOR [ OR \

RDWR2D:	TLO I,UNRCHF
	JRST RDWR2C

RDWR7:	MOVE A,LIMBO1
	CAIE A,15
	CAIN A,12
RDWR2C:	TLO LINK,SCEND	;SCAN ENDED
RDWR2:	MOVE T,A	;RET LAST CHR IN T
	TLNE LINK,RDRPTF	;ON REPEAT,
	POPJ P,	;THATS ALL RETURN
RDWR2A:	HRL A,PUTCNT	;SAVE COUNT OF LENGTH THIS ARG IN CHRS
	HRR A,RDWRDP
	HLLM A,-1(A)
STPWR:	MOVEI A,375	;WRITE STOP CODE

PUTREL:	MOVE CH1,FREEPT	;STORE CHR IN A IN MACRO TABLE
	IDIVI CH1,4
	DPB A,PTAB(CH2)
	AOS A,FREEPT
	AOS PUTCNT
	IFE SLOW\MACGRO,[CAIGE A,+<MACL-2>*4
	POPJ P,
]
	IFN SLOW\MACGRO,[CAMGE A,MMAXMC
	POPJ P,
	]
	MOVEM A,GCHI


GC:	MOVEM 17,GCSV+15
	MOVE 17,[(2)GCSV
	BLT 17,GCSV+14

IFN MACGRO,[	;ONLY IF MACRO GROWTH
	SOSE NGCS	;NUMBER OF GCS DOWN
	JRST NOCORM	;DONT GOBBLE CORE

IFN ITS,[
	.SUSET [.RMEMT,,A]	; GOBBLE MEMTOP
	ASH A,-10.	;TO BLOCKS
	.CORE 1(A)	; BUMP CORE
	JRST NOCORM
]

	MOVEI A,MAXGC	;NUMBER OF GCS
	MOVEM A,NGCS
	MOVEI A,4*MACGR
	ADDM A,MAXMAC
	ADDM A,MMAXMC

NOCORM:]

	CLEARB T,GCENDF
	MOVEI A,3
	MOVEM A,REDPT
	MOVEM A,FREEPT
	SETOM CPTRB
	MOVE A,[(141000)MACTAB
	MOVEM A,FREPTB
	MOVEM A,REDPTB

IFE SLOW,SYMMG:	MOVSI A,-SMK
IFN SLOW,SYMMG:	MOVS A,MSMK
	IFE SLOW,LDB B,[(400400)ST(A)
	IFN SLOW,LDB B,[400400,,@STA]
	CAIN B,PSUDO_-14.
	JRST SYMMG1
SYMMG2:	AOS A
	AOBJN A,SYMMG+1
	MOVEM T,SYMSTR

MSTG:	MOVE C,REDPT
	MOVE TT,FREEPT
	MOVEM TT,FREPTS
	MOVE TT,FREPTB
	MOVEM TT,FRPTBS
	PUSHJ P,RDTRNS
	MOVE TT,B
MSTG1:	CAML LINK,GCHI
	JRST GCEND
	CAIN B,375
	JRST MSTG2
MSTG1B:	PUSHJ P,RDTRNS
	JRST MSTG1

ADDTR1:	CLEARM PUTCNT
ADDTRN:	MOVE A,FREEPT
ADDTR2:	MOVEM A,@RDWRDP
	AOS A,RDWRDP
	CAIL A,DSTG+DSSIZ
	ERRUUO (SIXBIT /TMA/)
CRDWR7:	POPJ P,RDWR7

RDWR6:	SOS RDWRDP
	PUSH P,LINK
	PUSHJ P,AGETFD
	TYPUUO (SIXBIT/USM/)
	POP P,LINK
	MOVE CH1,A
	PUSHJ P,ADDTR1
	PUSH P,CRDWR7
RDWR6A:	LSHC CH1,-35.
	LSH CH2,-1
	DIV CH1,ARADIX
	HRLM CH2,(P)
	JUMPE CH1,.+2
	PUSHJ P,RDWR6A
	HLRZ A,(P)
	ADDI A,60
	JRST PUTREL


RDWR1:	TDZA C,C
RDWR1A:	PUSHJ P,PUTREL
	PUSHJ P,RCHCNT
	JRST RDWR1A

	TLNE LINK,RDRPTF
	JRST RDWR2

RDWR2B:	PUSHJ P,RCH
	TLO I,UNRCHF
	CAIE A,15
	CAIN A,12
	TLO LINK,SCEND+LCRIND
	JRST RDWR2



PUT1:	IDIVI CH1,4
	DPB A,PTAB(CH2)
	POPJ P,


RDTRNS:	ILDB B,REDPTB
	IDPB B,FREPTB
	AOS LINK,REDPT
	AOS A,FREEPT
IFE SLOW\MACGRO,[	CAILE A,<MACL-2>*4-10
	ERRUUO (SIXBIT /MCE/)
	POPJ P,
]
;THIS CODE FOR FAST MIDAS WITH MACRO GROWTH

IFN MACGRO,[
	CAMG A,MAXMAC	;ANY ROOM?
	POPJ P,	;YES

	PUSH P,A
MACCUP:	PUSHJ P,EXTCOR	;EXTEND CORE
	MOVEI A,4*MACGR	; UPDATE POINTERS
	ADDM A,MAXMAC
	ADDM A,MMAXMC
	POP P,A
	POPJ P,

]
;THIS CODE FOR SLOW MIDAS, GROWS MACRO STORAGE
MACGR==2000-1	;AMOUNT OF GROWTH

IFN SLOW,[CAMG A,MAXMAC	;HAVE WE EXCEEDED
	POPJ P,	;NO, QUIT
	PUSH P,B	;SAVE SOME ACS
	PUSH P,A
	MOVEI B,MACGR	;UPDATE LINKED GOODIES
	MOVE A,SYMSTR	;BEGINS LIST
	JUMPE A,UPDON	;QUIT IF END OF LIST
UPDGO:	HRRZ TM,(A)	;GET CONTENTS
	JUMPE TM,UPDON	;ZERO, FINISH
	ADDM B,(A)	;ADD IN RELOCATION
	MOVEI A,(TM)	;STEP THROUGH
	JRST UPDGO
UPDON:	ADDM B,SYMSTR	;UPDATE FIRST POINTER

;HERE TO UPDATE CONSTANT GLOBAL TABLE AND GLOBAL TABLE

	MOVE A,[-20,,GLOTB]	;AOBJN POINTER TO GLOBAL TABLE
GLOLOP:	HRRZ TM,(A)	;GET ADR
	JUMPE TM,GLOLO1	;IF ZERO, IGNORE
	PUSHJ P,CONSTC	;CHECK FOR UPDATAGE
	ADDM B,(A)	;ELSE UPDATE IT
GLOLO1:	AOBJN A,GLOLOP

	MOVE A,[-LCNGLO,,CONGLO]	;NOW CONST GLOBAL TAB
CONLOP:	HRRZ TM,(A)	;GET ADR
	JUMPE TM,.+2
	ADDM B,(A)	;UPDATE
	AOBJP A,.+2
	AOBJN A,CONLOP

	PUSHJ P,EXTCOR	;EXPAND CORE
	ADDI A,MACGR	;A CONTAINS LAST WORD
	HRR B,SYMPTR	;GET POINTER TO MAIN SYMBOL TABLE
	ADDI B,MACGR	;POINTS TO WHERE SYMBOL TABLE IS GOING
	HRL B,SYMPTR	;FORM BLT POINTER
	HRRM B,SYMPTR	;SAVE NEW SYMBOL TABLE LOC
	PUSHJ P,UPDSPT;UPDATE ALL OTHER SYMBOL TABLE POINTERS
	PUSHJ P,BTB	;GO DO BACKWARD BLT
	MOVEI B,4*MACGR	;UPDATE MACRO TABLE LENGTH
	ADDM B,MAXMAC
	ADDM B,MMAXMC	;AND OTHER COUNT
	MOVEI B,MACGR	;UPDATE LOCATION OF 3RDWORD TABLE
	ADDM B,3RDWRD
	POP P,B
	POP P,A
	POPJ P,
]



IFE SLOW,SYMMG1:	HRRZ B,ST+1(A)
IFN SLOW,SYMMG1:	HRRZ B,ST1A@	
	CAIE B,MACCL
	JRST SYMMG2
	IFE SLOW,HRRM T,ST+1(A)
	IFN SLOW,HRRM T,@ST1A
	IFE SLOW,MOVEI T,ST+1(A)
	IFN SLOW,MOVEI T,@ST1A
	JRST SYMMG2

GCEND:	SETOM GCENDF
MSTG2:	CLEARM SVF
	MOVE D,REDPT
	SUB D,FREEPT
	MOVEM D,COFST
	MOVE B,REDPT
	CAIE TT,374
	JRST MSTG3	;NOT A MACRO
	MOVE T,SYMSTR
	JUMPE T,MSTG3
MSTG5:	HLRZ TT,(T)
	CAML TT,C
	CAML TT,B
	JRST MSTG4
	SETOM SVF
	SUB TT,COFST
	HRLM TT,(T)
MSTG4:	HRRZ T,(T)
	JUMPN T,MSTG5

MSTG3:	MOVE T,TOPP
	MOVEI CH1,DMYAGT
	PUSHJ P,MSCN
	HRRZ T,MACP
	HRROI CH1,MACPDL
	PUSHJ P,MSCN
	HRRZ T,PRCALP
	AOS T
	MOVEI CH1,PRSTG
	PUSHJ P,MSCN
	HRRZ T,RDWRDP
	MOVEI CH1,DSTG
	PUSHJ P,MSCN


MSTG13:	SKIPGE GCENDF
	JRST GCEND1
	MOVE TT,FREPTS
	SKIPL SVF
	MOVEM TT,FREEPT
	MOVE TT,FRPTBS
	SKIPL SVF
	MOVEM TT,FREPTB
	JRST MSTG
GCEND1:
USYMG:	MOVE T,SYMSTR
	MOVEI A,MACCL
	JUMPE T,USYMG1
	HRRZ TT,(T)
	HRRM A,(T)
	MOVE T,TT
	JRST USYMG+2

USYMG1:	MOVS 17,[(2)GCSV
	BLT 17,17
	POPJ P,

MSCN:	CAIG T,(CH1)
	POPJ P,
	HRRZ TT,-1(T)
	CAML TT,C
	CAML TT,B
	JRST MSCN1
	SUB TT,COFST
	HRRM TT,-1(T)
	SETOM SVF
MSCN1:	SKIPGE CH1
	SOS T
	SOJA T,MSCN

MACCL:	MOVSI 17,-14	;MACRO EXPANSION TIME
	PUSH P,2(17)
	AOBJN 17,.-1
	AOS PRCALP
	AOS MDEPTH
	PUSH P,RDWRDP
	MOVEI LINK,0
	HLRZ A,B
	PUSHJ P,REDINC
	CAIE B,374
	ERRUUO (SIXBIT /IAE/)
	PUSHJ P,REDINC
	SKIPN B
	TLO I,UNRCHF	;SAVE CHR FOLLOWING MACRO W/NO ARGUEMENTS
	MOVEM A,@PRCALP
	MOVE A,LIMBO1
	CAIE A,15
	CAIN A,12
	JRST MAC2A
	JUMPE B,MAC4
	TRZE B,200
	TLO LINK,LNSCN
MAC3:	PUSH P,B
	TLNE LINK,LNSCN
	SOJE B,MAC3A
MAC3B:	PUSHJ P,RDWRDA
	POP P,B
	TLNE LINK,SCEND
	SOJA B,MAC2
	SOJG B,MAC3
MAC4:	MOVE A,@PRCALP
MAC1:	PUSHJ P,REDINC
	MOVEM A,@PRCALP
	JUMPE B,MAC1A
MAC5:	PUSH P,B
	TLNE LINK,SCEND
	PUSHJ P,GENSYM
	TLNN LINK,SCEND
	PUSHJ P,RDWRDAî	POP P,B
	SOJG B,MAC5

MAC1A:	MOVEI B,RCHSV1
	PUSHJ P,PUSHEM
	MOVE A,@PRCALP
	MOVEM A,CPTR
	POP P,A
	PUSHJ P,DMYTRN
	SOS PRCALP
MACC2:	MOVSI 17,-13(P)
	HRRI 17,2
	BLT 17,15
MACC1:	SUB P,[(14)14
MACCR:	POP P,A
	HRRZS A
	CAIE A,GETFD6
	ERRUUO (SIXBIT /IAE/)
	JRST GETFD1

MAC2A:	TLO LINK,SCEND
	TRZ B,200
MAC2:	SOJL B,MAC4
	MOVEI A,0
	PUSHJ P,ADDTR2
	JRST MAC2

MAC3A:	TLO LINK,ALNSCN+400000
	JRST MAC3B

GENSYM:	PUSHJ P,ADDTR1
	MOVEI A,5
	MOVEM A,SCKSUM
	î	MOVEI A,"G
	PUSHJ P,PUTREL
	PUSH P,[RDWR2A
	AOS A,GENSM
	IDIVI A,10
	HRLM B,(P)
	SOSLE SCKSUM
	PUSHJ P,.-3
	HLRZ A,(P)
	ADDI A,60
	JRST PUTREL

DMYTRN:	MOVE B,TOPP
	MOVEM B,BBASE
	PUSH P,A
DMYTR2:	CAML A,RDWRDP
	JRST DMYTR1
	MOVE B,(A)
	MOVEM B,@TOPP
	AOS B,TOPP
	CAIL B,TOPPP
	ERRUUO (SIXBIT /TMD/)
	AOJA A,DMYTR2
DMYTR1:	POP P,RDWRDP
	POPJ P,

A.GSYL:	MOVNI D,100000
	MOVEM D,STRCNT
	TDZA SYM,SYM
GSYL:	CLEARB SYM,STRCNT
	MOVEI D,6
	MOVE T,[(440700)STRSTO
	MOVEM T,STRPNT
GSYL3:	AOSG A,STRCNT
	JRST GSYL2
	PUSHJ P,RCH
	IDPB A,STRPNT
GSYL2A:	PUSHJ P,LOWRCS	;CONVERT TO LOWER CASE
	CAIN A,".
	JRST GSYL1C
	HLRZ CH1,GDTAB(A)
	CAIN CH1,(JSP CH2,)
	JRST GSYL1A	;NUMBER
	PUSHJ P,GSYL1B	;RETURN ONLY ON SYL SEP
	HRRZ A,GDTAB(A)
	MOVE T,LIMBO1
C%:	POPJ P,"%

GSYL2:	ILDB A,A.GST3
	TRZN A,200
	JRST GSYL2A
	CAIG A,100
	JRST GSYL2
	HRROI T,(A)
	POPJ P,


GSYL1B:	XCT GDTAB(A)	;POPJ FOR SYL SEPS
	SUB P,[1,,1
GSYL1D:	SOJGE D,GSYL3
	AOJA D,GSYL3

GSYL1C:	ADD SYM,%.SQ(D)
	JRST GSYL1D

GSYL1A:	XCT NSQTB-60(A)
	JRST GSYL1D


STRTYP:	PUSHJ P,REDINC	;DEBUGGING AID ONLY
	EXCH A,B
	TRZE A,200
	JRST STRTP1
STRTP2:	PUSHJ P,TYO"
	MOVE A,B
	JRST STRTYP

STRTP1:	PUSH P,A
	MOVEI A,"*
	PUSHJ P,TYO
	POP P,A
	TRNE A,100
	JRST STRTP3
	ADDI A,260
	JRST STRTP2

STRTP3:	CAIN A,175
	SKIPA A,C%
	MOVEI A,"/
	JRST STRTP2


WRTSS:	MOVE T,[(440700)STRSTO
	MOVE B,STRCNT
	SOJL B,CPOPJ
	ILDB A,T
	PUSHJ P,PUTREL
	JRST .-3

ADEFINE:	MOVEI A,DMYDEF
	MOVEM A,DMYTOP
	PUSHJ P,GETSYL
	JUMPE SYM,.-1
	MOVEM SYM,MACNM
	MOVE A,FREEPT
	MOVEM A,PRDEF
	CLEARB LINK,B
	MOVEI A,374
	PUSHJ P,PUTREL
	MOVE T,LIMBO1
DEFNC:	CAIE T,12
	CAIN T,15
	JRST DEFNA
	CAIN T,"\
	JRST DEFNB
	CAIN T,"/
	JRST DEFNE
DEFND:	PUSHJ P,PDEF
	JUMPE SYM,DEFNC
	AOJA B,DEFNC
DEFNE:	TRO B,200
DEFNB:	MOVE A,B
	PUSHJ P,PUTREL
	MOVEI B,0
	TRO LINK,GENF
	JRST DEFND

DEFNA:	MOVE A,B
	PUSHJ P,PUTREL
	MOVEI A,0
	TRNN LINK,GENF
	PUSHJ P,PUTREL
	PUSHJ P,RCH
	CAIE A,12
	TLO I,UNRCHF
	PUSHJ P,WRQOTE
	PUSHJ P,STPWR
	MOVE SYM,MACNM
	PUSHJ P,ES
	JFCL
	MOVEI B,MACCL
	HRL B,PRDEF
	CLEARM PRDEF
	MOVSI C,77
	MOVSI T,PSUDO
	PUSHJ P,VSM2
	JRST ASSEM1

;ADD A NAME TO THE DUMMY DEFINITION TABLE
PDEF:	PUSHJ P,GSYL	;READ IN SYL
	CAIE T,",	;IF DELIMITING CHR NOT ,
	JUMPE SYM,CPOPJ	;AND SYM NULL, RETURN
	MOVEM SYM,@DMYTOP	;STORE SYM
	AOS A,DMYTOP	;INCR PNTR
	CAIL A,DMYDEF+DMDEFL	;CHECK FOR TABLE SIZE EXCEEDED
	ERRUUO (SIXBIT /TMD/)	;YES
	POPJ P,

WRQOTE:	CLEARM DCNT
	PUSHJ P,GSYL
	PUSHJ P,ES
	MOVEI A,0
	MOVE TT,A
	CAIE A,PSUDO/40000
	JRST WRQOT4
	HRRZS B
	CAIN B,A.QOTE
	JRST A.QOT1
WRQOT4:	JUMPE SYM,WRQOT1
	MOVEI A,DMYDEF
	CAML A,DMYTOP
	JRST WRQOT2
	CAME SYM,(A)
	AOJA A,.-3
	SUBI A,DMYDEF
	PUSH P,A	;FLUSH ! S WHEN THEY DO WORK
	MOVE A,FREEPT
	SOS A
	PUSHJ P,REDINC
	CAIE B,"!
	JRST WRQOT5
	SOS FREEPT
	SOS PUTCNT
WRQOT5:	POP P,A
	TRO A,200
	PUSHJ P,PUTREL
	MOVE A,T
	CAIE A,"!
	PUSHJ P,PUTREL
	JRST WRQOTE+1
WRQOT2:	CAIE TT,PSUDO/40000
	JRST WRQOT1
	CAIE B,ADEFINE
	CAIN B,AIRP
	AOS DCNT
	CAIN B,ATERMIN
	SOSL DCNT
	JRST WRQOT1
	POPJ P,

WRQOT1:	PUSHJ P,WRTSS
	JRST WRQOTE+1

A.QOT1:	MOVE A,LIMBO1
	CAIN A,40
	PUSHJ P,RCH
	MOVEM A,A.QOT2
A.QOT3:	PUSHJ P,RCH
	CAMN A,A.QOT2
	JRST WRQOTE+1
	PUSHJ P,PUTREL
	JRST A.QOT3

AREPEAT:	MOVE A,FREEPT
	MOVEM A,PRREPT
	PUSHJ P,AGETFD
	TYPUUO (SIXBIT /USR/)
	JUMPLE A,COND4	;NO REPEAT PLAY LIKE STRING COND FALSE
	CAIN A,1
	JRST COND2	;REPEAT ONCE, PLAY LIKE SCOND TRUE
	PUSH P,A
	MOVEI A,373	;CHECK CHAR FOR REPEAT
	PUSHJ P,PUTREL	;STORE AS FIRST CHR OF BODY
	MOVSI LINK,400000+RDRPTF
	PUSHJ P,RDWRDA+1
	MOVEI A,15
	CAIE T,"]
	PUSHJ P,PUTREL
SWRET1:	PUSHJ P,STPWR	;ALSO RETURN FROM STRING WRITE (.F .I)
	POP P,B
	PUSHJ P,PUSHEM
	MOVEI B,REPT1
	PUSHJ P,PUSHEM
	MOVE A,PRREPT
	CLEARM PRREPT
	MOVE B,MACP
	HRRM A,-1(B)
	HRRZ A,CRPTCT
	HRLM A,-1(B)
	SETOM CRPTCT
	CLEARM CPTR
	JRST MACCR

A.IRPCNT:	SKIPA A,CIRPCT
A.RPCNT:	MOVE A,CRPTCT
	JRST CLBPOP

AFNM1:	SKIPA A,AFNAM1"
AFNM2:	MOVE A,AFNAM2"
	JRST CLBPOP
IFE TS,[
AFN1:	SKIPA A,FNAM1"
AFN2:	MOVE A,FNAM2"
	JRST CLBPOP
]

IFN TS,[
AFN1:	SKIPA A,RFNAM1
AFN2:	MOVE A,RFNAM2
	JRST CLBPOP
AIFN1:	SKIPA A,INFN1	;PICK UP INSERT FILE NAMES
AIFN2:	MOVE A,INFN2	;NNAME 2
	JRST CLBPOP
]

REPT1:	PUSH P,A
	PUSH P,C
	HRRZ A,(B)
	MOVEM A,REPTTM
	PUSHJ P,REDINC
	CAIE B,373
	ERRUUO (SIXBIT /IAE/)
	HRRZ C,MACP
	HRRZ B,-2(C)
	SOJL B,REPT2
	AOS CRPTCT
	MOVEM A,CPTR
	SETOM CPTRB
	HRRM B,-2(C)
REPT3:	POP P,C
	POP P,A
	JRST REPT6


A.STOP:	HRRZ A,MACP
	JUMPL B,A.STP1
	HRRZ B,(A)
	CAIN B,AIRP4
	HRRZS -1(A)
A.STP1:	CLEARM CPTR
	SETOM CPTRB
	JRST ASSEM1

	IMPUR
REPTTM:	0
	PUR
REPT2:	MOVE B,REPTTM
	MOVE A,CPTR
	SKIPL CRPTCT
	CAMN A,FREEPT
	MOVEM B,FREEPT
	MOVE A,[-3,,-2]
	ADDB A,MACP
	HLRZ A,1(A)
	MOVEM A,CRPTCT

REPT4:	PUSHJ P,POPEM
	JRST REPT3

BGETFD:	SETOM FLDCNT
AGETFD:	MOVE CH1,@(P)
	MOVEM CH1,GTVER
	PUSH P,I
	TRO I,PSEUDF
AGTFD3:	PUSHJ P,GETFLD
	MOVE CH1,LIMBO1
	CAIE CH1,12
	CAIN CH1,15
	JRST .+3
	TRNN I,FLD
	JRST AGTFD3
	TLNE I,MWRD
	JRST AGTFD1
AGTFD2:	MOVEM I,ISAV	;SAVE FLAGS FOR FLD GOTTEN
	POP P,I
	AOS (P)
	POPJ P,

AGTFD1:	PUSH P,A
	PUSHJ P,GETFLD
	TLNE I,MWRD
	JRST .-2
	POP P,A
	JRST AGTFD2

COND:	HRRI B,COND2
	PUSH P,B
	PUSHJ P,AGETFD
	TYPUUO (SIXBIT /UCD/)
	POP P,T
	XCT T	;JUMP IF COND T,ASSEMBLE STRING
COND4:	PUSHJ P,RCH
	CAIN A,"[
	JRST COND3
ANULL:	TLO I,UNRCHF
	PUSHJ P,RCH
	CAIE A,15
	JRST .-2
	JRST ANULL1

COND3:	MOVEI C,0
	PUSHJ P,RCHCNT
	JRST .-1
	JRST MACCR

;HERE FOR IF PUNCHING PASS HACK

COND5:	JUMPGE FF,COND4	;IF FF>0, THIS IS NOT PUNCHING PASS
COND2:	PUSHJ P,RCH
	CAIE A,"[
ANULL1:	TLO I,UNRCHF
	JRST MACCR

COND1:	HRRI B,PSS
	XCT B
	JRST COND4	;NO
	JRST COND2

SWINI:	MOVE A,FREEPT
	MOVEM A,PRREPT
	MOVEI A,373
	JRST PUTREL

SWRET:	PUSH P,[1]	;REPEAT COUNT
	JRST SWRET1

SWFLS:	MOVE A,PRREPT	;FLUSH RETURN
	MOVEM A,FREEPT
	JRST MACCR

SCOND:	MOVE A,FREEPT
	MOVEM A,PRSCND
	MOVEM A,PRSCN1
	HRRI B,SCONDF
	MOVEM B,SCINST
	MOVEI LINK,0
	PUSHJ P,RDWRDA+1
	SETOB C,SCONDF
	PUSHJ P,RCH
	CAIN A,"[
	MOVEI C,0
	SKIPN C
SCND2A:	PUSHJ P,RCH
SCOND2:	JUMPGE C,SCOND1
	CAIN A,",
	JRST SCOND3
SCOND6:	EXCH A,PRSCND
	PUSHJ P,REDINC
	EXCH A,PRSCND
	CAMN B,A
	JRST SCND2A
SCOND4:	CLEARM SCONDF
SCOND5:	PUSHJ P,RCH
	JUMPGE C,SCOND7
	CAIE A,",
	JRST SCOND5
SCOND3:	CLEARB A,C
	EXCH C,PRSCN1
	MOVEM C,FREEPT
	EXCH A,PRSCND
	PUSHJ P,REDINC
	CAIE B,375
	CLEARM SCONDF
	XCT SCINST
	JRST COND4
	JRST COND2

SCOND1:	PUSHJ P,QSCNT
	JRST SCOND6
	JRST SCOND3

SCOND7:	PUSHJ P,QSCNT
	JRST SCOND4+1
	JRST SCOND3

RCHCNT:	PUSHJ P,RCH
QSCNT:	CAIN A,"[
	AOJA C,CPOPJ
	CAIN A,"]
	SOJL C,POPJ1
	POPJ P,

AIRP:	MOVSI 17,-14
	PUSH P,2(17)
	AOBJN 17,.-1
	PUSH P,RDWRDP
	HLLZM B,AIRPT
	CLEARB LINK,IRPCR
	MOVEI A,DMYDEF
	MOVEM A,DMYTOP

AIRP1:	PUSHJ P,PDEF
	CAIE T,",
	JUMPE SYM,AIRP2
	PUSHJ P,PDEF
	MOVEI A,377
	MOVSI TT,200000
	TDNE TT,AIRPT
	PUSHJ P,PUTREL
AIRPS6:	PUSHJ P,ADDTRN
	MOVEI A,377
	PUSHJ P,PUTREL
	PUSHJ P,RDWRDA
	MOVE A,RDWRDP
	SOS -1(A)
	MOVSI TT,200000
	TDNE TT,AIRPT
	SOS -1(A)
	AOS IRPCR
	TLNN LINK,SCEND
	JRST AIRP1

	TLNE LINK,LCRIND
	TLZ I,UNRCHF	;FLUSH CR

AIRP2:	MOVE A,FREEPT
	MOVEM A,PRIRP
	PUSHJ P,RCH
	CAIE A,12
	TLO I,UNRCHF
	PUSHJ P,WRQOTE
	PUSHJ P,STPWR
	PUSHJ P,PUSHEM
	POP P,A
	PUSH P,TOPP
	PUSHJ P,DMYTRN
	MOVE B,MACP
	MOVE A,CIRPCT
	HRRM A,(B)
	SETOM CIRPCT
	MOVS A,IRPCR
	HRR A,PRIRP
	IOR A,AIRPT
	PUSH B,A
	POP P,A
	HRLS A
	HRRI A,AIRP4
	PUSH B,A
	MOVEM B,MACP
	CLEARM CPTR
	JRST MACC2


AIRP4:	PUSH P,A
	PUSH P,C
	PUSH P,T
	PUSH P,TT
	AOS CIRPCT
	HRRZ A,(B)
	MOVEM A,CPTR
	SETOM CPTRB
	SETOM AIRPT
	TRNE FF,MRSW
	JRST AIRP9
	HLRZ T,1(B)
	LDB C,[(220600+B)
	JUMPE C,AIRP9
	SKIPGE TT,(B)
	JRST AAIRPC
AIRP6:	SETOM AIRPT1
	HRRZ A,(T)
AIRP6A:	PUSHJ P,REDINC
	CAIN B,375
	JRST AIRP10
	CAIE B,377
	JRST AIRP6A
	SETOM AIRPT2
AIRP7E:	MOVEM A,(T)
AIRP7:	PUSHJ P,REDINC
	CAIN B,375
	JRST AIRP5
	TLNE TT,200000
	JRST AIRPS2
	CLEARM AIRPT
	CAIN B,"[
	AOSE AIRPT2
	JRST AIRP7A
AIRP7C:	MOVEI B,376
	DPB B,PTAB(CH2)
	JRST AIRP7
AIRP7A:	SKIPL AIRPT2
	CAIE B,"]
	JRST AIRP7B
	SOSGE AIRPT2
	JRST AIRP7C
AIRP7B:	CAIN B,15
	JRST AIRP7D
	CAIE B,12
	CAIN B,",
	SKIPL AIRPT2
	JRST AIRP7
AIRPS1:	MOVEI CH1,-1(A)
	IDIVI CH1,4
	MOVEI B,377
	DPB B,PTAB(CH2)
	TLNN TT,200000
	MOVEM A,1(T)
AIRP8:	ADDI T,2
	SOJG C,AIRP6
AIRP9:	POP P,TT
	POP P,T
	SKIPL AIRPT
	JRST REPT3
	MOVE A,[-3,,-2
	ADDB A,MACP
	HRRZ A,(A)
	MOVEM A,CIRPCT
	POP P,C
	POP P,A
	JRST RCHSAV


AIRPS2:	CAIL B,101
	CAILE B,132
	JRST AIRPS3
AIRPS4:	CLEARM AIRPT
	CLEARM AIRPT1
	JRST AIRP7

 AIRPS3:	CAIG B,71
	CAIGE B,60
	CAIN B,"$
	JRST AIRPS4
	CAIE B,"%
	CAIN B,".
	JRST AIRPS4
	CAIN B,"!
	JRST AIRPS4
	SKIPGE AIRPT1
	JRST AIRP7E
	HRRZ CH1,1(T)
	IDIVI CH1,4
	DPB B,PTAB(CH2)
	JRST AIRPS1

AIRP7D:	SKIPL AIRPT2
	JRST AIRP7
	JRST AIRP7C

AIRP10:	CLEARM (T)
AIRP5:	CLEARM 1(T)
	JRST AIRP8

AAIRPC:	MOVE A,(T)
	PUSHJ P,REDINC
	CAIN B,375
	JRST AIRPC3
	CAIE B,377
	JRST AAIRPC+1
	MOVEM A,1(T)
	PUSHJ P,REDINC
	CAIN B,375
	JRST AIRPC3
	CLEARM AIRPT
	MOVEI CH1,-2(A)
	MOVEM CH1,(T)
	IDIVI CH1,4
	DPB B,PTAB(CH2)
	MOVEI B,377
	DPB B,PTAB+1(CH2)
	AOS 1(T)
	ADDI T,2
AIRPC2:	SOJG C,AAIRPC
	JRST AIRP9

AIRPC3:	CLEARM (T)
AIRPC1:	CLEARM 1(T)
	JRST AIRPC2-1

COLON:	TLNN I,WRDF
	TRNN I,SYL
	TYP6UU (SIXBIT /ILF/)
	TRNN I,OPFLD+PSEUDF
	TRNN I,LET
	TYP6UU (SIXBIT /ILT/)
	TLNE FF,INDEFF
	TYP6UU (SIXBIT /CLI/)
	MOVE T,CLOC
	SKIPGE BYTM
	HLL T,BYTWP
	ADD T,OFLOC
	MOVEM T,WRD
	EXCH T,SYLOC
	MOVEM T,SYLOC1
	EXCH SYM,SYSYM
	MOVEM SYM,SYSYM1
	MOVE SYM,SYSYM
	MOVE A,CRLOC
	ADD A,OFRLOC
	HRRZM A,WRDRLC
	CLEARM PARBIT
	SKIPN LDCCC
	TRNE FF,GLOLOC+GLOFFS
	JRST GCOL1
	PUSHJ P,ES
	JRST EQL1A
	PUSH P,C
	TLZ C,777700
	JUMPE C,COLON5
	CAME C,[(77)
	CAMN C,LEV
	JRST COLON1
	JRST COLON2
COLON1:	POP P,C
	JRST .+1(A)
	TYP6UU (SIXBIT /MDT/)	;COMMON
	TYP6UU (SIXBIT /RES/)	;MACRO OR PSEUDO
	JRST COLON4	;SYM
	JRST EQL1C	;LOCAL UNDEF
	TYP6UU (SIXBIT /MDT/)
	TYP6UU (SIXBIT /MDT/)
	TYP6UU (SIXBIT /MDT/)
	TYP6UU (SIXBIT /MDT/)
	JRST EQL1E	;GLOBAL ENTRY
	JRST EQL1E	;GLO EXIT


COLON4:	CAME B,WRD
	JRST COL4E
	PUSHJ P,RCHK
	JUMPN C,.+2
	CAME B,WRDRLC
COL4E:	TYP6UU (SIXBIT /MDT/)	;MDGS
CASSM1:	JRST ASSEM1


COLON5:	CAIE A,2
	JRST COLON1
	JRST COLON2

		;COLON WHEN LOCATION VIRTUAL

GCOL1:	PUSHJ P,ES
	JRST GCOL2	;NOT ALREADY DEFINED
	PUSH P,A
	CAIE A,GLOEXT_-14.
	TLNE I,GLI
	TLO SYM,40000
	PUSHJ P,LKPNRO
	TLZ SYM,40000
	POP P,A
	CAIE A,GLOETY_-14.
	CAIN A,GLOEXT_-14.
	JRST GCOL3
	TLNN I,GLI
	JRST GCOL4
	PUSHJ P,PLOGLO
GCOL3:	MOVSI T,GLOETY
	TLOA SYM,40000
GCOL4:	MOVSI T,LCUDF
GCOL5:	PUSHJ P,VSM2LV
	PUSH P,C1ASSEM1

PDEFPT:	PUSHJ P,PBITS7
	MOVEI A,CDEFPT
	PUSHJ P,PBITS
	JRST OUTSM

C1ASSEM1:
GCOL2:	SETZI B,ASSEM1
	MOVE C,LEV
	TLNN I,GLI
	JRST GCOL4	;SYM NOT FOLLOWED BY "
	MOVSI C,77
	JRST GCOL3

CNSTNT:	PUSH P,CASSM1
	SOSGE CONCNT
	ERRUUO (SIXBIT /TCA/)	;TOO MANY CONSTANTS AREAS
	MOVE B,CLOC
	ADD B,OFLOC
	HRRZ T,PBCON
	TRNE FF,PSS
	JRST CNST2
	HRRM B,1(T)
	MOVEI D,0
	MOVE A,CRLOC
	ADD A,OFRLOC
	TRNE A,-1
	TLO D,CTRL
	TRNE FF,GLOLOC
	TLO D,CGBAL
	IORM D,2(T)

CNST1:	MOVE SYM,(T)
	TRNE FF,GLOLOC
	PUSHJ P,PDEFPT
CNST1B:	MOVEI A,CONTAB
CNSTH:	CAML A,PLIM
	JRST CNSTA	;THRU
	MOVE TT,(A)
	MOVEM TT,WRD
	PUSHJ P,CPTMK
	LDB F,C
	TRZE F,2
	TLO F,1
	MOVEM F,WRDRLC
	MOVEI D,GLOTB
	MOVEM D,GLSP2
	MOVEI C,CONGLO
CNSTC:	CAML C,CONGOL
	JRST CNSTB
	CAME A,1(C)
	JRST .+4
	AOS D
	MOVE B,(C)
	MOVEM B,(D)
	AOS C
	AOJA C,CNSTC

CNSTB:	MOVEM D,GLSP1
	PUSH P,A
	PUSHJ P,PWRD
	AOS CLOC
	POP P,A
	AOJA A,CNSTH


CNST2:	MOVSI A,CGBAL
	TDZ A,2(T)
	TRNE FF,GLOLOC
	TLC A,CGBAL
	SKIPN A
	ERRUUO (SIXBIT /CVD/)
	HRRZ B,1(T)
	SUB B,OFLOC
	HRRZS B
	CAME B,CLOC
	ERRUUO (SIXBIT /CLD/)
	MOVE B,2(T)
	ROT B,2
	XOR B,CRLOC
	XOR B,OFRLOC
	TRNE B,1
	ERRUUO (SIXBIT /CRD/)
	JRST CNST1


CNSTA:	HRRZ T,PBCON
	TRNE FF,GLOLOC
	JRST CNSTD
	TRNN FF,NPSS
	SKIPGE 2(T)
	JRST CNSTDA
	TRO I,CONT
	PUSHJ P,P70
	MOVE A,(T)
	MOVE TT,CRLOC
	ADD TT,OFRLOC
	TRNE TT,-1
	TLO A,100000
	PUSHJ P,OUTPUT
	HRRZ A,1(T)
	PUSHJ P,OUTPUT
	TRZ I,CONT
CNSTDA:	MOVSI A,CTDEF
	IORM A,2(T)
CNSTD:	TRNE FF,PSS
	JRST CNST3

	MOVE A,CLOC
	HRLM A,1(T)

CNSTE:	MOVEI A,CONTAB
	MOVEM A,PLIM
	MOVEI A,CONGLO
	MOVEM A,CONGOL
	MOVEI T,3
	ADDB T,PBCON
	CAML T,PBCONL
	MOVEM T,PBCONL
	AOS A,CSQZ
	MOVEM A,(T)
	POPJ P,

CNST3:	PUSHJ P,EBLK
	HLRZ A,1(T)
	MOVEM A,CLOC
	JRST CNSTE

CONBUG:	MOVEI A,CONGLO	;B VAL C FLAGS ST(D) SADR
	PUSH P,T
	PUSH P,C
CONBG2:	MOVE C,(P)
	CAML A,CONGOL
	JRST CONBG1
	HRRZ F,(A)
	IFE SLOW,CAIE F,ST(D)
	IFN SLOW,CAIE F,@STD
	AOJA A,CONBG6
	PUSH P,B
	MOVE T,(A)
	LDB CH2,[(221200)T
	SKIPE CH2
	IMUL B,CH2
	TLNE T,MINF
	MOVNS B
	TLNE T,HFWDF
	HRRZS B
	TLNE T,ACF
	ANDI B,17
	TLNE T,SWAPF
	MOVSS B
	TLNE T,ACF
	LSH B,5
	ADD B,@1(A)
	TLNN T,SWAPF
	HRRM B,@1(A)
	TLNE T,SWAPF
	HLLM B,@1(A)
	TLNN T,HFWDF
	MOVEM B,@1(A)
	LDB CH1,[420200+P,,-1
	TLNE T,HFWDF
	TRZ CH1,2
	TLNE T,SWAPF
	LSH CH1,1
	TRZE CH1,4
	TRO CH1,1
	PUSH P,A
	HRRZ A,1(A)
	PUSHJ P,CPTMK
	LDB B,C
	TLNE T,MINF
	JRST CONBG8
	TRNE B,(CH1)
	TYP6UU (SIXBIT /CRI/)
	IOR B,CH1
CONB8A:	DPB B,C
	POP P,A
	CLEARM (A)
	CLEARM 1(A)
	POP P,B
	AOS A

CONBG6:	AOJA A,CONBG2

CONBG1:	MOVEI A,CONGLO
	PUSH P,B
	MOVEI B,CONGLO
CONBG7:	CAML A,CONGOL
	JRST CONBG3
	SKIPN C,(A)
CONBG5:	AOJA A,CONBG4
	MOVEM C,(B)
	MOVE C,1(A)
	MOVEM C,1(B)
	AOS B
	AOJA B,CONBG5

CONBG4:	AOJA A,CONBG7
CONBG3:	MOVEM B,CONGOL
	POP P,B
	POP P,C
	POP P,T
	POPJ P,
CONBG8:	XORI B,3
	TRNE B,(CH1)
	TYP6UU (SIXBIT /CRI/)
	ANDCB B,CH1
	JRST CONB8A

OPEN:	MOVEM A,LSYL
	MOVEM B,LSYLR
	PUSH P,D
	PUSHJ P,GETWRD
	POP P,D
	CAME D,LIMBO1
	TLNE I,MWRD
	POPJ P,
	MOVE D,LIMBO1
	CAIE D,15
	CAIN D,12
	POPJ P,
	TYP2UU (SIXBIT /ILC/)	;ILLEGAL CLOSE
	POPJ P,

LSSTH:	MOVEI D,">
	JSP LINK,SAVWD1
	PUSHJ P,OPEN
	TLNE I,MWRD
	PUSHJ P,IGTXT
LSSTH3:	JSP LINK,USVWLD
	TLZE I,LSRET
	JRST LSSTHR	;RETURN TO ^ OR _

LSSTH2:	ADDM A,LSYL
	MOVE A,LSYLR
	PUSHJ P,HFWDAD
	MOVEM A,LSYLR
LSSTH5:	MOVE A,LIMBO1
	CAIE A,15
	CAIN A,12
	JRST LSSTH6
	PUSHJ P,RCH
	CAIN A,"!
	JRST .-2
	TLO I,UNRCHF
	HLRZ CH1,GDTAB(A)
	CAIE A,"_
	CAIN A,"]
	JRST LSSTH7
	CAIE CH1,(POPJ P,)
	JRST LSSTH4
	HRRZ CH1,GDTAB(A)
	MOVE CH1,DTB-40(CH1)
	CAIN A,";
	JRST LSSTH7
	TLNE CH1,DSYL
	JRST LSSTH4
LSSTH7:	PUSHJ P,GETSYL
LSSTH6:	TRO I,SYL
	MOVE A,LSYL
	MOVE B,LSYLR
	POP P,LSYLR
	POP P,LSYL
	JRST GETFD6

LSSTH1:	ADDM A,WRD
	MOVE A,WRDRLC
	PUSHJ P,HFWDAD
	MOVEM A,WRDRLC
	JRST LSSTH5

LSSTH4:	SKIPN LSYL
	SKIPE LSYLR
	TYP2UU (SIXBIT /NOS/)	;IMPROPER +( ... )
	POP P,LSYLR
	POP P,LSYL
	JRST GETFD1


LSSTHR:	POP P,LSYLR
	POP P,LSYL
	POPJ P,

AVARIAB:	PUSH P,CASSM1
	SOSG VARCNR
	ERRUUO (SIXBIT /TVA/)
	IFE SLOW,MOVSI D,-SMK
	IFN SLOW,MOVS D,MSMK
	MOVE T,CLOC
	MOVEM T,VCLOC
	ADD T,OFLOC
	MOVE C,CRLOC
	ADD C,OFRLOC
	TRNE FF,PSS
	JRST AVAR1
	HRL T,VARCNT
	TRNE C,-1
	TLO T,400000
	MOVEM T,@VARPNT

AVAR2:	3GET C,D
	IFE SLOW,MOVE B,ST+1(D)
	IFN SLOW,MOVE B,@ST1D
	IFE SLOW,MOVE SYM,ST(D)
	IFN SLOW,MOVE SYM,@STD
	TLZ SYM,740000
	IFE SLOW,LDB LINK,[(400400)ST(D)]
	IFN SLOW,LDB LINK,[400400,,@STD]
	CAIE LINK,UDEFLV_-14.
	CAIN LINK,UDEFGV_-14.
	JRST AVAR3
	CAIE LINK,DEFGVR_-14.
	CAIN LINK,DEFLVR_-14.
	JRST AVAR4
AVAR2A:	AOS D
	AOBJN D,AVAR2
	HLRZ A,@VARPNT
	TRZ A,400000
	ADD A,VCLOC
	MOVEM A,CLOC
	PUSHJ P,EBLK
	CLEARM VARCNT
	AOS VARPNT
	POPJ P,

AVAR3:	CAIN LINK,UDEFGV_-14.
	TLO SYM,40000
	PUSHJ P,LKPNRO
	MOVSI T,DEFLVR
	CAIN LINK,UDEFGV_-14.
	MOVSI T,DEFGVR
	TRNE FF,GLOLOC
	JRST AVAR3A
	MOVEI B,-1(B)
	ADD B,VCLOC
	ADD B,OFLOC
	MOVE TT,CRLOC
	ADD TT,OFRLOC
	SKIPE TT
	TLO C,3RLR
	CAIE LINK,UDEFGV_-14.
	TLZN C,3VCNT
	SKIPA
	PUSHJ P,CONBUG
	PUSHJ P,VSM2
AVAR4B:	PUSHJ P,OUTDE2
	JRST AVAR2A

AVAR1:	HRRZ A,@VARPNT
	CAIE A,(T)
	ERRUUO (SIXBIT /VLD/)
	HLRZ A,@VARPNT
	TRZE A,400000
	XORI C,1
	TRNE C,-1
	ERRUUO (SIXBIT /VRD/)	;CAUSE ERROR ROUTINE TO PRINT OUT RIGHT LOCN
	SKIPE VARCNT
	ERRUUO (SIXBIT /VND/)
	JRST AVAR2

AVAR4:	TLNN C,3VAS2
	JRST AVAR2A
	TLOE C,3VP
	JRST AVAR2B
	CAIN LINK,DEFGVR_-14.
	TLO SYM,40000
	PUSHJ P,LKPNRO
	TRNN FF,GLOLOC
	JRST AVAR4A
AVAR3A:	PUSHJ P,VSM2LV
	PUSHJ P,PDEFPT
	PUSHJ P,PBITSZ
	PUSHJ P,OUTPUT
AVAR2B:	TRNE FF,GLOLOC
	AOS CLOC
	JRST AVAR2A

AVAR4A:	CAIN LINK,DEFGVR_-14.
	JRST AVAR4B
	3PUT C,D
	JRST AVAR2A

LEFTP:	MOVEI D,")
	JSP LINK,SAVWD1
	MOVEI C,0
	TRNE I,OPFLD
	TRNE I,SYL
	TLO C,400000
	PUSH P,C
	PUSHJ P,OPEN
	POP P,C
	MOVSM A,T1
	MOVSM B,T2
	MOVSI B,SWAPF
	PUSHJ P,LNKTC1
	TLNE I,MWRD
	PUSHJ P,IGTXT
	JSP LINK,USVWLD
	MOVE A,T1
	MOVE B,T2
	JUMPL C,LSSTH1	;ADD TO WHOLE WORD
	JRST LSSTH2


LBRAK:	TRO I,FLD
	JSP LINK,SAVWD1
LBRAK1:	AOS CONDEP
	MOVEI D,"]
	PUSHJ P,OPEN
LBRAK3:	SOS CONDEP
LBRAK2:	MOVE F,GLSP1
	SUB F,GLSP2
	JUMPE F,SCON
	MOVEI B,-1(F)
	MOVN TT,B
	JUMPE B,SCON
LSORT:	HRL T,TT
	HRR T,GLSP2
	MOVE A,1(T)
	CAML A,2(T)
	EXCH A,2(T)
	MOVEM A,1(T)
	AOBJN T,.-4
	SOJG B,LSORT


SCON:	PUSHJ P,RCHKMV
	ROT T,2
	TLNE I,MWRD+MWRD1
	JRST NOCON
	MOVEI A,CONTAB
SCON1:	CAML A,PLIM
	JRST NOCON	;NO MATCH
	MOVE B,WRD
	CAME B,(A)
SCON2:	AOJA A,SCON1	;VAL DISAGREES
	PUSHJ P,CPTMK
	LDB F,C
	CAME F,T
	JRST SCON2	;RLC DIFFRS
	MOVEI B,CONGLO
	SKIPA C,GLSP2
	AOS B
	CAML B,CONGOL
	JRST SCON3
	CAME A,1(B)
SCON7:	AOJA B,.-4
	MOVE D,(B)
	CAME D,1(C)
	JRST SCON2
	AOJA C,SCON7

SCON3:	CAME C,GLSP1
	JRST SCON2
NOCON4:	MOVE C,GLSP2
	MOVE B,PBCON	;BY GOD,THEY MATCH
	SKIPL 2(B)
	AOJA C,SCON4	;CONST AREA UNDEF
	MOVEM C,GLSP1	;DEF
	MOVE C,1(B)
	ADDI A,(C)
	LDB B,[(420100+B)2
	JRST SCON6

SCON4:	TLNE I,MWRD
	TLNN I,MWRD1
	JRST .+2
	JRST SCON6A
	MOVEM C,GLSP1
	MOVEM B,(C)
SCON6A:	MOVEI B,0
SCON6:	SUBI A,CONTAB
	TLZE I,MWRD1
	JRST SCOM1
	TLNN I,MWRD
	JRST LSSTH3
	PUSH P,A
	PUSH P,B
	PUSH P,GLSP1
	PUSH P,GLSP2
	PUSHJ P,.+1
SCOM2:	TLO I,MWRD1
	MOVEI D,"]
	AOS CONDEP
	PUSHJ P,OPEN+2
	JRST LBRAK3
SCOM1:	TLNE I,MWRD
	JRST SCOM2
	MOVEI TT,SCOM2
	JSP LINK,POPLIS
	JRST LSSTH3

NOCON:	AOS A,PLIM
	CAIL A,CONTAB+LCONTB+1
	ERRUUO (SIXBIT /TMC/)
NOCON2:	MOVE AA,WRD
	MOVEM AA,-1(A)
	SOS A
	PUSHJ P,CPTMK
	DPB T,C
	MOVE B,GLSP2
NOCON3:	CAML B,GLSP1
	JRST NOCON4
	MOVE C,1(B)
	MOVEM C,@CONGOL
	HRRZS C
	PUSHJ P,NOCON5
	3GET1 D,C
	TLO D,3VCNT
	MOVEM A,@CONGOL
	PUSHJ P,NOCON5
IFE SLOW,CAIL C,CONTAB
IFN SLOW,[
	MOVE TM,C	;PREPARE TO TEST FOR A WINNER
	PUSHJ P,CONSTC	;SKIP IF IN SYMBOL TABLE
	JRST .+2
]
	AOJA B,NOCON3
	3PUT1 D,C
	AOJA B,NOCON3

CONST3:	PUSHJ P,EBLK
	HLRZ A,1(T)
	MOVEM A,CLOC
	JRST CNSTE


NOCON5:	AOS AA,CONGOL
	CAIL AA,CONGLO+LCNGLO
	ERRUUO (SIXBIT /TMC/)
	POPJ P,

CPTMK:	PUSH P,A
	SUBI A,CONTAB
	PUSH P,B
	IDIVI A,18.
	MOVEI C,CONBIT(A)
	DPB B,[(370500)C
	TLO C,200
	POP P,B
	JRST POPAJ

INIT":	HRRM A,RETURN
	SKIPGE ISYMF
	JRST INIT1
	IFE SLOW,MOVSI A,-SMK
	IFN SLOW,MOVS A,MSMK
IFE SLOW,INIT4:	SKIPN B,ST(A)
IFN SLOW,INIT4:	SKIPN B,@STA
	JRST INIT2
	3GET C,A
	TLNN C,3INI	;INITIAL SYM
	IFE SLOW,CLEARM ST(A)
	IFN SLOW,CLEARM @STA
INIT2:	AOS A
	AOBJN A,INIT4
	MOVSI A,-LCONTB-.LCN18-NCONS*3
	CLEARM CONBIT(A)
	AOBJN A,.-1

SP4:	PUSH P,CRETN
P1INI:	CLEARB I, LDCCC
	CLEARM  BKBUF
	CLEARM STGSW
	TDZ FF,[-1-VOT-PTPF,,-1]	;INITIALIZE MOST FF FLAGS
	CLEARM ISYMF
	CLEARM MDEPTH
	MOVEI A,DMYAGT
	MOVEM A,TOPP
	MOVEM A,BBASE
	MOVE A,[-MPDLL,,MACPDL]
	MOVEM A,MACP
	MOVEI A,3
	MOVEM A,FREEPT
	MOVEI A,CONGLO
	MOVEM A,CONGOL
	MOVEI A,PCNTB
	MOVEM A,PBCONL
	MOVSI A,2
	MOVEM A,LEV
	MOVE A,[SQUOZE 0,.MAIN
	MOVEM A,PRGNM
	MOVE A,[PUSHJ P,RPA"
	MOVEM A,GETCHR

P2INI:	MOVEI A,1
	MOVEM A,CPGN
	MOVEM A,CLNN
	TLZ FF,INDEFF
	CLEARM GENSM
	MOVEI A,NCONS
	MOVEM A,CONCNT
	MOVEI A,VARTAB
	MOVEM A,VARPNT
	MOVEI A,NVARS
	MOVEM A,VARCNR
	CLEARM OFLOC
	CLEARM OFRLOC
	CLEARM VARCNT
	CLEARM PBITS2
	MOVE A,[(440300)PBITS1
	MOVEM A,BITP
	MOVEI A,PBITS4
	HRRZM A,PBITS4
	CLEARB I,PBITS1
	MOVEI A,CONTAB
	MOVEM A,PLIM
	MOVEI A,PCNTB
	MOVEM A,PBCON
	MOVE A,[34773600001
	MOVEM A,PCNTB
	AOS A
	MOVEM A,CSQZ
	MOVEI A,10
	MOVEM A,ARADIX
	MOVEI A,100
	MOVEM A,CLOC
	CLEARM CRLOC
	MOVEI A,BKBUF+1
	MOVEM A,OPT1
	TRO FF,IPSYMS+FIRWD
	CLEARM CRPTCT
	CLEARM SYLOC
	CLEARM SYSYM
	CLEARM BYTW	;CLEAR BYTE MODE WORD
	MOVE A,[IFORTB,,FORTAB]	;INITIALIZE FORMAT TABLE ON EACH PASS
	BLT A,FRTBE
IFN LISTSW,[
	MOVE A,[440700,,LISTBF]
	MOVEM A,PNTBP
	CLEARM LISTPF
	SETOM LISTBC
]
CRETN:	POPJ P,RETURN

A.QOTE:
ASSEM1:	TRZ I,FLD+SYL+LET+PSEUDF+COM+CONT+GMINF+OPFLD
	TLZ I,GLI+VAR+FLO+DECP+UARI+MNSFLG+WRDF+NPRC
	TRO I,DEF
	TLZ FF,SKILF
	MOVE P,[(,-LPDL)PDL"
	HRRZM P,GETCNR
	MOVEI A,GLOTB
	MOVEM A,GLSP1
	TLNE I,MWRD
	JRST ASSEM2
	PUSHJ P,RCH
	CAIG A,40
	JRST .-2
	CLEARM PARBIT
	TLO I,UNRCHF
	CLEARM CONDEP
ASSEM2:	PUSHJ P,GETWRD
	TLZN I,WRDF
	JRST ASSEM1
	SKIPE STGSW
	TYPUUO (SIXBIT /SWD/)
	SKIPGE BYTM
	JRST PBYTE
ASSEM3:	PUSHJ P,PWRD
	AOS CLOC
	HRRZS CLOC	;MAKE SURE LOCATION DOESN'T INCREMENT TO BITS IN LEFT HALF
	JRST ASSEM1

PSYMS":	HRRM A,RETURN
	TRNN FF,IPSYMS
	JRST APSYM1
	PUSHJ P,SYMDMP
	XCT RETURN

	IMPUR

RETURN:	JRST .

	PUR


APSYM1:	SKIPN CONTRL
	PUSHJ P,SYMDA	;PUNCH PROG NAME
	JRST RETURN

EQUAL:	CAMN SYM,[SQUOZE 0,.]	;.=FOO, SAME AS LOC FOO
	JRST ALOC
	TRNN I,OPFLD+PSEUDF
	TRNN I,LET
	TYP6UU (SIXBIT /IPA/)	;ILLEG ARG EQ
	PUSH P,SYM
	PUSH P,I
	MOVE A,[TYPUUO (SIXBIT /USP/)
	MOVEM A,GTVER
	TRO I,PSEUDF+DEF+EQLF
	PUSHJ P,RCH
	CAIE A,"=
	TLOA I,UNRCHF
	TLO FF,SKILF
	PUSHJ P,GETWRD
	MOVEI CH1,CRDF
	MOVEM CH1,PARBIT
	TRNN I,DEF
	JRST ASEM1A
IFN LISTSW,[
	SKIPGE LISTPF
	PUSHJ P,PNTR
	MOVE SYM,WRD
	MOVEM SYM,LISTWD
	MOVE SYM,WRDRLC
	MOVEM SYM,LSTRLC
	SETOM LISTAD
	SETOM LISTPF
]
	TDZ I,[-1-(MWRD)
	IOR I,(P)
	POP P,(P)
	POP P,SYM
	SKIPE LDCCC
	JRST EQG1
	MOVE A,GLSP1
	CAMN A,GLSP2
	JRST EQL1
EQG1:	PUSHJ P,ES	;GLOBALS TO RIGHT
	JRST EQL2	;NOT FOUND
	JRST .+1(A)
	TYP6UU (SIXBIT /IPA/)	;COMMON
	TYP6UU (SIXBIT /IPA/)	;PSEUDO OR MACRO
	JRST EQL2	;SYM
	JRST EQL2	;LOCAL UNDEF
	TYP6UU (SIXBIT /IPA/)	;DEF LOC VAR
	TYP6UU (SIXBIT /IPA/)	;UNDEF LOC VAR
	TYP6UU (SIXBIT /IPA/)	;DEF GLO VAR
	TYP6UU (SIXBIT /IPA/)	;UNDEF GLO VAR
	JRST EQL7	;GLO ENTRY



EQL8:	JUMPE B,EQL7	;GLO EXIT REACHED BY DISP
	PUSHJ P,GLKPNR
EQL7:	PUSH P,CASM1A
GLOPRA:	MOVSI T,GLOETY	;GLOBAL PARA ASSIGN
	MOVSI C,3LLV+77	;VAL MUST BE PUT IN BYLOADER FLAG
	MOVEI B,0
	TLO SYM,40000
LOPRA1:	PUSHJ P,VSM2
	JUMPGE FF,CPOPJ
	TLO C,3VP
	3PUT C,D
	PUSHJ P,EBLK
	MOVEI TT,LGPA
	DPB TT,[(310700)BKBUF
	PUSHJ P,OUTSM
	PUSHJ P,PWRDA
	JRST EBLK

GLKPNR:	TLO SYM,40000	;GLO BIT
LKPNRO:	TLNN C,3RLNK
	TLNE B,-1
	TROA I,CONT
	POPJ P,
	MOVEI A,6
	PUSHJ P,PBITS
	PUSHJ P,OUTSM
	HLRZ A,B
	TLZE C,3RLNK	;RELOC OF LINK PNR
	TLO A,100000
	HRRZS B
	TRZ I,CONT
	JRST OUTPUT


EQL2:	TLNE I,GLI
	JRST  EQL7
	PUSHJ P,LOPRA
CASM1A:	JRST ASEM1A

LOPRA:	MOVSI T,LCUDF
	TLZ C,77
	IOR C,LEV
	TLO C,3LLV
	TLNE FF,SKILF	;TEST HALF KILLING
	TLO C,3SKILL	;SET BIT
	JRST LOPRA1


EQL1:	PUSHJ P,ES
	JRST EQL1A	;NOT FOUND
	JRST .+1(A)
	TYP6UU (SIXBIT /IPA/)	;COMMON
	JRST EQL1B2	;PSEUDO OR MACRO
	JRST EQL1B	;SYM
	JRST EQL1C	;LOCAL UNDEF
	TYP6UU (SIXBIT /IPA/)	;DEF LOC VAR
	TYP6UU (SIXBIT /IPA/)	;UNDEF LOC VAR
	TYP6UU (SIXBIT /IPA/)	;DEF GLO VAR
	TYP6UU (SIXBIT /IPA/)	;UNDEF GLO VAR
	JRST EQL1D	;GLO ENTRY
EQL1E:	PUSHJ P,GLKPNR	;GLO EXIT REACHED BY DISP DUMP LINKING POINTER
EQL1D:	PUSHJ P,RCHK	;GLO ENTRY
EQLB2:	PUSHJ P,RMOVE
	TLO C,77
	MOVE B,WRD
	HRLZI T,GLOETY
	SKIPE LDCCC
	TLO C,3LLV
	PUSHJ P,VSM2
	TLO SYM,40000
EQL1CE:	JUMPGE FF,ASEM1A
	PUSHJ P,OUTDE1
ASEM1A:	TLNE I,MWRD
	PUSHJ P,IGTXT
ATERMIN:	JRST ASSEM1

OUTDE2:	MOVEM B,WRD
OUTDE1:	TLNE FF,PPSS
OUTD1:	TLO C,3VP
	3PUT C,D
OUTDEF:	TRO I,CONT
	PUSHJ P,P70
	PUSHJ P,GTVL7B
EQL1CC:	PUSHJ P,OUTSM
	TRZ I,CONT
	JRST OUTWD

EQL1C:	MOVE T,LEV
	CAME T,ESL1
	JRST EQL1CD
	TLNE I,GLI
	JRST EQL1CA
EQL1CB:	PUSH P,C
	PUSHJ P,LKPNRO
	PUSHJ P,RCHKMV
	MOVSI T,SYMC
	PUSHJ P,EQA2A
	POP P,AA
	TLNE AA,3VCNT	;USED IN CONSTANT
	PUSHJ P,CONBUG
	JRST EQL1CE

P70:	PUSHJ P,PBITS7
PBITSZ:	SKIPA A,PARBIT
PBITS7:	MOVEI A,7
	JRST PBITS

EQL1CD:	TLNN I,GLI
	JRST EQL1BA
	JRST EQL1E

EQL1CA:	PUSHJ P,PLOGLO
	JRST EQL1E
EQA2:	PUSH P,CASM1A
EQA2A:	IOR C,LEV
	TLNE FF,SKILF
	TLO C,3SKILL
	JRST VSM2W

EQL1B2:	TYP2UU (SIXBIT /QPA/)
EQL1B:	PUSHJ P,RCHK
	TLNE I,GLI
	JRST EQLB2

EQL1B1:	MOVE T,LEV
	CAME T,ESL1
	JRST EQL1BA
	PUSHJ P,RMOVE
	MOVSI T,SYMC
	MOVE C,ESL2	;RESTORE 3RD WORD TO C
	JRST EQA2

COLON2:	POP P,C
IFE SLOW,[EQL1BA:	MOVNI TT,SMK
	PUSHJ P,HASH
	MOVE B,ST(C)
]
IFN SLOW,[EQL1BA:	HRRE TT,MSMK
	MOVEI C,0
	MOVE B,@STC
]
	TLZ B,740000
	JUMPE B,EQL1BB
	CAMN B,SYM
	JRST EQL1SM
EQL1SR:	ADDI C,2
IFE SLOW,[
	CAIL C,2*SMK
	MOVEI C,0
]
	AOJN TT,EQL1BA+2
IFE SLOW,	ERRUUO (SIXBIT /SCE/)	;SCE
IFN SLOW,[	PUSHJ P,EXTSYM	;GROW TABLE
	JRST EQL1BB	;HAPPY TO USE THIS WHOLE
]
EQL1BB:	MOVE D,C
EQL1A1:	PUSHJ P,RCHKMV
	HRLZI T,SYMC
	TLO C,3MAS
	JRST EQA2

EQL1SM:	3GET D,C
	TLO D,3MAS
	3PUT D,C
	JRST EQL1SR


EQL1A:	TLNN I,GLI
	JRST EQL1A1
EQ12:	MOVEI C,0
	JRST EQL1D

RCHKMV:	PUSHJ P,RCHK
	JRST RMOVE

GETVAL:	HLR AA,I
	ANDI AA,3	;MICRO DISP ON GLI+VAR
	PUSHJ P,ES
	JRST GTVL2	;NOT FOUND
	JRST .+1(A)
	JRST GTVL1	;COMMON
	JRST GTVPM	;PSEUDO OR MACRO
	JRST GTVL3	;SYM
	JRST GTVLE	;LOCAL UNDEF
	JRST GTVL6A	;DEF LOC VAR
	JRST GTVL6	;UNDEF LOC VAR
	JRST GTVLB	;DEF GLO VAR
	JRST GTVLE4	;UNDEF GLO VAR
	JRST GTVL5	;GLO ENTRY
GTVLD:	TLNE I,VAR	;GLO EXIT REACHED BY DISP
	JRST GTVLD1	;MAKE UNDEF GLO VAR
GTVLD4:
GTVLD2:	AOS GLSP1
	IFE SLOW,MOVEI T,ST(D)
	IFN SLOW,MOVEI T,@STD
GTVD2A:	HRRZM T,@GLSP1
GTVLZ:	JRST CABPOP

GTVLD1:	MOVSI T,UDEFGV	;MAKE UNDEF GLO VAR
GTVL1A:	AOS VARCNT
	HRR B,VARCNT
GTV2C1:	TLO C,77
GTVLD3:	JRST GTVL2D

GTVL2:	MOVE C,LEV
	JRST .+1(AA)	;NOT FOUND
	JRST GTVL2P	;NEITHER  MAKE UNDEF LOCAL
	JRST GTVL2C	;GLI  MAKE GLO EXIT
	SKIPA T,[(UDEFLV)]	;VAR   MAKE UNDEF LOC VAR
	MOVSI T,UDEFGV	;VAR+GLI  MAKE UNDEF GLO VAR
	AOS B,VARCNT
GTVL2D:	PUSHJ P,VSM2
	JRST GTVLD2

GTVL2C:	MOVSI T,GLOEXT	;GLO EXIT
	JRST GTV2C1
GTVL2P:	PUSHJ P,GETVUN
MLCUDF:	MOVSI T,LCUDF	;LOCAL UNDEF
	MOVEI B,0
	JRST GTVL2D


GTVPM:	TLZE I,VAR+GLI
	TYPUUO (SIXBIT /ILV/)
	JRST (B)

GTVL1:	TRO I,COM
	HRRZ A,B
	JRST CLBPOP

GTVL3:	TLZE I,VAR
	TYPUUO (SIXBIT /MDV/)	;MDV
	TLNE I,GLI
	JRST GTVL7	;MAKE GLO ENTRY
GTVL8:	MOVE A,B	;USED IN LBRAK
	BYB LDB B,C,\3RLR_18.
	TLNE C,3RLL
	TLO B,1
	POPJ P,

GTVL7:	HRLZI T,GLOETY	;GLO ENTRY
	TLO C,77
	PUSHJ P,VSM2
GTVL5A:	JUMPGE FF,GTVL8G
	TLNN C,3LLV
	TRNE I,PSEUDF+EQLF
	JRST GTVL8G
	TLO SYM,40000
	PUSH P,WRD
	PUSHJ P,OUTDE2
	POP P,WRD
GTVL8G:	TRNN I,EQLF
	TRNN I,PSEUDF
	TLNN C,3REL
GTVL8L:	TLNE C,3LLV
GTVD2:	JRST GTVLD2
	JRST GTVL8


GTVL6A:	PUSHJ P,GTVB1
	TLZN I,GLI	;DEF LOC VAR
	JRST GTVL8L
GTVL6B:	PUSHJ P,PLOGLO
	HRLZI T,DEFGVR	;DEF GLO VAR
	PUSHJ P,VSM2
GTVLB:	PUSH P,GTVD2;DEF GLO VAR
GTVB1:	TRNE FF,PSS
	TLNN I,VAR
	POPJ P,
	TLO C,3VAS2
	JRST VSM3A

GTVL6:	TLNN I,GLI	;UNDEF LOC VAR
	JRST GTVLE4
	PUSHJ P,PLOGLO
	HRLZI T,UDEFGV	;UNDEF GLO VAR
	JRST GTVL2D

GTVL5:	TLZE I,VAR
	TYPUUO (SIXBIT /MDV/)	;MDGV
	TLNE I,GLI
	TLNE C,3VP
	JRST GTVL8G
	JRST GTVL5A

GTVL7A:	TLO SYM,40000
GTVL7B:	TLNE C,3RLL
	TLO SYM,200000
	TLNE C,3RLR
	TLO SYM,100000
	POPJ P,



GTVLP:	TLNE FF,INDEFF
	JRST GTVLP1
	TRNE FF,GLOLOC+GLOFFS
	JRST GTVLP2
	MOVE B,OFRLOC
	ADD B,CRLOC
	MOVE A,CLOC
	SKIPGE BYTM
	HLL A,BYTWP
	ADD A,OFLOC
	TLZ I,FLO+DECP+PERI
	POPJ P,
GTVLP1:	TLNE FF,PPSS
	TYPUUO (SIXBIT /PNI/)
	JRST GTVLZ

GTVLP2:	MOVEI T,$.H	;LOCATION GLOBALAND/OR OFFSET GLOBAL
	AOS GLSP1
	HRRZM T,@GLSP1
	TRNE FF,GLOFFS
	JRST CABPOP
	MOVEI A,0
	SKIPGE BYTM
	HLL A,BYTWP
	ADD A,OFLOC
	MOVE B,OFRLOC
	POPJ P,

$.H:	(GLOETY)+SQUOZE 0,$.

GTVLE:	TLNE C,3LLV
	JRST GTVLD2
	JRST .+1(AA)
	JRST GTVLE4	;NEITHER  CONT AS LOCAL UNDEF
	JRST GTVLE1	;GLI
	JRST GTVLE2	;MAKE LOCAL UNDEF VAR
	PUSHJ P,PLOGLO
	JRST GTVLD1	;MAKE UNDEF GLO VAR

GTVLE2:	PUSHJ P,GETVUN
	HRLZI T,UDEFLV
	JRST GTVL1A
GTVLE1:	PUSHJ P,PLOGLO
	JRST GTVL2C

GTVLE4:	PUSHJ P,GETVUN
	JRST GTVLD2

GETVUN:	TRZ I,DEF
	TRNE I,PSEUDF
	JRST GTVER
	TRNN FF,PSS
	POPJ P,
	SKIPN CONDEP
	TYPUUO (SIXBIT /USW/)
	SKIPE CONDEP
	TYPUUO (SIXBIT /USC/)
GTVER1:	POP P,1(P)
	JRST GTVLZ


	IMPUR
GTVER:	0
	JRST GTVER1
	PUR

PLOGLO:	PUSH P,A
	PUSHJ P,PBITS7
	MOVEI A,CLGLO
	PUSHJ P,PBITS
	PUSHJ P,OUTSM
	JRST POPAJ

DSYL==400000
DFLD==200000
DWRD==100000
DASSEM==40000

DTB:	(DWRD)SPACE	;40  SP
	(DSYL)RRL2	;EXCLAIM
	(DSYL)DQUOTE	;"
	(DFLD)XORF	;NUM SIGN
	0	;DOLLARS
	0	;PERCNT
	(DFLD)ANDF	;AMPERSAND
	(DSYL)SQUOTE	;'
	(DFLD)LEFTP	;(  50
	0	;)
	(DFLD)MULTP	; STAR  TIMES
	(DFLD)PLS	;+  PLUS
	(DWRD)COMMA	; ,
	(DFLD)MINUS	;-
	(DSYL)PERIOD	;
	(DFLD)DIVID	;/
	(DSYL)COLON	;COLON  60
	(DSYL)SEMIC	;SEMI 
	(DFLD)LSSTH	;<
	(DSYL)EQUAL	;=
	0	;>
	0	;?
	(DSYL)ATSGN	;AT SIGN
	(DFLD)LBRAK	;[
	(DFLD)IORF	;BACKSLASH
	(DSYL)RBRAK	;]
	(DSYL)UPARR	;^
	(DSYL)BAKAR	;BACKARR
	0	;CR
	(DWRD)SPACE	;TAB
	0	;ALL OTHER


ES:	SETOB D,ESL1
IFE SLOW,[
	MOVNI TT,SMK
	MOVE B,SYM
	TLZ B,740000
	TSC B,B
	MOVMS B
	IDIVI B,SMK
	ASH C,1
ES4:	SKIPN B,ST(C)
	JRST ES2	;TERM SEARCH
]

;THE FOLLOWING CODE IS FOR LINEAR UNORDERED SYMBOL TABLE.  IT IS USED WHEN
;SLOW IS NON-ZERO.

IFN SLOW,[

	MOVS C,MSMK	;MAKE C AN AOBJN POINTER
ES4:	SKIPN B,@STC	;REACHED 0 SYMBOL?
	JRST ES2	;YES QUIT
]
	TLZ B,740000
	CAME B,SYM
	JRST ES3
	3GET A,C
	MOVE TM,A
	TLZ A,777700
	SKIPN FLDCNT
	JUMPE A,ES3B1
	CAMG A,ESL1
	JRST ES3
ES3B:	MOVEM TM,ESL2
	MOVEM A,ESL1
	MOVEM C,SADR
	TLNN TM,3MAS
	JRST ES2A
ES3:	JUMPN B,ES5
	SKIPGE D	;EXPUNGE
	MOVE D,C
IFE SLOW,[ES5:	ADDI C,2
	CAIL C,2*SMK
	MOVEI C,0
	AOJN TT,ES4
	SKIPL ESL1
	JRST ES2A
	JUMPGE D,ES6A
	ERRUUO (SIXBIT /SCE/)	;SCE
]

;THE FOLLOWING CODE CONTINUES SEARCHING TABLE AND EXTENDS TABLE
;IF OVERFLOW OCCURS

IFN SLOW,[

ES5:	AOS C	;INCR BY ONE
	AOBJN C,ES4	;NOW POINT TO NEXT SYMBOL
	SKIPL ESL1
	JRST ES2A
	JUMPGE D,ES6A

;HERE ZF TABLE FULL

	PUSHJ P,EXTSYM
]


ES2:	SKIPGE ESL1
	JRST ES6
ES2A:	MOVE D,SADR
	IFE SLOW,MOVE A,ST(D)
	IFN SLOW,MOVE A,@STD
	ROT A,4
	ANDI A,17
	IFE SLOW,MOVE B,ST+1(D)
	IFN SLOW,MOVE B,@ST1D
	MOVE C,ESL2
POPJ1:	AOS (P)
	POPJ P,


ES3B1:	MOVSI A,100	;TAKE 0 LEVEL SYM IN LEFT MOST FIELD
	JRST ES3B
IFN SLOW,[
;THIS SUBROUTINE EXTENDS THE SYMBOL TABLE AND MOVES THE 3RDWORD TABLE
;OUT OF THE WAY OF THE WORLD.

EXTSIZ==2000	;AMOUNT OF EXTENSION
3RDLNT==EXTSIZ/5	;AMOUNT OF BLOCK USED FOR 3RD WORD TABLE
3RDMOV==EXTSIZ-3RDLNT-1	;AMOUNT MOVED UP

EXTSYM:	PUSH P,A	;SAVE SOME ACS
	PUSHJ P,EXTCOR	;GROW THE CORE OF THE WORLD (BEGINNING RETURNED IN A)
	HRR B,3RDWRD	;GET OLD POINTER TO 3RDWRD TABLE
	ADDI B,3RDMOV	;COMPUTE WHERE TO MOVE IT
	HRL B,3RDWRD	;SET UP A BLT POINTER
	HRRM B,3RDWRD	;MAKE NEW 3RDWRD
	PUSH P,B	;SAVE FOR ZEROING
	ADDI A,3RDMOV	;A CONTAINS FINAL ADDRESS
	PUSHJ P,BTB	;DO BACKWARD BLT
	POP P,B	;B/ OLD3RD,,NEW3RD
	HLR A,B	;A/ OLD 3RD
	HRLI A,(A)	;OLD 3RD,,OLD 3RD
	SETZM (A)	;CLOBBER FIRST
	AOS A
	BLT A,-1(B)	;ZEROING DONE
	MOVEI B,-3RDMOV/2	;GET NUMBER OF NEW SYMBOLS
	ADDM B,MSMK	;UPDATE NEGATIVE TOTAL
	HRRZS MSMK	;JUST IN CASE
	MOVEI B,3RDMOV	;ALSO UPDATE OTHER POINTERS
	ADDM B,2SMK
	MOVEI B,0
	POP P,A
	POPJ P,

;PROGRAM TO GROW CORE
;HERE TO UPDATE  RANDOM SYMBOL POINTRTS

UPDSPT:	PUSH P,A	;SAVE A
	HRRZ A,SYMPTR	;GET POINTER
	MOVSI TM,-NPTR1
PTLOOP:	HRRM A,STA(TM)
	AOBJN TM,.-1
	MOVSI TM,-NPTR2
	AOS A
PTLOP1:	HRRM A,ST1A(TM)
	AOBJN TM,.-1
	POP P,A
	POPJ P,


]

IFN ITS,[
IFN SLOW\MACGRO,[
EXTCOR:	.SUSET [.RMEMT,,A]	;READ  MEMORY BOUND INTO A
	PUSH P,A
	IDIVI A,2000	;COMPUTE BLOCKS
RECORE:	.CORE 1(A)	;GET OONE ADDITIONAL BLOCK
	JRST MEMERR
	POP P,A
	POPJ P,

;ERROR DUE TO LACK OF CORE

MEMERR:	PUSH P,A	;SAVE A
TYPR [ASCII /NO CORE, TYPE ANY CHARACTER TO RETRY
!/]
	PUSHJ P,TYI	;WAIT HERE
	POP P,A
	JRST RECORE
]
]

IFN SLOW,[

;SUBROUTINE TO SIMULATE BLT BACKWARDS B/ BLT POINTER; A/ FINAL ADDR

BTB:	SETOM FUNPDL	;SET PDL FLAG
	HLRZ TM,B	;TM/ START FROM ADDR
	HRLI A,(A)	;A/ FINAL,,FINAL
	HRRZS B	;B/ START TO ADDR
	SUB A,B	;A/FINAL,,FINAL-INITIAL
	SUB B,TM	;B/ START TO-START FROM
	MOVSS A	;FINAL-INITIAL,,FINAL
	SUB A,B	;FINAL-INITIAL,,FINAL-START TO+START FROM
	HRLI B,A	;B/A,,START TO-START FROM

;BLTING LOOP

BLOOP:	POP A,@B	;NOTE TERMINATION DONE BY PDL OVERFLOW
	JRST BLOOP


]


ES6:	SKIPGE D
	MOVE D,C
ES6A:	MOVEM D,SADR
	IFN SLOW,HRRZS C	;C LH MUST BE 0
	POPJ P,

VSM2LV:	TLOA C,3LLV
VSM2W:	MOVE B,WRD
VSM2:	MOVE CH1,SYM
	TLZ CH1,740000
	IOR T,CH1
	IFE SLOW,MOVEM T,ST(D)
	IFN SLOW,MOVEM T,@STD
IFE SLOW,VSM3:	MOVEM B,ST+1(D)
IFN SLOW,VSM3:	MOVEM B,@ST1D
VSM3A:	3PUT C,D
	POPJ P,


IFE SLOW,[
HASH:	MOVE B,SYM
	TLZ B,740000
	TSC B,B
	MOVMS B
	IDIVI B,SMK
	ASH C,1
	POPJ P,
]

;4.9-4.4 ETC SPECIFY SHIFT
;4.4-3.6 ETC SPECIFY NUMBER BITS

	IMPUR
IFORTB:	2200,,	;NCNCF P 13
	2200000000	;NCFSN
	0
	0
	0
	4400000000	;FSNSN  20
	0	;3 IMPOS
	0
	0
	2200440000	;FSFSN   24
	2200220044	;FSFSF
	270400440000	;FSFCN  26
	2227040044	;FSFCF
	4400000000
	0	;IMPOS
	22220000
	2200002222
	2200440000	;FCFSN  34
	0
	0
	0
FORTAB:	BLOCK 24
FRTBE:	BLOCK 1

	PUR
A.FORMAT:	PUSHJ P,AGETFD
	TYPUUO (SIXBIT /UF1/)
	PUSH P,A
	PUSHJ P,AGETFD
	TYPUUO (SIXBIT /UF2/)
	POP P,B
	MOVEM A,FORTAB-13(B)
	JRST ASSEM1

;GETWORD
GETWRD:	MOVE T,GLSP1
	MOVEM T,GLSP2
	CLEARM FORMAT
	CLEARM WRD
	CLEARM WRDRLC
	TDZ I,[(WRDF)AIOWD
	CLEARM FLDCNT
	MOVE T,[(50100)FORMAT
	MOVEM T,FORPNR
GTWD1:	PUSHJ P,GETFLD
	MOVEI T,1
	MOVE C,CDISP
	TLNE C,DWRD
	JRST (C)	;NO DISPATCH MEANS WD TERM
	MOVE C,GLSP1
	MOVEM C,LINKL
	TRNN I,FLD
	JRST GETWD2	;LAST FIELD NULL
	IDPB T,FORPNR
GTWD4A:	TLO I,WRDF
GTWD4:	MOVE TT,FORMAT
	SKIPN TT,FORTAB-13(TT)	;FORMAT NOT DEFINED
	TYP2UU (SIXBIT /UFM/)
	MOVEM TT,FORMAT
	MOVE T,[(301400)FORMAT
	MOVEM T,FORPNR
GTWD3:	LDB T,FORPNR
	MOVE D,FLDCNT
	CAIG D,2
	IBP FORPNR
	PUSHJ P,INTFLD
	SOSGE FLDCNT
	JRST GTWD5
	POP P,GLSP2
	POP P,GLSP1
	POP P,B
	POP P,A
	JRST GTWD3

GTWD5:	MOVE A,WRD
	MOVE B,WRDRLC
	MOVE C,LINKL
	MOVEM C,GLSP1
	TRZ I,AIOWD
	POPJ P,

SPACE2:	POP P,A
COMMA:	TRNN I,FLD
	JRST COMMA1
	IDPB T,FORPNR
COMMA4:	IDPB T,FORPNR
PUSHFD:	PUSH P,A
	PUSH P,B
	PUSH P,GLSP1
	PUSH P,GLSP2
POPFD1:	AOS FLDCNT
	MOVE TT,GLSP1
	MOVEM TT,GLSP2
	HRRZ T,FORPNR
	CAIN T,FORMAT
	JRST GTWD1
GTWD6:	HRLZI T,440000	;STABILIZE FORPNR
	HLLM T,FORPNR
	JRST GTWD1

GETWD2:	SKIPN FORMAT
	POPJ P,
	SOS FLDCNT
	POP P,GLSP2
	POP P,GLSP1
	POP P,B
	POP P,A
	JRST GTWD4A

COMMA1:	IBP FORPNR
	JRST COMMA4

SPACE:	PUSH P,A
	PUSHJ P,RCH
	CAIE A,11
	CAIN A,40
	JRST .-3
	CAIN A,",
	JRST SPACE2
	POP P,A
	TLO I,UNRCHF
	TRNN I,FLD
	JRST GTWD1
	IDPB T,FORPNR
	IBP FORPNR
	JRST PUSHFD


INTFLD:	TRNE I,AIOWD
	JRST .+4
	MOVE TT,GLSP2
	CAMN TT,GLSP1
	JUMPE B,INTFD1
	CAIN T,2222	;LH
	JRST INTL
	CAIN T,22	;RH
	JRST INTR
	CAIN T,44	;WHOLE WORD
	JRST INTW
	SKIPE B
	TYP2UU (SIXBIT /IRL/)	;RELOCATION ATTEMPTED IN 
;IRRELOCATABLE FIELD
	CAIN T,2704	;HIGH AC
	JRST INTACH
	CAIN T,504	;AC LOW
	JRST INTACL
	JUMPGE FF,INTFD1
	CAME TT,GLSP1
	TYP2UU (SIXBIT /IGS/)	;GLOB SYM APPEARS IN ILLEGAL FIELD
INTFD1:	MOVEI TT,C_12.
	ROTC T,-12.
	MOVEI C,0
	DPB A,TT
	MOVE A,C
	CAMN TT,[(2200)C
	JRST INTFD2
	ADDM A,WRD
INTFD3:	MOVE A,WRDRLC
	PUSHJ P,HFWDAD
	MOVEM A,WRDRLC
	POPJ P,

INTFD2:	ADD A,WRD
	HRRM A,WRD
	JRST INTFD3

INTL:	HRLZ D,B
	HRLZI B,SWAPF
INTR1:	PUSH P,T
	HRLZI C,HFWDF
	PUSHJ P,LNKTC1
PRTCL:	MOVE B,D
	POP P,T
INTW:	MOVE D,GLSP2
	HRLOI LINK,377777
	CAML D,GLSP1
	JRST INTFD1
	ANDM LINK,1(D)
	AOJA D,.-3


INTR:	HRRZ D,B
	MOVEI B,0
	JRST INTR1

INTACL:	TDZA B,B
INTACH:	HRLZI B,SWAPF
INTAC1:	TRNE I,AIOWD	;IO DEVICE DIDDLE
	LSH A,1
	TRNE I,AIOWD
	ADDI T,6
	HRLZI C,ACF
	PUSH P,T
	PUSHJ P,LNKTC1
	MOVEI B,0
	JRST PRTCL+1



PBITS3:	PUSH P,A
	MOVEI A,14
	MOVEM A,PBITS2
	MOVE A,[(440300)PBITS1
	MOVEM A,BITP
	MOVE A,PBITS1
	MOVEM A,@PBITS4
	AOS A,OPT1
	TRNN FF,BITF
	SOSA A
	TRO FF,INVTF
	HRRZM A,PBITS4
	POP P,A
	CLEARM PBITS1
PBITS:	SKIPE CONTRL
	POPJ P,
	SOSGE PBITS2
	JRST PBITS3
	TRZ FF,BITF
	CAIN A,7
	TRO FF,BITF
	IDPB A,BITP
	POPJ P,

OUTSM:	SKIPA A,SYM
OUTWD:	MOVE A,WRD
OUTPUT:	SKIPE CONTRL
	POPJ P,		;DO NOTHING IF ABSOLUTE ASSEMBLY
	PUSH P,AA
	MOVE AA,OPT1
	TRZN FF,INVTF
	AOS AA
	MOVEM A,-1(AA)
	MOVE A,CLOC
	TRZE FF,FIRWD
	HRRM A,BKBUF
	POP P,AA
	AOS A,OPT1
	CAIL A,BSIZE+BKBUF
	TRNE I,CONT
	POPJ P,
		;MAY DROP THROUGH

		;END CURRENT OUTPUT BLOCK

EBLK:	PUSH P,T
	PUSH P,TT
	PUSH P,A
	PUSH P,B
	MOVE T,CONTRL
	JUMPE T,EBLK3	;JUMP IF RELOCATABLE ASSEMBLY
	TRNN T,SBLKS
	TRNE T,ARIM10
	JRST ESBLK
	JRST EBLK5

EBLK3:	MOVE T,PBITS1
	MOVEM T,@PBITS4
	MOVEI T,PBITS4
	MOVEM T,PBITS4
	MOVE T,[(440300)PBITS1
	MOVEM T,BITP
	CLEARB TT,PBITS2
	CLEARM PBITS1
	MOVEI T,BKBUF
	MOVE B,OPT1	;GET POINTER TO END OF BLOCK
	SUBI B,BKBUF+1	;CONVERT TO # WORDS IN BLOCK (EXCLUDING HEADER)
	DPB B,[(220700)BKBUF]	;SET COUNT FIELD IN HEADER
	TRNE FF,FIRWD
	JUMPLE B,EBLK4
	TRZ FF,LOCF
	PUSHJ P,FEED
EBK1:	CAML T,OPT1	;DONE WITH BLOCK?
	JRST EBK2	;YES
	MOVE A,(T)	;NO, GET DATA WORD
	JFCL 4,.+1	;UPDATE CHECKSUM
	ADD TT,A
	JFCL 4,[AOJA TT,.+1]
	PUSHJ P,PPB	;OUTPUT WORD
	AOJA T,EBK1
EBK2:	SETCM A,TT	;DONE OUTPUTTING BLOCK, NOW GET CHECKSUM
	PUSHJ P,PPB	;OUTPUT CHECKSUM
	MOVE T,CDATBC	;GET BLOCK TYPE
	DPB T,[(310700)BKBUF]	;SET NE T BLOCK TYPE TO THIS BLOCK TYPE
	MOVEI T,BKBUF+1
	MOVEM T,OPT1
EBLK4:	TLO FF,OUTF	;INDICATE THAT OUTPUT HAS OCCURED (FOR 1PASS MULTIPLE-ASSEMBLY HACKING)
EBLK5:	TRO FF,FIRWD
	POP P,B
	POP P,A
	POP P,TT
	POP P,T
	POPJ P,

ALOC3A:	SKIPN CONTRL
	TRZN FF,LOCF
	POPJ P,
	TRZ FF,FIRWD
	JRST EBLK

PWRDA:	TROA FF,NLIKF
PWRD:	TRZ FF,NLIKF
	JUMPGE FF,CPOPJ
IFN LISTSW,[
	SKIPGE LISTPF
	PUSHJ P,PNTR
	SETOM LISTPF
	MOVE LINK,WRD
	MOVEM LINK,LISTWD
	MOVE LINK,WRDRLC
	MOVEM LINK,LSTRLC
	MOVE LINK,CLOC
	MOVEM LINK,LISTAD
	MOVE LINK,CRLOC
	DPB LINK,[220100,,LISTAD]
]
	SKIPE LINK,CONTRL
	JRST PWRD1
	PUSHJ P,RCHK
	MOVE A,GLSP2
	CAMN A,GLSP1
	JRST PWRD2	;NO GLOBALS
	HRLZ B,WRD
	HRR B,WRDRLC
	JUMPN B,PWRD3
	MOVEI T,0

PWRD4:	CAML A,GLSP1
	JRST PWRD5
	HRRZ TT,1(A)
	JUMPE TT,PWRD7A
	LDB TT,[(400400+TT)
	CAIE TT,DEFGVR_-14.
	CAIN TT,GLOETY_-14.
	JRST PWRD3
	HLRZ TT,1(A)
	TRNN FF,GLOLOC+NLIKF
	TRNE TT,1777
	JRST PWRD3
	SKIPN LDCCC
	TRNE TT,MINF
	JRST PWRD3
	TRNE TT,HFWDF
	JRST PWRD7
	TRNE TT,ACF
	TRNN TT,SWAPF
	JRST PWRD3
PWRD7A:	AOJA A,PWRD4
PWRD7:	TRNE TT,SWAPF
	AOJA A,PWRD4
	MOVEI D,1(A)
	AOS T
	AOJA A,PWRD4

PWRD5:	SOJN T,PWRD3
	HRRZ T,(D)	;HOORAY WE CAN ADR LINK
	IFE SLOW,CAIL T,2*SMK+ST
	IFN SLOW,PUSHJ P,CNSTCH	;SKIP IF REALLY IN CONSTANT TABLE
	JRST PWRD3
	PUSH P,T
PWRD5E:	CAMLE D,GLSP1
	JRST PWRD5B
	MOVE A,1(D)
	MOVEM A,(D)
	AOJA D,PWRD5E
PWRD5B:	SOS GLSP1
	PUSHJ P,PWRD31
	POP P,D
	3GET1 A,D
	BYB LDB A,A,\3RLNK_18.
	MOVE B,WRDRLC
	TLNE B,1
	TRO A,2
	PUSHJ P,PBITS
	HLR A,1(D)
	HLL A,WRD

	PUSHJ P,OUTPUT
	MOVE A,CLOC
	HRLM A,1(D)
	MOVE A,CRLOC
	3GET1 B,D
	TLZ B,3RLNK	;RLC OF LINK PNR
	JUMPE A,.+2
	TLO B,3RLNK
	3PUT1 B,D
	POPJ P,

PWRD31:	MOVE T,GLSP2	;DUMP ALL GLO S IN GENERAL FORMAT
PWRD3A:	CAML T,GLSP1
	POPJ P,
	MOVE B,1(T)
	TRNN B,-1
	AOJA T,PWRD3A
	TLNE B,1777
	JRST RPWRD	;REPEAT
RPWRD1:	BYB LDB A,B,\MINF_18.
	TRO A,4
	PUSHJ P,PBITS
	MOVE  A,(B)	;CODEBITS +SQUOZE FOR SYM
	HLRZ C,A
	TLZ A,740000
	CAIL C,DEFGVR
	TLO A,40000	;SYM IS GLO
PWRD3D:	TLNE B,SWAPF
	TLO A,400000
	TLNE B,ACF
	JRST PWRD3E	;AC HIGH OR LOW
	TLNN B,HFWDF
	JRST PWRD3F	;ALL THROUGH
	TLO A,100000
	TLNE B,SWAPF
	TLC A,300000
PWRD3F:	PUSHJ P,OUTPUT
	AOJA T,PWRD3A



RPWRD:	PUSHJ P,PBITS7
	MOVEI A,CRPT
	PUSHJ P,PBITS
	LDB A,[(221200)B
	PUSHJ P,OUTPUT
	JRST RPWRD1

PWRD3E:	TLO A,300000
	JRST PWRD3F

PWRD3:	PUSHJ P,PWRD31
PWRD2:	PUSHJ P,RCHK
	HRRZ A,B
	DPB C,[(10100)A
	PUSHJ P,PBITS
	JRST OUTWD


RCHK:	HRRZ B,WRDRLC
	HLRZ C,WRDRLC
	CAIE B,1
	JUMPN B,RLCERR
	CAIE C,1	;OK
	JUMPN C,RLCERR
	POPJ P,
RLCERR:	TYPUUO (SIXBIT /IRC/)
	POPJ P,


RMOVE:	MOVEI T,0
	DPB C,[(430100)T
	DPB B,[420100,,T
	MOVE C,T
	POPJ P,


PWRD1:	MOVE A,GLSP1
	CAME A,GLSP2
	TYP2UU (SIXBIT /ILA/)	;GLOBALS APPEARING ILLEGALLY
	SKIPE WRDRLC
	TYP2UU (SIXBIT /IRA/)
	TRNE LINK,ARIM
	JRST PRIM
	TRNE LINK,ARIM1
	JRST PRIM1
SBLKS1:	MOVE A,WRD
	MOVEM A,@OPT1
	MOVE A,CLOC
	TRZE FF,FIRWD
	MOVEM A,BKBUF
	AOS A,OPT1
	CAIGE A,BKBUF+BSIZE
	POPJ P,


SBLKS2:	SUBI A,BKBUF+1
	JUMPE A,CPOPJ
	MOVNS A
	HRLM A,BKBUF
	PUSHJ P,FEED
	MOVEI T,BKBUF
	CLEARM SCKSUM
SBLK1:	CAML T,OPT1
	JRST SBLK2
	MOVE A,SCKSUM
	ROT A,1
	ADD A,(T)
	MOVEM A,SCKSUM
	MOVE A,(T)
	PUSHJ P,PPB
	AOJA T,SBLK1

SBLK2:	TRO FF,FIRWD
	MOVEI A,BKBUF+1
	MOVEM A,OPT1
	MOVE A,SCKSUM
	JRST PPB

ESBLK:	MOVE A,OPT1
	PUSHJ P,SBLKS2
	JRST EBLK4

PRIM:	MOVSI A,(DATAI PTR,)
	HRR A,CLOC
	PUSHJ P,PPB
	MOVE A,WRD
	JRST PPB

PRIM1:	MOVSI A,(HRRI 17,)
	HRR A,WRD
	PUSHJ P,PPB
	MOVSI A,(HRRM 17,)
	HRR A,CLOC
	PUSHJ P,PPB
	MOVSI A,(HRRI 17,)
	HLR A,WRD
	PUSHJ P,PPB
	MOVSI A,(HRLM 17,)
	HRR A,CLOC
	JRST PPB

A.LIFND:	MOVEI A,LTCN	;LOAD TIME COND ON NEED
	JRST LIB9	;JOIN .LIFS,.LIBRA CODE
A.LIFS:	SKIPA A,[LTCP
A.LIB:	MOVEI A,LLIB
LIB9:	HRRM A,LIB3
LIB4:	CLEARM LIBOP
	PUSHJ P,EBLK
LIB1:	PUSHJ P,GETSYL
	TRNN I,SYL
	JRST LIB2
	IOR SYM,LIBOP
	TLO SYM,40000
	PUSHJ P,OUTSM
	MOVSI A,400000
	ANDCAM A,LIBOP
LIB2:	MOVE B,LIMBO1
	CAIE B,12
	CAIN B,15
	JRST LIB3
	MOVE A,LIBOP
	CAIN B,",
	MOVSI A,400000
	CAIN B,"+
	TLZ A,200000
	CAIN B,"-
	TLO A,200000
	MOVEM A,LIBOP
	JRST LIB1
	IMPUR


LIB3:	MOVEI A,LLIB	;OR LTCP
	DPB A,[(310700)BKBUF
	PUSHJ P,EBLK
	CAIN A,LLIB
	JRST ARELC1
	JRST LIB5
LIBOP:	0
	PUR


A.ELDC:	PUSHJ P,EBLK
	MOVEI A,ELTCB
	DPB A,[(310700)BKBUF
	TRZ FF,FIRWD
	PUSHJ P,EBLK
	SOSGE LDCCC
	CLEARM LDCCC
	JRST ASSEM1

A.LDCV:	LSH B,-27.
	PUSH P,B
	PUSHJ P,EBLK
	MOVE A,[TYPUUO (SIXBIT/ULC/)
	MOVEM A,GTVER
	TRO I,PSEUDF
	PUSHJ P,GETWRD
	POP P,B
	DPB B,[(400300)BKBUF
	MOVEI A,LDCV
	PUSHJ P,PLDCM
	MOVEI A,0
	DPB A,[(400300)BKBUF
LIB5:	AOS LDCCC
CCASM1:	JRST ASSEM1

AEND:
IFN LISTSW,[
	MOVE A,[440700,,LISTBF]
	EXCH A,PNTBP
	MOVEM A,LISTTM
]
	PUSHJ P,AVARIAB+1
	PUSHJ P,CNSTNT+1
	SKIPN A,CONTRL
	PUSHJ P,AEND5
AEND5D:	TRNN FF,PSS	;IF PASS 2,
	TRNN FF,NPSS	;OR IF 1PASS ASSEMBLY,
	JRST AEND1	;THEN MUCH HAIR, (IF RELOCATABLE THEN EVENTUALLY GOES TO AEND3)
AEND4:	PUSHJ P,GETWRD	;OTHERWISE EAT UP WORD,
	JRST RETURN	;AND RETURN


AEND3:	HRRZ A,CLOC
	HRRM A,BKBUF
	TRZ FF,FIRWD
	PUSHJ P,EBLK
	MOVEI A,LCJMP	;PASS ONE OF ONE
	PUSHJ P,PLDCM
	JRST AEND2

PS2":	HRRM A,RETURN
	JUMPL FF,PA2A
	TDO FF,[(PPSS)PSS
	TRZ FF,GLOLOC
	PUSHJ P,P2INI
	JRST ASSEM1

IFE SLOW,PA2A:	MOVSI A,-SMK	;PASS 2 OF 1
IFN SLOW,PA2A:	MOVS A,MSMK
IFE SLOW,PA2C:	MOVE SYM,ST(A)
IFN SLOW,PA2C:	MOVE SYM,@STA
	LDB B,[(400400)SYM
	CAIE B,LCUDF_-14.
	JRST PA2B
	3GET C,A
	TLZ SYM,740000
	TLNN C,3LLV
	TYPUUO (SIXBIT /USW/)
PA2B:	AOS A
	AOBJN A,PA2C
	JRST RETURN


PLDCM:	TRZ FF,FIRWD
	HRRM A,BKBUF
	MOVEI A,LLDCM
	DPB A,[(310700)BKBUF
	PUSHJ P,PWRDA
	JRST EBLK

PLOD":	HRRM A,RETURN
	PUSHJ P,PLOD1
	JRST RETURN

PLOD1:	PUSHJ P,FEED1
	MOVE B,CONTRL
	TRNE B,ARIM10
	JRST PLOD2
	TRNN B,SBLKS
	POPJ P,
PLOD1A:	MOVSI B,SLOAD-SLOADP
	MOVSI C,(DATAI PTR,)
PLOAD1:	MOVE A,C
	PUSHJ P,PPBA
	CAMN C,[DATAI PTR,13
	HRRI C,27
	MOVE A,SLOAD(B)
	PUSHJ P,PPBA
	AOS C
	AOBJN B,PLOAD1
	MOVE A,[JRST 1
	PUSHJ P, PPBA
	JRST FEED1

A.SLDR:	PUSHJ P,FEED1
	PUSHJ P,PLOD1A
	JRST SIMBLK

PLOD2:	MOVSI C,LDR10-ELDR10
PLOD3:	MOVE A,LDR10(C)
	PUSHJ P,PPBA
	AOBJN C,PLOD3
	JRST FEED1

AEND1:	PUSHJ P,EBLK
	TRO I,PSEUDF+DEF
	MOVE A,[TYPUUO (SIXBIT /USE/)
	MOVEM A,GTVER
IFN LISTSW,[
	SKIPGE LISTPF
	PUSHJ P,PNTR
	MOVE A,LISTTM
	MOVEM A,PNTBP
]
	PUSHJ P,GETWRD
IFN LISTSW,[
	MOVEM A,LISTWD
	MOVEM B,LSTRLC
	SETOM LISTAD
	SETOM LISTPF
	PUSHJ P,PNTR
	CLEARM LISTF
]
	SKIPN CONTRL
	JRST AEND3
	TLNN A,777000	;CHECK INSTRUCTION PART
	ADD A,[JRST]	;INSTRUCTION PART 0; HE WANTS JRST
	PUSHJ P,PPB
	JUMPG A,.+3
	TYPUUO (SIXBIT /EWN/)	;END WORD NEGATIVE
	HRLI A,(JRST)	;END SYMTAB WITH POSITIVE WORD
	MOVEM A,STARTA	;SAVE FOR PUNCHOUT AT END OF SYMTAB
	PUSHJ P,FEED1
AEND2:	PUSHJ P,CNARTP
	JRST RETURN

A.LNKOT:	PUSH P,CCASM1

AEND5:	JUMPGE FF,CPOPJ
	IFE SLOW,MOVSI D,-SMK
	IFN SLOW,MOVS D,MSMK
IFE SLOW,AEND5A:	MOVE SYM,ST(D)
IFN SLOW,AEND5A:	MOVE SYM,@STD
	LDB T,[(400400)SYM
	CAIE T,DEFLVR_-14.
	CAIN T,DEFGVR_-14.
	JRST AEND5E
	CAIE T,LCUDF_-14.
	CAIN T,GLOEXT_-14.
	JRST AEND5B
AEND5C:	AOS D
	AOBJN D,AEND5A
	POPJ P,

AEND5E:	3GET C,D
	TLNN C,3LLV
	JRST AEND5C
IFE SLOW,AEND5B:	HLLZ B,ST+1(D)
IFN SLOW,AEND5B:	HLLZ B,@ST1D
	3GET C,D
	TLNN C,3RLNK
	JUMPE B,AEND5C
	TLZ SYM,740000
	CAIE T,LCUDF_-14.
	CAIN T,DEFLVR_-14.
	SKIPA
	TLO SYM,40000
	PUSHJ P,LKPNRO
	IFE SLOW,HRRZS ST+1(D)
	IFN SLOW,HRRZS @ST1D
	TLZ C,3RLNK	;DONT RELINK
	3PUT C,D
	JRST AEND5C

ALOC:	CLEARM BLOCKF
	TLZ FF,INDEFF
	TRO I,PSEUDF+DEF
	PUSHJ P,EBLK
	MOVE A,[TYPUUO (SIXBIT /USL/)
ABLK1:	MOVEM A,GTVER
	MOVEI A,",
	MOVEM A,LIMBO1
	TLO I,UNRCHF
	PUSHJ P,GETWRD
	SKIPE BLOCKF
	JRST ABLK2
	CLEARM SYLOC
	CLEARM SYSYM
ABLK3:	TRNN I,DEF
	JRST ALOC1
	MOVE T,GLSP2
	CAME T,GLSP1
	JRST ALOC3
	TRZE FF,GLOLOC
	JRST ALOC5
ALOC2:	TRO FF,LOCF
	HRRZM A,CLOC
	HRRZM A,BKBUF
	TDNE B,[-2
	TYP2UU (SIXBIT /LAI/)
	HRRZM B,CRLOC
	MOVEI A,2(B)
ALOC4:	DPB A,[(310700)BKBUF	;LABS OR LREL
	MOVEM A,CDATBC
	JRST ASSEM1

ALOC1:	TLO FF,INDEFF
	MOVE A,[SQUOZE 0,INDEFF
	MOVEM A,SYSYM
	MOVE A,CLOC
	MOVEM A,SYLOC
	TLNE FF,PPSS
	TYPUUO (SIXBIT /USL/)
	JRST ASSEM1


AXWORD:	PUSHJ P,GETFLD
	MOVE C,CDISP
	CAIN C,COMMA
	JRST .+3
	TRNN I,FLD
	JRST AXWORD
	HRLM A,WRD
	HRLM B,WRDRLC
	MOVSI C,HFWDF
	MOVSI B,SWAPF
	PUSHJ P,LNKTC1
	PUSH P,GLSP1
	PUSHJ P,GETFLD
	TRNN I,FLD
	JRST .-2
	HRRM A,WRD
	HRRM B,WRDRLC
	MOVSI C,HFWDF
	MOVEI B,0
	POP P,T
	PUSHJ P,LINKTC
	JRST CABPOP

ALOC3:	PUSHJ P,ALOC3A
	MOVEI A,LCGLO
	PUSHJ P,PLDCM
	CLEARM CLOC
	MOVEI T,1
	MOVEM T,CRLOC
	TRO FF,GLOLOC
AREL1:	MOVEI A,LREL
	JRST ALOC4

ALOC5:	PUSH P,A
	TRZ FF,FIRWD
	MOVEI A,LCEGLO
	PUSHJ P,PLDCM
	POP P,A
	JRST ALOC2
	
A.LENGTH:	MOVE A,LIMBO1
	CAIN A,40
	PUSHJ P,RCH
	MOVEM A,A.LN1
	MOVEI B,0
	PUSHJ P,RCH
	CAME A,A.LN1
	AOJA B,.-2
	MOVE A,B
	JRST VALRET
	IMPUR
A.LN1:	0
	PUR

ABLOCK:	TRO I,PSEUDF+DEF
	PUSHJ P,EBLK
	MOVE A,[TYPUUO (SIXBIT /USB/)
	SETOM BLOCKF
	JRST ABLK1

ABLK2:	TRNE FF,GLOLOC
	JRST ABLK4
ABLK5B:	MOVE A,CLOC
	ADDB A,WRD
	MOVE B,CRLOC
	ADDB B,WRDRLC
	JRST ABLK3
ABLK4:	MOVEI T,$.H
	AOS GLSP1
	HRRZM T,@GLSP1
	JRST ABLK3


ANOSYMS:	TRZ FF,IPSYMS
	JRST ASSEM1

AEXPUNG:	PUSHJ P,GETSYL
	PUSHJ P,ES
	JRST .+3
	HRLZI A,400000	;EXPUNGED ZERO SYM
	IFE SLOW,MOVEM A,ST(D)
	IFN SLOW,MOVEM A,@STD
	MOVE A,LIMBO1
	CAIE A,15
	CAIN A,12
	JRST ASSEM1
	JRST AEXPUNG


	IMPUR

ERROR:	0
	JRST ERRORP

	PUR

ERRORP:	PUSH P,T
	LDB T,[(331100)40]
	CAIN T,<TYPR_<-27.>>
	JRST TYPR1
	SUBI T,1
	PUSH P,B
	PUSH P,A
	HRRZ T,40
	CAIE T,(SIXBIT /MDT/)
	CAIN T,(SIXBIT /RES/)
	SKIPA T,SYSYM1
	JRST .+4
	MOVEM T,SYSYM
	MOVE T,SYLOC1
	MOVEM T,SYLOC
	MOVE A,SYSYM
	JUMPE A,ERR1
	PUSHJ P,SYMTYP
	MOVE B,CLOC
	SUB B,SYLOC
	JUMPE B,.+4
	MOVEI A,"+
	PUSHJ P,TYO
	PUSHJ P,OCTPNT

ERR1:	PUSHJ P,TAB"
	PUSH P,ERROR
	PUSH P,40
	TRNE FF,GLOLOC
	TYPR [ASCII /GLOBAL+!/
	POP P,40
	POP P,ERROR
	MOVE B,CLOC
	PUSHJ P,OCTPNT
	PUSHJ P,TAB
	MOVE B,MDEPTH
	PUSHJ P,OCTPNT
	PUSHJ P,TAB
	MOVE A,CPGN
	PUSHJ P,DPNT
	PUSHJ P,TAB
	MOVE A,CLNN
	PUSHJ P,DPNT
	PUSHJ P,TAB
	IFN ITS,.SUSET [.SPICL,,[-1]]	;RE-ENABLE INTERRUPTS
	LDB A,[(331100) 40
	CAIGE A,8	;ERROR UUO MAX
	JRST .(A)
	JRST IAE	;NO RECV
	JRST ERR2	;SYM+MESS
	JRST ERR3	;MESS
	JRST 4,.	;TECO ERR
	JRST ERR4	;IGNORE LINE RET TO ASSEM1
	JRST ERR5	;RET TO ASSEM1
	JUMPL FF,ERR2	;SYM ON PPSS
	JRST ERRRET+1

SYMTYP:	IDIVI A,50
	HRLM B,(P)
	JUMPE A,.+2
	PUSHJ P,.-3
	HLRZ A,(P)
	JUMPE A,CPOPJ
	CAIL A,45
	JRST SYMTP1
	CAIL A,13
	ADDI A,7
	ADDI A,257
	JRST TYO

SYMTP1:	MOVE A,SYTB-45(A)
	JRST TYO"

SYTB:	".
	"$
	"%

DPNT:	IDIVI A,10.
	HRLM B,(P)
	SKIPE A
	PUSHJ P,DPNT
	JRST DPNT1

OCTPNT:	HRRZ A,B
	IDIVI A,10
	HRLM B,(P)
	JUMPE A,.+2
	PUSHJ P,.-3
DPNT1:	HLRZ A,(P)
	ADDI A,260
	JRST TYO


ERR4:	PUSHJ P,RCH
	CAIE A,12
	JRST .-2

ERR5:	MOVEI A,ASSEM1
	MOVEM A,ERROR
ERR2:	MOVE A,SYM
	PUSHJ P,SYMTYP
	PUSHJ P,TAB
ERR3:	HRLZ B,40
	PUSHJ P,SIXTYO
ERRRET:	PUSHJ P,CRR
	POP P,A
	POP P,B
TYPR1A:	POP P,T
	JRST 2,@ERROR

SIXTYO:	JUMPE B, CPOPJ
	MOVEI A,0
	ROTC A,6
	ADDI A,40
	PUSHJ P,TYO
	JRST SIXTYO

CRR:	MOVEI A,15
	PUSHJ P,TYO
	MOVEI A,12
	JRST TYO

IAE:	HRLZ B,40
	PUSHJ P,SIXTYO
	PUSHJ P,TAB
	SOS B,ERROR	;MAKE OUTPUT POINT TO ERROR INSTRUCTION
	PUSHJ P,OCTPNT
	JRST GO2"

TYPR1:	HRLZI B,440700
	HRR B,40
	ILDB A,B
	CAIN A,"!
	JRST TYPR1A
	PUSHJ P,TYO
	JRST .-4

PS1":	HRRM A,RETURN
SIMBLK:	SKIPA A,[SBLKS
SRIM1:	MOVEI A,ARIM1
	TRO FF,NPSS
	TLO A,400000
	MOVEM A,CONTRL
	IFN A1PSW,PUSHJ P,OUTUPD
	JRST ASSEM1

SRIM10:	SKIPA A,[ARIM10]
SRIM:	MOVEI A,ARIM
	JRST SIMBLK+2

AEXP:	TRZ I,SYL+LET
CABPOP:	CLEARB A,B
	POPJ P,

ATITLE:	MOVEI A,15
	TRNN FF,PSS
	PUSHJ P,TYO
	PUSHJ P,GSYL
	SKIPE SYM
	MOVEM SYM,PRGNM
	MOVE T,[(440700)STRSTO
ATIT2:	SOSGE STRCNT
	JRST ATIT1
	ILDB A,T
	PUSHJ P,TYO
	JRST ATIT2
ATIT1:	CAIN A,12
	JRST ASSEM1
	PUSHJ P,RCH
	PUSHJ P,TYO
	JRST ATIT1

ASBEG:	HRLZI A,1
	ADDM A,LEV
	JRST ASSEM1

ASEND:	MOVSI TT,400000
	IFE SLOW,HRLZI T,-SMK
	IFN SLOW,HRLZ T,MSMK
IFE SLOW,ASEND2:	SKIPE B,ST(T)
IFN SLOW,ASEND2:	SKIPE B,@STT
	TDNN B,[( 37777)777777
	JRST ASEND1
	3GET B,T
	TLZ B,777700
	CAIN B,77
	JRST ASEND1
	CAMN B,LEV
	IFE SLOW,MOVEM TT,ST(T)
	IFN SLOW,MOVEM TT,@STT
ASEND1:	AOS T
	AOBJN T,ASEND2
	HRLZI A,-1
	ADDM A,LEV
	JRST ASSEM1

IOINST:	HLLZ A,B
	SKIPN FLDCNT
	TRO I,AIOWD
CLBPOP:	MOVEI B,0
CPOPJ:	POPJ P,

;THIS CODE PUTS OUT SYMBOLS AND IF IT IS A RELOCATABLE ASSEMBLY
;PUTS OUT A BLOCK OF SYMBOLS THAT SHOULD BE HALF-KILLED IN DDT


SYMDMP:	MOVEI A,FRSTDS	;GET ADR OF FIRST DISPATCH TABLE
	MOVEI B,LDDSYM	;GET RIGHT BLOCK TYPE
RESYM:	HRRM A,DISPT	;INTO DISPATCH CONTROL WORD
	TRZ I,CONT	;OK TO END BLOCK
	CLEARM GLSP1
	CLEARM GLSP2
	CLEARM WRDRLC
	MOVE T,CONTRL
	MOVEI A,BKBUF+1
	MOVEM A,OPT1
	IFE SLOW,MOVSI AA,-SMK
	IFN SLOW,MOVS AA,MSMK
SYMD5:	CLEARM CLOC
	CLEARM BKBUF
	JUMPN T,SYMD4	;JUMP IF NOT RELOCATABLE
	DPB B,[(310700)BKBUF	;SET BLOCK TYPE
	MOVEM B,CDATBC
IFE SLOW,SYMD4:	SKIPE B,ST(AA)	;GET SYMTAB ENTRY
IFN SLOW,SYMD4:	SKIPE B,@STAA
	TDNN B,[37777,,-1]	;IF SQUOZE IS NULL,
	JRST SYMD1	;THEN INCREMENT POINTER (AA) AND TRY AGAIN
	MOVEI C,0
	ROTC B,4	;SHIGT FLAGS INTO C
	IFE SLOW,MOVE D,ST+1(AA)	;GET WHAT MAT BE VALUE
	IFN SLOW,MOVE D,ST1AA@
	3GET CH1,AA
	JRST @DISPT	;DISPATCH ON FLAGS
FRSTDS:	JRST SYMD1	;COM
	JRST SYMD1	;PSEUDO OR MACRO
	JRST SYMD6	;SYM
	JRST SYMD1	;LOCAL UNDEFINED
	JRST SYMD3	;DEFINED LOCAL VARIABLE
	JRST SYMD1	;UNDEFINED LOCAL VARIABLE
	JRST SYMD3A	;DEF GLO VAR, IGNORE IF RELOCATABLE ASSEMBLY (ALREADY OUTPUT)
	JRST SYMD1	;UNDEFINED GLOBAL VARIABLE
	JRST SYMD2A	;GLO ENTRY, IGNORE IF RELOCATABLE ASSEMBLY
	JRST SYMD1	;GLO EXIT

SYMD3A:	JUMPE T,SYMD1	;DEFINED GLOBAL VARIABLE, IGNORE IF RELOCATABLE ASSEMBLY
SYMD3:	HRRZS D		;DEFINED LOCAL VA5IABLE, TRUNCATE TO HALFWORD
SYMD2:	TLNE CH1,3LLV	;IF LOADER MUST INSERT VALUE
	JRST SYMD1	;THEN IGNORE
	ROT B,-4	;SHIFT SQUOZE INTO ALIGNMENT, WITH FLAGS CLEARED
	JUMPE T,SYMD2B	;JUMP IF RELOCATABLE ASSEMBLY
	TLO B,40000	;ABSOLUTE ASSEMBLY, DEFINE SYM AS GLOBAL
	TLNE CH1,3SKILL	;IF SEMI-KILLING DESIRED
	TLO B,400000	;THEN SET DELETE OUTPUT BIT
	MOVEM B,WRD
	PUSHJ P,PWRD	;OUTPUT THE SYM
	MOVEM D,WRD
	PUSHJ P,PWRD	;OUTPUT THE VALUE
SYMD1:	AOS AA		;GO GET NEXT SYM (MAIN LOOP POINT)
	AOBJN AA,SYMD4
	PUSHJ P,EBLK	;DONE PUNCHING SYMS, END CURRENT BLOCK
	JUMPE T,SYMDA	;IF RELOCATABLE ASSEMBLY THEN PUNCH PROGRAM NAME AND RETURN
	MOVE A,STARTA	;RETRIEVE JRST BLOCK WORD
	JRST PPB	;OUTPUT IT AND RETURN

SYMD2A:	JUMPN T,SYMD2
	JRST SYMD1

		;PUNCH OUT LOCAL SYM (RELOCATABLE ASSEMBLY)

SYMD2B:	TLNE CH1,3RLL
	TLO B,200000	;RELOCATE LEFT HALF
	TLNE CH1,3RLR
	TLO B,100000	;RELOCATE RIGHT HALF
	TLNE CH1,3SKILL
	TLO B,400000	;HALF-KILL
	MOVE A,B
	PUSHJ P,OUTPUT	;OUTPUT SYM
	MOVE A,D
	PUSHJ P,OUTPUT	;OUTPUT VALUE
	JRST SYMD1

IFN A1PSW,[
	;ROUTINE TO SET VARIABLES FOR BENEFIT OF NED LOGIC
	;CALLED BY OUTPUT SELECTING PSEUDOS OTHER THAN 1PASS AND .LIBRA

OUTUPD:	TRNN FF,PSS	;IF PASS 1,
	TLZ FF,PPSS	;THEN INDICATE NOT PUNCHING PASS
	TROE FF,NPSS
	TLNN FF,OUTF
	JRST OUTCHK
	AOS OUTN1	;INDICATE "OUTPUT" HAS OCCURED OTHER THAN IN 1PASS MODE
OUTCHK:	TLZE FF,OUTF
	AOS OUTC	;INDICATE "OUTPUT" HAS OCCURED DURING CURRENT ASSEMBLY
	POPJ P,
]

SYMD6:	TLNN CH1,77
	JRST SYMD1
	JRST SYMD2

CNARTP:	MOVNI D,1
CNTP1:	MOVEI TT,PCNTB
	CAML TT,PBCONL
	POPJ P,
	HRRZ B,1(TT)
	HLRZ A,1(TT)
	CAMN A,B
	JRST CNTP2
	AOSN D
	TYPR [ASCII /CONSTANTS AREA INCLUSIVE
FROM   TO
!/
	BYB LDB B,2(TT),\CGBAL_18.
	SKIPE B
	TYPR [ASCII /GLOBAL+!/
	HRRZ B,1(TT)
	PUSHJ P,OCTPNT
	PUSHJ P,TAB
	HLRZ B,1(TT)
	SOS B
	PUSHJ P,OCTPNT
	PUSHJ P,CRR
CNTP2:	ADDI TT,3
	JRST CNTP1+1

SYMDA:	MOVE B,CDATBC	;GET BLOCK TYPE
	CAIN B,LDHLFK	;HAVE W OUTPUT "HALF KILL BLOCK"
	JRST PTERMN	;PUNCH TERMINATOR
	MOVEI A,LPRGN	;GET PROG NAME TYPE WORD
	DPB A,[310700,,BKBUF]	;INTO BLOCK
	MOVE A,PRGNM	;GET PROG NAME
	TLO A,40000
	PUSHJ P,OUTPUT	;AND BARF IT OUT
	PUSHJ P,OUTPUT
	PUSHJ P,EBLK	;TERMINATE BLOCK
	MOVEI B,LDHLFK	;NO, MAKE THAT THE NEW BLOCK TYPE
	MOVEI A,DISP2	;GET NEW DISPATCH
	JRST RESYM	;RE-ENTER SYMBOL TABLE SCANNING LOOP

DISP2:	JRST SYMD1	;COMMON
	JRST SYMD1	;MACRO OR PSEUDO
	JRST CHKLL	;SYMBOL
	JRST HLFKL1	;UNDEFINED LOCAL
	JRST CHKLL	;DEFINED LOCAL VARIABLE
	JRST SYMD1	;UNDEFINED LOCAL VARIABLE
	JRST SYMD1	;CAN'T HALF KILL GLOBALS
	JRST SYMD1
	JRST SYMD1
	JRST SYMD1
 
CHKLL:	TLNN CH1,3LLV+3VP	;IS THIS VALUE GOING TO BE LOADER SUPPLIED
	JRST SYMD1	;NO, SKIP IT
HLFKL1:	ROT B,-4	;RE POSITION SYMBOL IN B
	TLZ B,740000	;KILL ALL OF ITS FLAGS
	MOVE A,B	;INTO A FOR OUT PUT
	TLNE CH1,3SKILL	;IS HALF KILLAGE DESIRED?
	PUSHJ P,OUTPUT	;YES, PUT IT OUT
	JRST SYMD1


	IMPUR

DISPT:	0,,0(C)	;DISPATCH CONTROL WORD

	PUR

PTERMN:	MOVEI A,LPTRMN	;GET BLOCK TYPE
	DPB A,[310700,,BKBUF]	;STORE BLOCK TYPE
IFN ITS,	.RDATE A,		;PUT OUT DATE AND TIME
IFE ITS,	MOVEI A,0
	PUSHJ P,OUTPUT
IFN ITS,	.RTIME A,
IFE ITS,	MOVEI A,0
	PUSHJ P,OUTPUT
	JRST EBLK

A.OP:	PUSHJ P,AGETFD
	TYPUUO (SIXBIT /U.0/)	;UNDEFINED SYMBOL IN FIELD 0
	PUSH P,A
	PUSHJ P,AGETFD
	TYPUUO (SIXBIT /U.1/)	;UNDEFINED SYMBOL IN FIELD 1
	PUSH P,A	;PDL NOW HAS FIELD 0 AND FIELD 1
	PUSHJ P,AGETFD
	TYPUUO (SIXBIT /U.2/)	;UNDEFINED SYMBOL IN FIELD 2
	POP P,B	;B NOW HAS FIELD 1, A HAS FIELD 2, PDL HAS FIELD 0
	EXCH A,B
	POP P,T	;T HAS FIELD 0, A HAS FIELD 1, B HAS FIELD 2
	TDNN T,[757,,-1]	;DO NOT SUPPLY AC AND ADR FIELDS IF ANY BITS ON IN ISTRUCTION'S AC,X,OR ADR FIELDS
	IOR T,[0 A,B]
	XCT T
	JFCL		;DON'T CARE IF IT SKIPS
	JRST VALRET

AASCIZ:	TDZA T,T
A.ASCII:	MOVEI T,1
	MOVEM T,AASCF1
	MOVE D,[440700,,T
	JRST AASC1

ASIXBIT:	SKIPA D,[440600,,T]
AASCII:	MOVE D,[440700,,T]
	SETOM AASCF1
AASC1:	MOVEI T,0
	TLZE I,MWRD
	JRST TEXT2
	MOVE A,LIMBO1
	CAIN A,40
	PUSHJ P,RCH
	MOVEM A,TEXT4
TEXT7:	PUSHJ P,RCH
AASC8:	CAMN A,TEXT4
	JRST AASC1A
	TLNN D,760000
	JRST TEXT6
TEXT9:	TLNE D,100
	JRST AASC2
	PUSHJ P,LOWRCS	;HELP OUT LOWER CASE LOSER
	SUBI A,40
	CAIG A,77
	JUMPGE A,.+2
	TYP2UU (SIXBIT /N6B/)
AASC3:	IDPB A,D
	TRO I,SYL
	JRST TEXT7

AASC1A:	TLNN D,760000
	SKIPGE AASCF1
	JRST TEXT5
	MOVEI CH1,1
	JRST AASC1B	;EXTRA 0 NEED FOR Z FLAVOR

AASC2:	CAIN A,"!
	SKIPG AASCF1
	JRST AASC3
	PUSH P,T
	PUSH P,D
	PUSH P,SYM
	MOVE D,[SETOM ASUDS1]
	TRNN FF,PSS
	TRNN FF,NPSS
	MOVE D,[TYP2UU (SIXBIT /USA/)]
	MOVEM D,AASM1
	CLEARM ASUDS1
	PUSHJ P,AGETFD
	XCT AASM1
	POP P,SYM
	POP P,D
	POP P,T
	SKIPGE ASUDS1
	MOVNI A,1	;HAD UNDEFINED SYMS SO ASSUME MAX
	MOVE CH1,[440700,,AASBF
	MOVEM CH1,ASBP1
	MOVEM CH1,ASBP2
	PUSH P,[AASC5
	MOVE CH1,A
AASC6:	LSHC CH1,-35.
	LSH CH2,-1
	DIV CH1,ARADIX
	HRLM CH2,(P)
	JUMPE CH1,.+2
	PUSHJ P,AASC6
	HLRZ A,(P)
	ADDI A,"0
	IDPB A,ASBP1
	POPJ P,

AASC5:	MOVEI A,0
	IDPB A,ASBP1
AASC8A:	TLNN D,760000
	JRST AASC7
	ILDB A,ASBP2
	JUMPE A,AASC9
	IDPB A,D
	JRST AASC8A

AASC9:	MOVE A,LIMBO1
	JRST AASC8


AASC7:	TDZA CH1,CH1

TEXT6:	MOVNI CH1,1
AASC1B:	MOVEM CH1,AASCF2
	CLEARM CDISP
	MOVEM A,TEXT8
	TLO I,MWRD
	MOVE A,T
	JRST CLBPOP

VALRET:	MOVE T,A
	MOVE B,LIMBO1
	CAIE B,15
	CAIN B,12
	JRST VALR1
TEXT5:	PUSH P,T
	PUSHJ P,GETSYL
	TRNE I,SYL
	TYP2UU (SIXBIT /NOS/)
	POP P,A
VALR1:	TRO I,SYL
TEXT10:	JRST CLBPOP

TEXT2:	MOVE A,TEXT8
	TRO I,FLD
	SKIPGE B,AASCF2
	JRST TEXT9
	JUMPE B,AASC8A
	JRST TEXT5

	IMPUR
AASM1:	.
AASCF1:	0	;-1 REG 1 .ASCI 0 ASCIZ
AASCF2:	0	;MULTIPLE WORD RETURN FLAG -1 REG 0  FINISH ! HACK 1 0 FOR Z
TEXT4:	0
TEXT8:	0
ASBP1:	0
ASBP2:	0
AASBF:	BLOCK 3
ASUDS1:	0

	PUR
IGTXT:	PUSH P,A
	PUSHJ P,RCH
	CAME A,TEXT4
	JRST .-2
	TLZ I,MWRD
	JRST POPAJ

ARDIX:	PUSHJ P,GETWRD
	MOVEM A,ARADIX
	JRST ASSEM1

AEQUAL:	PUSHJ P,GETSYL
	PUSHJ P,ES
	IFE SLOW,MOVEM SYM,ST(D)
	IFN SLOW,MOVEM SYM,@STD
	PUSH P,SADR
	PUSHJ P,GETSYL
	PUSHJ P,ES
	TYP6UU (SIXBIT /IEQ/)
	POP P,T
	IFE SLOW,DPB A,[(400400) ST(T)]
	IFN SLOW,DPB A,[400400,,@STT]
	IFE SLOW,MOVEM B,ST+1(T)
	IFN SLOW,MOVEM B,@ST1T
	3GET B,T
	TLNE B,3MAS
	TLO C,3MAS
	3PUT C,T
	JRST ASSEM1

AWORD:	PUSHJ P,EBLK
	PUSHJ P,GETWRD
	PUSHJ P,PPB
	JRST ASSEM1

A1PASS:	IFN A1PSW,PUSHJ P,OUTUPD
	TLO FF,PPSS
IFE A1PSW,[	TRZ FF,NPSS
ARELOC:]
IFN A1PSW,[	TRZA FF,NPSS
ARELOC:	PUSHJ P,OUTUPD
]
ARELC1:	CLEARM CLOC
	MOVEI A,1
	MOVEM A,CRLOC
	CLEARM CONTRL
	JRST AREL1

AOFFSET:	TRO I,DEF+PSEUDF
	PUSHJ P,EBLK	;TERMINATE STINK BLOCK
	MOVE A,[TYPUUO (SIXBIT /USO/)
	MOVEM A,GTVER
	PUSHJ P,GETWRD
	TRNN I,DEF
	JRST ALOC1
	MOVE T,GLSP2
	CAME T,GLSP1
	JRST	AOFFS1	;IF GLSP2 NE GLSP1 THEN OFFSET IS GLOBAL
	TRZE FF,GLOFFS
	JRST AOFFS3	;IF OFFSET WAS GLOBAL ISN'T ANY MORE

AOFFS2:	MOVEM A,OFLOC
	MOVEM B,OFRLOC
	JRST ASSEM1
AOFFS1:	PUSHJ P,ALOC3A	;FUDGE
	MOVEI	A,LDOFS	;GET LOADER COMMAND
	PUSHJ P,PLDCM	;GENERATE BLOCK
	TRO FF,GLOFFS	;SET "OFFSET IS GLOBAL FLAG"
	CLEARB A,B
	JRST AOFFS2

AOFFS3:	PUSH P,A
	TRZ FF,FIRWD	;MAKE SURE BLOCK PUNCHED
	MOVEI A,LDROFS	;RESET GLOBAL OFFSET
	PUSHJ P,PLDCM
	POP P,A
	JRST AOFFS2

APRNTC:
APRNTX:	MOVE A,LIMBO1
	CAIN A,40
	PUSHJ P,RCH
APRTX1:	MOVE T,A
	PUSHJ P,RCH
	CAIN A,"!
	JUMPGE B,.-2
	CAMN A,T
	JRST ASSEM1
	PUSHJ P,TYO
	JRST APRTX1+1

SLOAD:	CONO PTR,60
	JSP 14,30
	DATAI PTR,16
	MOVE 15,16
	JUMPGE 16,16
	JSP 14,30
	DATAI PTR,(16)
	ROT 15,1
	ADD 15,(16)
	AOBJN 16,5
	MOVEI 14,33
	JRST 30
	CONSO PTR,10
	JRST 30
	JRST (14)
	DATAI PTR,16
	CAMN 15,16
	JUMPA 1
	JRST 4,
SLOADP:

;PDP10 SBLK LOADER
;FOLLOWING CODING ACTUAL WORDS TO BE OUTPUT
	;BY ASSEMBLER, COMPILER, OR WHATEVER
;SHOULD BE EXECUTED BY PDP10 HARDWARE READIN FEATURE
;USES ONLY THE AC'S (BUT ALL OF THEM)

LDR10:
	-17,,0		;BLKI POINTER FOR READ SWITCH

LDRC=0		;CHECKSUM (OK, SO YOU'RE NOT ALLOWED TO LOAD
		;INTO IT DURING HARDWARE READIN, BUT WHO SAYS
		;YOUR PROGRAM CAN'T USE IT?)
OFFSET -.+1		;BEGIN LOADING INTO 1 AS PER HEADER
LDRGO:	CONO PTR,60	;START UP PTR (RESTART POINT)
LDRRD:	HRRI LDRB,.+2	;INITIALIZE INDEX
LDRW:	CONSO PTR,10	;WAIT FOR WORD TO BE AVAILABLE
	JRST .-1
	ROT LDRC,-LDRRD(LDRB)	;BEFORE READING IN HEADER, ROTATE 2 BITS (THEN IGNORE)
		;BEFORE READING IN EACH DATA WORD, ROTATE 1 BIT (FOR UPDATING CHECKSUM)
		;BEFORE READING IN CHECKSUM, ROTATE NOT AT ALL (DON'T ROTATE CALCULATED CHECKSUM)
	DATAI PTR,@LDRT1-LDRRD(LDRB)	;READ WORD INTO RIGHT PLACE
		;HEADER => READ INTO C
		;STORAGE WORD => READ INDEXED BY AOBJN POINTER IN A
		;CHECKSUM => READ INTO A FOR COMPARISON WITH C(C)
	XCT LDRT1-LDRRD(LDRB)	;EXECUTE RELEVANT T1 ENTRY (MAYBE SKIPS)
	XCT LDRT2-LDRRD(LDRB)	;EXECUTE RELEVANT T2 ENTRY (MAYBE JUMPS)
LDRB:	SOJA .,		;-RD(B) IS 2, 1, AND 0 FOR SUCCESSIVE ENCOUNTERS OF THIS INSTRUCTION
			;USED AS INDEX INTO TABLES, ETC.

		;TABLE 1
		;INDIRECTED THROUGH FOR DATAI
		;THEN EXECUTED TO SEE WHAT TO DO WITH READ IN WORD
		;ENTRIES EXECUTED IN REVERSE ORDER

LDRT1:	CAME LDRC,LDRA	;COMPARE CHECKSUM WITH CALCULATED, SKIP TO B IF THEY AGREE
	ADD LDRC,(LDRA)	;UPDATE CHECKSUM
	SKIPL LDRA,LDRC	;INITIALIZE HEADER AND SKIP UNLESS JUMP BLOCK

		;TABLE 2
		;EXECUTED IF CORRESPONDING ENTRY IN TABLE 1 DIDN'T SKIP WHEN EXECUTED

LDRT2:	JRST 4,LDRGO	;CHECKSUM ERROR
	AOBJN LDRA,LDRW	;UPDATE AOBJN POINTER AND GO BACK FOR NEXT STORAGE WORD IF NOT EXHAUSTED
LDRA:	JRST LDRRD		;WHEN INITIALLY LOADED IS JUMP BLOCK TO THIS LOADER
		;DURING LOADING USED TO HOLD HEADER (AOBJN POINTER), WHICH MAY BE LOADED JUMP BLOCK

OFFSET 0
ELDR10:

ASQOZ:	PUSHJ P,AGETFD
	TYP2UU (SIXBIT /USS/)
	LSH A,36
	PUSH P,A
ASQOZ3:	PUSHJ P,GETSYL
	TRNN I,SYL	;IF NO SYLLABLE ASSEMBLED,
	JRST ASQOZ2	;THEN TRY AGAIN UNLESS CR OR LF
ASQOZ4:	TLZ SYM,740000
	POP P,A
	ADD A,SYM
	TLO I,UNRCHF
	JRST VALRET

ASQOZ2:	CAIE A,15	;SYLLABLE FOR SQUOZE NULL, IF TERMINATOR WAS CR,
	CAIN A,12	;OR LF,
	JRST ASQOZ4	;THEN OUTPUT NULL SYM
	JRST ASQOZ3	;OTHERWISE OTRY AGAIN


A.LOP:	SKIPE CONTRL
	TYP6UU (SIXBIT /ILO/)
	PUSHJ P,EBLK
	PUSHJ P,GETFLD
	MOVEM A,WRD
	MOVEM B,WRDRLC
	PUSHJ P,PWRDA
	MOVEI  A,GLOTB
	MOVEM A,GLSP1
	MOVEM A,GLSP2
	PUSHJ P,GETFLD
	MOVEM A,WRD
	MOVEM B,WRDRLC
	PUSHJ P,PWRDA
	MOVEI  A,GLOTB
	MOVEM A,GLSP1
	MOVEM A,GLSP2
	PUSHJ P,GETFLD
	MOVEM A,WRD
	MOVEM B,WRDRLC
	MOVEI A,LD.OP
	PUSHJ P,PLDCM
	JRST ASSEM1

STGWS:	HLLZM B,STGSW
	JRST ASSEM1

A.LIBRQ:	PUSHJ P,GETSYL
	TRNN I,SYL
	JRST ASSEM1
	PUSHJ P,PBITS7
	MOVEI A,3
	PUSHJ P,PBITS
	TLO SYM,40000
	PUSHJ P,OUTSM
	JRST A.LIBRQ

A.GLOB:	TRO I,PSEUDF	;TEEL GETVAL WE ARE A PSEUDO OP
AGLOB1:	MOVEI A,GLOTB
	MOVEM A,GLSP1
	SETOM FLDCNT
	PUSHJ P,GETSYL
	TRNN I,SYL
	JRST ASSEM1
	TLO I,GLI
	PUSHJ P,GETVAL
	JRST AGLOB1

A.GSSET:	PUSHJ P,AGETFD
	TYPUUO (SIXBIT /USG/)
	MOVEM A,GENSM
	JRST ASSEM1


A.TYPE:	PUSHJ P,GETSYL
	PUSHJ P,ES
	MOVEI A,17
	JRST VALRET



A.GST:		;POPJ IF END OF STRING
	PUSHJ P,BCOMPA	;CONVERT A TO BYTE POINTER
	MOVEM CH2,A.GST3

A.GST1:	ILDB B,A.GST3
	CAIL B,300
	POPJ P,
	CAIE B,".
	JRST A.GST1
	PUSHJ P,A.GSYL
	JUMPL T,CPOPJ
	CAME SYM,[SQUOZE 0,TAG
	JRST A.GST1
	PUSHJ P,A.GSYL
	JUMPL T,CPOPJ
	CAME SYM,A.GST4
	JRST A.GST1
	MOVE CH2,A.GST3
	PUSHJ P,CCOMP	;CONVERT BACK
	JRST POPJ1
;HERE TO DO ENTRY AND EXTERN BLOCKS

A.ENTRY:	MOVEI A,LENTRY	;GET ENTRY BLOCK HEADER

ENTRY3:	PUSH P,A	;SAVE IT
	PUSHJ P,EBLK	;TERMINATE CURRENT BLOCK
	TRO I,PSEUDF	;TELL GETVAL WE ARE APSEUDO OP
ENTRY1:	SETOM FLDCNT
	PUSHJ P,GETSYL	;START READING LIST
	TRNN I,SYL	;WAS A SYLLABLE READ?
	JRST ENTRY2	;NO, FINISH BLOCK
	PUSHJ P,OUTSM	;PUT OUT SYMBOL
	TLO I,GLI	;ALSO ENTER INTO SYMBOL TABLE AS BEING GLOBAL
	PUSHJ P,GETVAL	;GO ENTER IT IN TABLE
	MOVEI A,GLOTB	;DON'T FILL GLOBAL POINTER BLOCK
	MOVEM A,GLSP1	;THEREFORE RESET POINTER EACH TIME
	JRST ENTRY1

ENTRY2:	POP P,A	;RESTORE BLOCK TYPE
	DPB A,[310700,,BKBUF]	;STORE INTO BLOCK HEADER
	PUSHJ P,EBLK	;CLOSE UP BLOCK
	JRST ASSEM1

;HERE FOR EXTERN BLOCK

A.EXTERN:	MOVEI A,LEXTERN	;GET EXTERN BLOCK TYPE
	JRST ENTRY3



AG.SP:	LSHC A,-2	;LOOK BACKWARD FOR STRING BEG PNTR IN A
	LDB CH1,[420200,,B
	MOVE B,MACTAB(A)
	IMULI CH1,-2
	XOR B,[300_28.+300_20.+300_12.+300_4
	JRST AG.SP2(CH1)

AG.SP3:	MOVE B,MACTAB(A)
	XOR B,[300_28.+300_20.+300_12.+300_4

	TRNN B,300_4
	JSP CH1,AG.SF
	TLNN B,3
	JSP CH1,AG.SF
	TLNN B,300_2
	JSP CH1,AG.SF
AG.SP2:	TLNN B,300_10.
	JSP CH1,AG.SF
AG.SP1:	SOJA A,AG.SP3

AG.SF:	MOVNI CH1,(CH1)
	ADDI CH1,AG.SP1
	LSH CH1,-1
	LSH A,2
	ADDI A,(CH1)
	HRRZS A
	POPJ P,

	IMPUR
A.GST4:	0
A.GST3:	0
	PUR

CCOMP:	TDZA A,A
CCOMP1:	IBP CH2
	TLNE CH2,700000
	SOJA A,CCOMP1
	SUBI CH2,MACTAB
	LSH CH2,2
	ADDI A,4(CH2)
	POPJ P,

BCOMPA:	SKIPE CH1,A
	SOS CH1
	IDIVI CH1,4
	MOVE CH2,PTAB(CH2)
	ADD CH2,CH1
	TLZ CH2,17
	POPJ P,

A.TAG:	PUSHJ P,GSYL
	JRST ASSEM1

A.GO:	PUSHJ P,GSYL
	MOVEM SYM,A.GST4

A.GO1:	TRNN FF,MACRCH
	JRST ASSEM1
	MOVE A,CPTR
	PUSHJ P,AG.SP
	PUSHJ P,REDINC
	CAIN B,374	;MACRO
	ADDI A,2
	PUSHJ P,A.GST
	JRST A.GO2
	MOVEM A,CPTR
	SETOM CPTRB
	JRST ASSEM1

A.GO2:	PUSHJ P,PMACP
	JRST A.GO1

PMACP:	MOVE B,MACP	;POP MACRO PDL
	HRRZ A,(B)
	SUB B,[1,,1
	CAIN A,AIRP4
	JRST A.GO6
	CAIN A,REPT1
	JRST A.GO4
	CAIE A,RCHSV1	;MACRO
	CAIN A,RCHSAV	;ARG
	JRST A.GO6
	ERRUUO (SIXBIT /UCP/)

A.GO4:	HRRZS -2(B)
A.GO6:	TRO FF,MRSW
	JRST (A)

A.BYTE:	CLEARM NBYTS	;# BYTES ASSEMBLED
	CLEARM BYTMT	;TOTAL ACTIVE BYTES IN TABLE
	MOVE A,[440700,,BYBYT]	;POINTER TO NEW TABLE
	MOVEM A,BYTMP
A.BY1:	PUSHJ P,AGETFD	;GET FIELD, .GE. 0 => BYTE, .LT. 0 => HOLE
	TYPUUO (SIXBIT /UBS/)
	MOVE C,ISAV
	TRNN C,FLD
	JRST A.BY2	;NO FIELD
	MOVM B,A
	SKIPGE A
	TRO B,100
	IDPB B,BYTMP
	AOS BYTMT
A.BY2:	CAIE CH1,15	;LIMBO1
	CAIN CH1,12
	JRST .+2
	JRST A.BY1	;NOT CR OR LF, LOOK FOR MORE FIELDS
	SKIPN BYTMT	;CR OR LF, ANY FIELDS?
	JRST A.BY3	;NO, DO .WALGN AND RESET TO WORD MODE
	SETOM BYTM	;ENTERING BYTE MODE
	PUSHJ P,BYSET
	JRST ASSEM1

		;RESET THE BYTE DESCRIPTOR TABLE POINTERS TO POINT TO NEW WORD

BYSET:	CLEARM BYTMC	;COUNT OF BYTES PROCESSED THIS TABLE SCAN
	MOVE A,[440700,,BYBYT]	;POINTER TO DESCRIPTOR TABLE
	MOVEM A,BYTMP
	ILDB A,BYTMP	;FIRST DESCRIPTOR BYTE
	AOS BYTMC
	DPB A,[300600,,BYTWP]	;DEPOSIT AS FIRST BYTE SIZE
	POPJ P,

A.BY3:	CLEARM BYTM	;NO LONGER IN BYTE MODE

A.WALGN:	LDB A,[360600,,BYTWP]
	CAIN A,44
	JRST ASSEM1	;ALREADY AT BEGINNING OF WORD
	MOVEI A,44
	DPB A,[360600,,BYTWP]	;MAKE IT POINT TO BEGINNING OF WORD
	PUSHJ P,BYSET
	CLEARM T1
	JRST PBY1

BYTIN1:	CLEARM BYTMC
	MOVE A,[440700,,BYBYT
	MOVEM A,BYTMP
BYTINC:	AOS A,BYTMC
	CAMLE A,BYTMT
	JRST BYTIN1
	ILDB A,BYTMP
	DPB A,[300600,,BYTWP
	MOVEM A,T1
	HLLZ A,BYTWP
	IBP A
	TRNN A,-1
	JRST BYTINR
PBY1:	MOVE A,BYTW
	MOVEM A,WRD
	CLEARM BYTW
	MOVEI A,44
	DPB A,[360600,,BYTWP
	PUSHJ P,PWRD
	AOS CLOC
BYTINR:	MOVE A,T1	;CURRENT BYTE SIZE
	TRNN A,100
	JRST ASSEM1
	MOVEI A,0	;ASSEMBLE HOLE (BLANK BYTE) IMMEDIATELY AFTER PREVIOUS BYTE
	JRST PBY2

PBYTE:	AOS NBYTS
	MOVE A,WRD
PBY2:	IDPB A,BYTWP
	JUMPGE FF,BYTINC
	SKIPE WRDRLC
	TYPUUO (SIXBIT /RIB/)
	MOVE A,GLSP1
	CAME A,GLSP2
	TYPUUO (SIXBIT /GIB/)
	JRST BYTINC

	IMPUR
LBYBYT==5
BYBYT:	BLOCK LBYBYT
	PUR

A.BYTC:	MOVE A,NBYTS
	JRST CLBPOP

;		'ALGEBRAIC' CRUFT MARO DEFINITIONS

DEFINE MOAN ARG/
	MOVEI D,[SIXBIT /!ARG!!/]
	JRST ERRCON
TERMIN

DEFINE RETLIN
	MOVEI A,15	;CARRIAGE RETURN
	PUSHJ P,PUTREL
	MOVEI A,12	;LINE FEED
	PUSHJ P,PUTREL
TERMIN

DEFINE NUMBER
	MOVE A,BTPNT
	ILDB I,A
	CAIE I,"#
	CAIGE I,"@
TERMIN

DEFINE RESTOR
	MOVE D,BTPNT
	SETZM STRING
	SETZM STRING+1
	SETZM STRING+2
TERMIN


DEFINE SPECN
	POP P,RANDM
	MOVE A,ENN
	SUB A,RANDM
	MOVEM A,ENN
TERMIN

DEFINE GET
	EXCH I,ACSAV+1
	PUSHJ P,RCH
	EXCH I,ACSAV+1
TERMIN

DEFINE GETT
	EXCH I,ACSAV+1
	PUSHJ P,RCH
	EXCH I,ACSAV+1
	IDPB A,TPN
TERMIN

;		START OF COMPILER PROPER

OPDL:	CH?CH?CH?CH?CH?CH?CH?CH	;COMMUTATOR
	CH?SP?CH?CH?CH?CR?CH?CH
	CH?CH?CH?CH?CH?CH?CH?CH
	CH?CH?CH?CH?CH?CH?CH?CH
	SP?CH?CH?CH?DL?CH?CH?CH
	LP?RP?TX?PL?CM?MN?CH?DV
	CH?CH?CH?CH?CH?CH?CH?CH
	CH?CH?CH?KL?LB?EQ?RB?CH

;	CH?CH?CH?CH?CH?CH?CH?CH
;	CH?CH?CH?CH?CH?CH?CH?CH
;	CH?CH?CH?CH?CH?CH?CH?CH
;	CH?CH?CH?CH?CH?CH?UP?CH
;	CH?CH?CH?CH?CH?CH?CH?CH
;	CH?CH?CH?CH?CH?CH?CH?CH
;	CH?CH?CH?CH?CH?CH?CH?CH
;	CH?CH?CH?CH?CH?CH?CH?CH

	IMPUR
ENN:	60	;ACCUMULATOR NUMBER - TROUBLE IF GOES PAST 9

BTPNT:	440700,,STRING	;D
STRING:	BLOCK 10	;CHARACTER ASSEMBLY (D) - TROUBLE IF OVERFLOWS 

TPN:	0
DIRPNT:	440700,,DIROUT	;TPN
DIROUT:	BLOCK 40	;COPY OF LINE IN PROGRESS (TPN) - TROUBLE IF OVERFLOWS

OPSTKL==40
	0
OPSTK:	BLOCK OPSTKL	;OPERATOR STACK (R) - TROUBLE IF OVERFLOWS
	0



ENDSTT:	0	;ON IF END OF STATEMENT ENCOUNTERED
CHARF:	0	;LAST WAS NOT OPERATOR
NUMFL:	0	;STRING IS NUMERIC CONSTANT (NEEDS [ AND ])
R1SV:	0	;SAVED A
R2SV:	0	;SAVED I, CALLED V EARLIER ON

INTEGR:	0	;INTEGER ARITHMETIC
WARN:	0	;ON AFTER ) TO STOP NON-OPERATOR
RANDM:	0	;DUMP COMMA COUNT HERE

ACSAV:	BLOCK 7
	PUR

;		ENTRANCE TO 'ALGEBRAIC' TRANSLATOR

A.I:	SETOM INTEGR
	SKIPA
A.F:	SETZM INTEGR
	PUSHJ P,SWINI	;INITIALISE PASSAGE TO MIDAS ASSEMBLER
	MOVE TM,[P,,ACSAV]
	BLT TM,ACSAV+6
	SETZM ENDSTT	;RESET END OF STMNT FLAG
	SETZM EQHIT'	;RESET LAST CHAR WAS= FLAG
	SETZM WARN	;SET OFF ERROR DETECTOR
	MOVEI A,"0	;INITIALISE POINTERS
	MOVEM A,ENN
	MOVE A,DIRPNT
	MOVEM A,TPN	;POINTER TO SAVED INPUT
	MOVE SYM,[-OPSTKL,,OPSTK]
	PUSH SYM,[0,,ENDSAT]
	PUSH P,[0]	;INITIALISE COMMA-COUNTER
	SETZM CHARF
CLSTR:	RESTOR
RDITTS:	SKIPE ENDSTT
	JRST BDEND
RDITA:	GETT
	CAIGE A,100	;FOR ABBREVIATED DISPATCH TABLE
	JRST @OPDL(A)
	CAIN A,"^
	JRST UP

CH:	SETZM EQHIT
	SKIPE WARN
	JRST CHBRT
CHEY:	IDPB A,D
	SETOM CHARF	;NON UNARY FLAG
	JRST RDITA

GAMB:	RESTOR
COMMT:	MOVE I,R2SV
	JRST GOPURT

SHORT:	;DECIDES IF STRING CAN BE USED IN IMMEDIATE TYPE OPS
	SETZM IMMED'
	SKIPN STRING
	POPJ P,		;NO STRING
	MOVE A,BTPNT
	ILDB I,A
	CAIN I,"#
	JRST APUPJ	;YEPE HE ASKED FOR IT
	SKIPE STRING+1
	POPJ P,		;STRING IS LONG
	SKIPA

TSTSHL:	ILDB I,A
	JUMPE I,APUPJ	;ITS OK FOUND ONLY NUMBERS
	CAILE I,"@
	POPJ P,		;NON-NUMBER IN STRING
	CAIE I,".
	JRST TSTSHL
	ILDB I,A
	SKIPN I		;ANYTHING FOLLOW '.' QST
APUPJ:	SETOM IMMED'	;INDICATE IMMEDIATE USAGE IS POSSIBLE
	POPJ P,

SZPRT:	SETZM CHARF
GOPRT:	SETZM WARN
GOPART:	MOVEM I,R2SV
GOPURT:	HLRZ B,I
	HLRZ C,(SYM)
	CAMLE B,C
	JRST PSOPR	;GO PUSH OPERATOR
	SKIPN INTEGR
	SETOM IMMED	;FOR ARITH OPS ONLY FIXED WILL DO IMMEDIATE
	PUSHJ P,SHORT	;ESTABLISH IF STRING CAN BE IMMEDIFIED
	POP SYM,A	;POP AN OPERATOR
	JUMPN A,(A)

	MOAN OVERPOPPED OPERATOR STACK

CHEX:	MOVE A,R1SV
	JRST CHEY

RP:	SKIPE EQHIT
	AOS ENN		;TAKE CARE OF UNSATISFIED = AT END
	SKIPN CHARF
	JRST RTONOP
	SETOM CHARF
BUDDY:	SETOM WARN
	MOVEI I,RPAR
	JRST GOPART

RTONOP:	MOVE I,(SYM)
	CAIN I,FUNCT
	JRST BUDDY	;NO ARGUMENT FUNCTION

	MOAN ) FOLLOWS OPERATOR

BDEND:	MOAN TOO MANY ('S

CHBRT:	MOAN NON-OPERATOR FOLLOWS )


CR:	SKIPE EQHIT
	AOS ENN	;HANDLES UNSATISFIED = AT END
	SETOM ENDSTT
	MOVEI I,RCAR
	JRST GOPRT

LP:	SETZM EQHIT
	SKIPE WARN
	JRST LFRHT
	SETZM CHARF
	SKIPE STRING
	JRST INDX
	PUSH P,[0]	;INITIALISE COMMA-COUNTER
	PUSH SYM,[0,,LFTPR]
	JRST RDITA

INDX:	NUMBER
	JRST NUSTRB
	GETT
	CAIG A,"9
	JRST NMRINX
	MOVEI I,"(	
	IDPB I,D
INDY:	IDPB A,D
	GETT
	CAIN A,"+	;IS IT COMPOUND SUBSCRIPT
	JRST CMPNDN
	CAIN A,"-
	JRST CMPNDN
	CAIE A,")	;SEARCH FOR NEXT RP
	JRST INDY
	IDPB A,D
CMBAN:	SETOM CHARF	;MAKE BELIEVE CHARATER LAST
	SETOM WARN	;YET SET ) TRAP
	JRST RDITA

NMRINX:	CAIN A,"-	;IS IT A MINUS
	JRST INDZ
	CAIN A,"+
	JRST INDZ
	MOVEI I,"+	;NUMERICAL SUBSCRIPT
	IDPB I,D
INDZ:	IDPB A,D
	GETT
	CAIN A,"+	;IS IT COMPOUND SUBSCRIPT
	JRST CMPNDC
	CAIE A,")
	JRST INDZ
	JRST CMBAN

CMPNDN:	MOVEI I,")
	IDPB I,D
	JRST INDZ

CMPNDC:	MOVEI I,"(
	IDPB I,D
	JRST INDY

LFRHT:	MOAN ( FOLLOWS DIRECTLY ON )

SP=RDITA	;USE FOR NON ARITH STATS

CM:	MOVE I,[1,,COMMX]
	SKIPN CHARF
	AOS ENN
	JRST SZPRT

EQ:	SETOM EQHIT
	SETZM WARN
	SKIPN CHARF	;TEST FOR EXISTANCE OF  L H S
	JRST EQFLOP
	NUMBER		;IS  L H S A NUMBER
	JRST EQNUMB
	MOVEI I,EQAAL
EQVAL:	SETZM CHARF
	PUSH SYM,I
	PUSH P,STRING
	PUSH P,STRING+1
	PUSH P,STRING+2
	PUSH P,[0]
	JRST CLSTR

PL:	MOVE I,[2,,PLUS]
	SKIPN CHARF
	JRST RDITA	;UNARY PLUS
	JRST SZPRT

MN:	MOVE I,[2,,MINUX]
	SKIPN CHARF
	MOVE I,[5,,UMINU]
	JRST SZPRT

LB:	SKIPN CHARF
	JRST LP	;TREAT LIKE (
	NUMBER
	JRST NUBRST
	MOVEI I,FUNCT
	JRST EQVAL

RB=RP

NUBRST:	MOAN '<' FOLLOWS NUMBER

NUSTRB:	MOAN '(' FOLLOWS NUMBER

EQFLOP:	MOAN '=' FOLLOWS OPERATOR

EQNUMB:	MOAN '=' FOLLOWS NUMBER

TX:	MOVE I,[4,,TIMES]
	SKIPN CHARF
	JRST RDITA	;UNARY TIMES
	JRST SZPRT

DL:	GET	;CONTINUE STATEMENT RC
	GET	;LF
	GET	;.
	CAIE A,".	;DOT
	JRST BDCONT
	GET	;F OR I
	GET	;CONTROL I OR SPACE
	MOVE A,DIRPNT
	MOVEM A,TPN	;RESET SAVED INPUT POINTER TO AVOID FILLING ITS BUFFER
	MOVEI A,"$
	IDPB A,TPN
	MOVEI A,40
	IDPB A,TPN	
	JRST RDITA

ERRCON:	TRNE FF,PSS	;NO OUTPUT ON SECOND PASS
	JRST CONRBT
;MAY ALSO WANT TO USE STATEMENT PLUS LINE NUMBER TYPE TACTIC
	MOVE B,DIRPNT
OUTRR:	ILDB A,B
	PUSHJ P,TYO
	CAME B,TPN
	JRST OUTRR
	SKIPE ENDSTT
	JRST CONERT
DORSTL:	MOVEI A,40
	PUSHJ P,TYO
	MOVEI A,"?	;POINT AT ERROR
	PUSHJ P,TYO
	MOVEI A,40
	PUSHJ P,TYO
DORSAL:	GET		;COPY UP TO LINE FEED
	PUSHJ P,TYO
	CAIE A,12	;LF
	JRST DORSAL
CONERT:	PUSHJ P,TIPIS
	PUSHJ P,CRR
CONRAT:	MOVE TM,[ACSAV,,P]
	BLT TM,P+6
	JRST SWFLS	;GO BACK AND FLUSH 


CONRBT:	GET
	CAIE A,12	;LF
	JRST CONRBT
	JRST CONRAT


UP:	SKIPN STRING
	JRST ITSEX
	MOVEM A,R1SV	;SAVE THE ARROW
	NUMBER
	JRST CHEX	;ITS PART OF A NUMBER
ITSEX:	MOVE I,[6,,STRSTR]
	SKIPN CHARF
	JRST EXMB
	JRST SZPRT

EXMB:	MOAN UNARY ^

BDCONT:	MOAN BAD CONTINUATION

KL=CR	;SEMICOLON ACTS LIKE CR IN TERMINATING

STRSTR:	SKIPN STRING
	JRST EXLS
	NUMBER
	SKIPA
	JRST EXLS
	SUBI I,61
	TDNE I,[-1,,777774]
	JRST EXLS
	MOVE A,STRING
	TDNE A,[3777,,-1]
	JRST EXLS
	ADDI I,POWR
	JRST @(I)

EXLS:	PUSH P,[ASCII !EXPLO!]
	PUSH P,[ASCII !G    !]
	PUSH P,[0]
	PUSH P,[1]
	SETOM EXRET'
	JRST FUNET

DV:	MOVE I,[4,,DIVIX]
	SKIPN CHARF
	MOVE I,[5,,UDIVI]
	JRST SZPRT

PSOPR:	PUSH SYM,I	;PUSH OPERATOR FOR LATER EXCECUTION
	SKIPN STRING
	JRST RDITTS
	PUSHJ P,SHORT	;CAN WE IMMEDIFY
	PUSHJ P,MVOI	;AND MOVE OPERAND INTO STACK
	JRST CLSTR


PRODB:	NUMBER		;OUTPUT WHAT IS IN STRING
	SKIPE IMMED	;NO [ & ] IF IMMEDIATE USE
	JRST OVNM
	PUSH P,A
	MOVEI A,"[	;[ FOR CONSTANT
	PUSHJ P,PUTREL
	POP P,A
	SETOM NUMFL
OVNM:	CAIN I,"#
	JRST PRDOC

	EXCH A,I
	PUSHJ P,PUTREL
	MOVE A,I
PRDOC:	ILDB I,A
	JUMPN I,OVNM
	SKIPN NUMFL
	POPJ P,
	MOVEI A,"]	;] FOR CONSTANT
	PUSHJ P,PUTREL
	SETZM NUMFL
	POPJ P,

PRODC:	HRLI A,440700	;MAKE BYTE POINTER
	JRST PRDOC

LFTPR:	SPECN
	JRST RDITTS	;IGNORE LP ON STACK

RCAR:	IFN ITS,.VALUE	;IMPOSSIBLE FOR THESE TO BE ON STACK
	IFE ITS,HALTF
RPAR:	IFN ITS,.VALUE
	IFE ITS,HALTF

EQAAL:	SPECN
	SKIPE STRING
	PUSHJ P,MVOI
	MOVEI A,[ASCIZ !	MOVEM A!]
	PUSHJ P,PRODC
	POP P,STRING+2
	POP P,STRING+1
	POP P,STRING
	MOVE A,ENN
	SOS A
	PUSHJ P,FINOF
	JRST GAMB

ENDSAT:	SPECN
	SKIPN ENDSTT
	JRST TOEARL
	SKIPE STRING
	PUSHJ P,MVOI
GETLF:	GET
	CAIE A,12	;LF
	JRST GETLF
	MOVE TM,[ACSAV,,P]
	BLT TM,P+6
	JRST SWRET	;GO BACK

MVOI:	MOVEI A,[ASCIZ !	MOVE A!]
	SKIPE IMMED
	MOVEI A,[ASCIZ !	MOVEI A!]
MVOIK:	PUSHJ P,PRODC
	MOVE A,ENN
	AOS ENN
FINOF:	PUSHJ P,PUTREL
	MOVEI A,",
	PUSHJ P,PUTREL
	PUSHJ P,PRODB
	RETLIN
	POPJ P,

TOEARL:	MOAN TOO MANY )'S


PLUS:	MOVEI A,[ASCIZ !	FADR A!]
	SKIPE INTEGR
	MOVEI A,[ASCIZ !	ADD A!]
	SKIPE IMMED
	MOVEI A,[ASCIZ !	ADDI A!]
OPERT:	PUSHJ P,PRODC
	SKIPE STRING
	JRST GAINS
	SOS ENN
OPRTE:	MOVE A,ENN
	SOS A
	PUSHJ P,PUTREL
	PUSHJ P,COMMAA
	MOVE A,ENN
	PUSHJ P,PUTREL
	RETLIN
	JRST COMMT

COMMAA:	MOVEI A,",
	PUSHJ P,PUTREL
	MOVEI A,"A
	JRST PUTREL

GAINS:	MOVE A,ENN
	SOS A
	PUSHJ P,FINOF
	JRST GAMB

MINUX:	MOVEI A,[ASCIZ !	FSBR A!]
	SKIPE INTEGR
	MOVEI A,[ASCIZ !	SUB A!]
	SKIPE IMMED
	MOVEI A,[ASCIZ !	SUBI A!]
	JRST OPERT

TIMES:	PUSHJ P,TMSTR
	SKIPE IMMED
	MOVEI A,[ASCIZ !	IMULI A!]
	JRST OPERT

DIVIX:	MOVEI A,[ASCIZ !	FDVR A!]
	SKIPE INTEGR
	MOVEI A,[ASCIZ !	IDIV A!]
	SKIPE IMMED
	MOVEI A,[ASCIZ !	IDIVI A!]
	JRST OPERT


UMINU:	CAMN B,C
	JRST BAKWD		;THESE HAVE TO BE STACKED REVERSE
	SKIPE STRING
	JRST MOABC
	MOVEI A,[ASCIZ !	MOVNS A!]
	PUSHJ P,PRODC
	MOVE A,ENN
	SOS A
	PUSHJ P,PUTREL
	RETLIN
	JRST COMMT

MOABC:	MOVEI A,[ASCIZ !	MOVN A!]
	SKIPE IMMED
	MOVEI A,[ASCIZ !	MOVNI A!]
	PUSHJ P,MVOIK
	JRST GAMB

MVONT:	MOVEI A,[ASCIZ !	MOVE A!]
	PUSHJ P,PRODC
	MOVE A,ENN
	JRST ONMVS

TMSTR:	MOVEI A,[ASCIZ !	FMPR A!]
	SKIPE INTEGR
	MOVEI A,[ASCIZ !	IMUL A!]
	POPJ P,

BAKWD:	PUSH SYM,A
	JRST PSOPR

UDIVI:	CAMN B,C
	JRST BAKWD	;THESE HAVE TO BE STACKED REVERSE
	SKIPE INTEGR
	JRST UINDV
	SKIPN STRING
	PUSHJ P,MVONT
	MOVEI A,[ASCIZ !	HRLZI A!]
	PUSHJ P,PRODC
	MOVE A,ENN
	SKIPN STRING
	SOS A
	PUSHJ P,PUTREL
	MOVEI A,[ASCIZ !,201400!]
	PUSHJ P,PRODC
	RETLIN
	AOS ENN
	JRST DIVIX

ONTMS:	PUSHJ P,TMSTR
	PUSHJ P,PRODC
	MOVE A,ENN
	SOS A
ONMVS:	PUSHJ P,PUTREL
	PUSHJ P,COMMAA
	MOVE A,ENN
	SOS A
LSTCHX:	PUSHJ P,PUTREL
	RETLIN
	POPJ P,

POWR:	GAMB?POWR2?POWAA?POWR4

POWR4:	PUSHJ P,ONTMS
POWR2:	PUSHJ P,ONTMS
	JRST GAMB

POWAA:	PUSHJ P,MVONT
	AOS ENN
	PUSHJ P,ONTMS
	SOS ENN
	PUSHJ P,TMSTR
	PUSHJ P,PRODC
	RESTOR
	JRST OPRTE

COMMX:	AOS (P)
	SKIPE STRING
	PUSHJ P,MVOI
	JRST GAMB

UINDV:	MOAN INTEGER UNARY DIVIDE

FUNCT:	SETZM EXRET
FUNET:	SKIPE STRING
	PUSHJ P,MVOI
	SPECN
	PUSHJ P,MORFMC
	MOVEI A,[ASCIZ !	PUSHJ P,!]
	POP P,STRING+2
	POP P,STRING+1
	POP P,STRING
	PUSHJ P,PRODC
	PUSHJ P,PRODB
	RESTOR
	RETLIN
	PUSHJ P,MORFNC
	SKIPN EXRET
	JRST RDITTS	;AS USED FROM FUNCT
	JRST COMMT	;AS USED FROM  STRSTR

MORFMC:	MOVE A,RANDM
	MOVEM A,RANSV'
	SKIPN CHARF	;NO ARGUMENTS
	AOS ENN
	SETOM CHARF
	MOVEI A,"1
	CAMN A,ENN	;ARE ARGUMENT ALREADY IN A0 AND UP
	POPJ P,
	SETZM CORDM
MORYLP:	PUSHJ P,ZENBD
	AOS CORDM
	SOSL RANSV
	JRST MORYLP
	POPJ P,

MORFNC:	MOVEI A,"1
	CAMN A,ENN
	POPJ P,
	MOVE A,RANDM
	MOVEM A,CORDM'
MORXLP:	PUSHJ P,ZENBD
	SOSL CORDM
	JRST MORXLP
	POPJ P,

ZENBD:	MOVEI A,[ASCIZ !	EXCH A!]
	PUSHJ P,PRODC
	MOVE A,CORDM
	ADDI A,"0
	PUSHJ P,PUTREL
	PUSHJ P,COMMAA
	MOVE A,ENN
	SOS A
	ADD A,CORDM
	JRST LSTCHX

TIPIS:	MOVE A,TEMP
	MOVEM A,BYTPNT
MORTP:	ILDB A,BYTPNT
	CAIN A,1	;EXCLAMATION
	POPJ P,
	ADDI A," 	;SPACE
	PUSHJ P,TYO
	JRST MORTP

	IMPUR
TEMP:	440607,,0	;INDIRECT VIA D

BYTPNT:	0
	PUR

IFN TS,[
	;CAPACITY OF INDIRECT FILE TABLE CAN BE SET ON PAGE 1
	;VARIABLES ON PAGE .+3

A.INSRT:	SETOM FFFLG
	SETZM SPCSNM
	PUSH P,B
	MOVE A,[2*MAXIND-1,,AFNAM2+2*MAXIND-2]
BLKPSH:	MOVE B,(A)
	MOVEM B,2(A)
	SUB A,[1,,1]
	JUMPGE A,BLKPSH
	POP P,B
	MOVE A,[DNAM,,TDNM]	;DEFAULT NAMES
	BLT A,TSNM
	SETZM INDDF
	SETZM INFNFG
A.IN1:	PUSHJ P,GETSBS
	CAIN B,":
	JRST INCOL
	CAIN B,";
	JRST INSEMC
	CAIE B,15
	JUMPE SYM,A.IN1
	CAIN B,15
	JUMPE SYM,A.IN2
	SKIPGE FFFLG
	MOVEM SYM,TF2
	AOSG FFFLG
	JRST A.IN5
	EXCH SYM,TF2
	MOVEM SYM,TF1
A.IN5:	CAIE B,15
	JRST A.IN1
A.IN2:	SKIPE INDDF	;GETTING CRUD FROM TTY?
	JRST A.IN4	;YES; FINISH TABLE ENTRY
	HRRZ A,TDNM	;NO, BUT MAYBE TDNM=@
	CAIN A,(SIXBIT /@/)
	JRST INDDV	;IF TDNM=@ THEN SEARCH TABLE
	MOVEI B,0
A.IN3:	MOVE A,TSNM(B)
IFN ITS,[
	.SUSET [.SSNAM,,A]
	.IOPUSH UTYIC,
]
IFE ITS,[
	EXCH 0,IOPDLP
	PUSH 0,IJFN
	EXCH 0,IOPDLP
	PUSH P,C
	PUSH P,D
	PUSH P,T
	PUSH P,TT
	MOVE D,[440700,,JFNSTR]
;BUILD TOPS-20 FILE SPEC

	SKIPN TT,TSNM(B)
	JRST NOSNM1
	CAMN TT,[-1]
	JRST NOSNM1
	MOVEI A,"<
	IDPB A,D

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,">
	IDPB A,D

NOSNM1:	MOVE TT,TDNM+1(B)

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,".
	IDPB A,D

	MOVE TT,TDNM+2(B)

	CAMN TT,[SIXBIT />/]
	MOVSI TT,(SIXBIT /MID/)

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,0
	IDPB A,D
]


	MOVE A,TDNM+1(B)
	MOVEM A,AFNAM1
	MOVE A,TDNM+2(B)
	MOVEM A,AFNAM2
IFN ITS,[
	.OPEN UTYIC,TDNM(B)
	JRST INFNF	;NON-STANDARD ERROR: CORRECTABLE FROM TTY
	MOVE A,[UTYIC,,TEMFNM]	;SETUP CALL TO READ STATUS
	.RCHST A,	;GOBBLE FROM SYSTEM
	SKIPN A,TEMFNM+1	;ANY INFO
	JRST A.IN10	;NO ,LOSE
	MOVEM A,INFN1
	MOVE A,TEMFNM+2
	MOVEM A,INFN2
]
IFE ITS,[
	MOVEM 1,JSYS1
	MOVEM 2,JSYS2
	MOVEM 3,JSYS3

	HRROI 2,JFNSTR
	MOVE 1,[100001,,0] 
	GTJFN
	JRST JFNERR
	MOVE 2,[440000,,200000]
	OPENF
JFNERR:	MOVEI 1,0

	MOVEM 1,IJFN
	JUMPE 1,NOSTR

	MOVE A,AFNAM1
	MOVEM A,INFN1
	MOVE A,AFNAM2
	MOVEM A,INFN2

NOSTR:	MOVE 1,JSYS1
	MOVE 2,JSYS2
	MOVE 3,JSYS3
	POP P,TT
	POP P,T
	POP P,D
	POP P,C
	SKIPN IJFN
	JRST INFNF
]

A.IN10:	MOVE B,INPDLP
	IRP A,,[UTIBED,UREDP,IBUFOF,EOFCH]
	PUSH B,A
	TERMIN
	MOVEM B,INPDLP
	MOVEI A,UTIBE-UTIBUF
	ADDM A,IBUFOF
	ADDB A,UTIBED
	SOS A
	HRLI A,700
	MOVEM A,UREDP
	MOVE A,IBUFOF
	MOVE B,EOFCH
	LSH B,29.
	MOVEM B,UTIBE(A)
	AOS A,ILEVEL
	CAIL A,5
	ERRUUO (SIXBIT /IOV/)
;EVENTUALLY THE FOLLOWING TO MAKE .INSRT WORK UNDER MACROS:
;	MOVE A,[PUSHJ P,INCHR]
;	PUSHJ P,PUSHTT
	JRST ASSEM1

INCOL:	JUMPE SYM,A.IN1
	HLRM SYM,TDNM
	MOVEM SYM,SPCSNM
	JRST A.IN1

INSEMC:	JUMPE SYM,A.IN1
	MOVEM SYM,TSNM
	MOVEM SYM,SPCSNM
	JRST A.IN1

GETSBS:	MOVEI SYM,0
	PUSHJ P,RCH
	PUSHJ P,LOWRCS	;FOR LOWER CASE LOSER
	SKIPA B,[440600,,SYM]
GTSBS:	PUSHJ P,RCH
	PUSHJ P,LOWRCS	;FOR LOWER CASE LOSER
	CAIG A,40
	JRST GTSBS1
	CAIE A,";
	CAIN A,":
	JRST GTSBS1
	SUBI A,40
	TLNE B,770000
	IDPB A,B
	JRST GTSBS
GTSBS1:	MOVE B,A
	POPJ P,

A.IN4:	MOVE B,INDDP	;FINISH TABLE ENTRY
	SUBI B,4
	MOVE A,[TDNM,,TDNM]
	ADD A,B
	BLT A,TSNM(B)
	PUSHJ P,POPTT
	JRST A.IN3
INDDV:	SETOM INDDF	;INDIRECT DEVICE SPECIFIED: SEARCH TABLE
	MOVE B,INDDP
INDVL2:	MOVE A,TF1
INDVL1:	CAIG B,4
	JRST INDV1	;FILE NOT ALREADY IN TABLE
	SUBI B,10
	CAME A,TF1(B)
	JRST INDVL1	;FILENAME 1 DOESN'T CHECK
	MOVE A,TF2
	CAME A,TF2(B)
	JRST INDVL2	;FILENAME 2 DOESN'T CHECK
	MOVE A,TSNM
	CAME A,TSNM(B)
	JRST INDVL2	;SYSTEM NAME DOESN'T CHECK
	MOVE A,TDNM
	CAME A,TDNM(B)
	JRST INDVL2	;DEVICE NAME DOESN'T CHECK
	ADDI B,4	;FILE IS IN TABLE!
	JRST A.IN3	;OPEN SPECIFIED FILE


INDV1:	MOVE B,INDDP	;PREPARE TO GET CRUD FROM TTY
	CAILE B,4+10*MAXIND	;TABLE CAPACITY ABOUT TO BE EXCEEDED?
	ERRUUO (SIXBIT /TM@/)
	ADDI B,10
	EXCH B,INDDP
	SETOM FFFLG	;RESET
	MOVE A,[TDNM,,TDNM]
	ADD A,B
	BLT A,TSNM(B)
	PUSHJ P,GTYIP	;PUSH RCH STATUS, REPLACE WITH TTY
	AOSN INFNFG
	POPJ P,
	MOVE A,DNAM	;DEFAULT DEVICE NAME
	MOVEM A,TDNM
	JRST A.IN1

INFNF:	SETOM FFFLG
IFN ITS,[
	.IOPOP UTYIC,
	SKIPE SPCSNM
]
IFE ITS,[
	EXCH 0,IOPDLP
	MOVEM 1,JSYS1
	MOVE 1,IJFN
	CLOSF
	JFCL

	MOVE 1,JSYS1

	POP 0,IJFN
	EXCH 0,IOPDLP
]
	JRST INFNFX
	MOVE A,[SIXBIT/MACLIB/]
	MOVEM A,SPCSNM
	JRST A.IN3+1	; PATCH MAKES MIDAS LOOK IN MACLIB; FOR UNFOUND INSERT FILES (MSS)

INFNFX:	SKIPN INDDF
	JRST INFNF2	;IF CRUD DIDN'T COME FROM TTY THEN TYPE REAL ERROR MESSAGE
	PUSHJ P,TYPFIL
	TYPR [ASCII /  NOT FOUND. TRY AGAIN.
!/]
	MOVS B,INDDP	;RESET TABLE
	SUB B,[7,,0]
	PUSHJ P,GTYIP
	JRST A.IN1

GTYIP:	MOVE A,[PUSHJ P,RCHA]
	PUSHJ P,PUSHTT
	TRO FF,TTYRCH
	POPJ P,

INFNF2:	SETOM INFNFG	;SO INDDV WILL RETURN
	PUSHJ P,INDDV
	TYP2UU (SIXBIT /FNF/)
	PUSHJ P,TYPFIL
	TYPR [ASCII /  CORRECTION PLEASE.
!/]
	JRST A.IN1

TYPFIL:	SKIPA SYM,[-4,,0]
TYPF2:	SKIPA B,TDNM(SYM)
	HRLZ B,TDNM(SYM)
	PUSHJ P,SIXTYO
	MOVE A,FILSPC(SYM)
	PUSHJ P,TYO
	AOBJN SYM,TYPF2
	POPJ P,
	IMPUR
FILSPC:	":
	40
	40
	";
	PUR

PUSHTT:	PUSH P,F
	MOVE F,ITTYP
	PUSH F,GETCHR
	MOVEM A,GETCHR
	MOVE A,LIMBO1
	TLZE I,UNRCHF
	TLO A,1
	TRZE FF,MACRCH
	TLO A,2
	TRZE FF,TTYRCH
	TLO A,4
	PUSH F,A
POPTT1:	MOVEM F,ITTYP
	POP P,F
	POPJ P,

POPTT:	PUSH P,F
	MOVE F,ITTYP
	POP F,A
	POP F,GETCHR
	HRRZM A,LIMBO1
	TRZ FF,MACRCH\TTYRCH
	TLNE A,4
	TRO FF,TTYRCH
	TLNE A,2
	TRO FF,MACRCH
	TLZ I,UNRCHF
	TLNE A,1
	TLO I,UNRCHF
	JRST POPTT1


	IMPUR
	LTYPDL==5*2

ITTYP:	-LTYPDL,,TTYPDL-1
TTYPDL:	BLOCK LTYPDL
TDNM:	0	;DATA FOR .INSRT
TF1:	0
TF2:	0
TSNM:	0
	BLOCK 10*MAXIND
INPDLP:	INPDL-1
INPDL:	BLOCK 4*6
ILEVEL:	0
FFFLG:	0
INDDF:	0	;-1 IF .INSRT GETTING CRUD FROM TTY
INDDP:	0	;SET TO 4 BY INITIALIZATION; POINTER TO INDIRECT FILE TABLE
INFNFG:	0	;GETS SET FOR TTY INPUT WHEN FNF (OTHER THAN @:)
TEMFNM:	BLOCK 5	;HOLDS CHANNEL GOODIES FROM SYSTEM
INFN1:	0	;INSERT NAME 1
INFN2:	0	;NAME 2
SPCSNM:	0	;MACLIB; HACKING
]
	PUR

IFN LISTSW,[
PNTR:	MOVEM 17,PNTSA+17
	MOVEI 17,PNTSA
	BLT 17,PNTSA+16
	JUMPGE FF,PNTR5
	SKIPL LISTF
	JRST PNTR5
	AOSE LISTPF
	JRST PNTR1
	SKIPGE T,LISTAD
	JRST PNTR2
	PUSHJ P,P6OD
	HLRZS T
	PUSHJ P,PSOS	;PRINT SPACE OR '
	PUSHJ P,PILPTS
PNTR3:	HLRZ T,LISTWD
	PUSHJ P,P6OD
	HLRZ T,LSTRLC
	PUSHJ P,PSOS
	HRRZ T,LISTWD
	PUSHJ P,P6OD
	HRRZ T,LSTRLC
	PUSHJ P,PSOS
	PUSHJ P,PILPTS
	PUSHJ P,PILPTS
PNTR4:	MOVE TT,[440700,,LISTBF]
PNTR6:	CAMN TT,PNTBP
	JRST PNTR5A
	ILDB A,TT
	PUSHJ P,PILPT
	JRST PNTR6

PNTR5A:	MOVEI A,15
	PUSHJ P,PILPT
	MOVE A,LISTBC
	CAIE A,14
	MOVEI A,12
PNTR5C:	PUSHJ P,PILPT
PNTR5D:	SETOM LISTBC

PNTR5:	MOVE TT,[440700,,LISTBF]
	MOVEM TT,PNTBP
	MOVSI 17,PNTSA
	BLT 17,17
	POPJ P,

PNTR5B:	MOVE A,LISTBC
	CAIN A,14
	JRST PNTR5C
	JRST PNTR5D

	IMPUR
PNTSA:	BLOCK 20
	PUR

PNTR2:	MOVEI T,8
	MOVEI A,40
	PUSHJ P,PILPT
	SOJG T,.-1
	JRST PNTR3

PNTR1:	MOVE TT,[440700,,LISTBF]
	CAMN TT,PNTBP
	JRST PNTR5B
	MOVEI T,25.
	MOVEI A,40
	PUSHJ P,PILPT
	SOJG T,.-1
	JRST PNTR4

PSOS:	MOVEI A,"'
	TRNN T,1
PILPTS:	MOVEI A,40
PILPT:	IFN ITS,	.IOT LPTC,A
	POPJ P,

P6OD:	MOVE TT,[220300,,T]
P6OD1:	ILDB A,TT
	ADDI A,"0
	PUSHJ P,PILPT
	TLNE TT,770000
	JRST P6OD1
	POPJ P,
]
IFN TS,[
HINIT:	MOVEI A,(SIXBIT \DSK\)
	MOVEM A,DNAM
	MOVEM A,ONAM
	MOVE A,[50,,FNAM1]
	BLT A,FNAM2
	MOVE A,50
	MOVEM A,ONAM+1
IFE IMLAC,	MOVEI A,(SIXBIT \BIN\)
IFN IMLAC,	MOVEI A,(SIXBIT \IML\)
	MOVSM A,ONAM+2
	JRST GO21

.IOT=40000,,	;LEAVE HERE SO THAT IT WILL ASSEMBLE OUT OF TS
.OPEN=41000,,
.OPER=42000,,
.CALL=43000,,
.VALUE=.CALL 4,
.CORE=.CALL 6,
.SUSET=.CALL 13,
.CLOSE=.OPER 7
.IOPUSH=.OPER 13
.IOPOP=.OPER 14
.EOFC=.OPER 50
.IOPDL=.OPER 57
.FEED=.OPER 72
.SMASK==400006
.RSNAM==16
.SSNAM==400016
.SPICL==400017

TYIC==1
TYOC==2
UTYIC==3
UTYOC==4
LPTC==5


	IMPUR
TSINT:	0		;ALL INTERRUPTS (AT PRESENT) ARE CONSIDERED FATAL ERRORS
	0		;EXCEPT FOR SOME PDL OVERFLOWS
	JRST TSINTP		;ERROR PROCESSOR RE-ENABLES INTERRUPTS
	PUR

TSINTP:	MOVEM P,INTSVP	;SAVE P FOR POSSIBLE DEBUGGING
	MOVE A,TSINT	;GET INTERRUPT REQUEST WORD
	MOVEI B,(SIXBIT /INT/)	;DEFAULT
	SOS TSINT+1	;MAKE IT POINT TO PLACE INTERRUPT CAME FROM
	TRNN A,200000	;PDL OVERFLOW?
	JRST TSINT7	;NO
	LDB A,[270400,,@TSINT+1]	;GET AC FIELD OF GUILTY INSTRUCTION
	CAIN A,P	;IF P,
	MOVE P,[-LPDL,,PDL]	;RESET PDL POINTER
	AOSN FUNPDL	;FUNNY PDL OVERFLOW, IF SO DONT'T SKIP
	JRST BTBHAC	;YES SCREW AROUND
	MOVEI B,(SIXBIT /PDL/)
TSINT7:	TRNE A,20000	;MEMORY PROTECTION VIOLATION?
	MOVEI B,(SIXBIT /ILM/)	;YES
	MOVEM B,40
	AOS A,TSINT+1	;SO ERROR ROUTINE WILL PRINT OUT PROPERLY
	JSA A,ERROR

	IMPUR
INTSVP:	0	;SAVES P ON INTERRUPT FOR POSSIBLE DEBUGGING
	PUR

BTBHAC:	POP P,TSINT+1	;CAUSE SIMULATION OF POPJ
IFN ITS,	.DISMISS TSINT+1	;AND RETURN


BEG:
IFN ITS,[
	.SUSET [.RSNAM,,RSYSNM]	;GET SYSTEM NAME
	.SUSET [.SMASK,,[220000]]	;PDL OVERFLOW,MEMORY PROTECTION VIOLATION
	.SUSET [.SPICL,,[-1]]	;PERMIT INTERRUPTS (IN CASE RESTARTED DURING INTERRUPT)
	.SUSET [.RJNAME,,JNAME]	;READ JOB NAME FOR E&S DECISION
]
	MOVE P,[-LPDL,,PDL]	;INITIALIZE P
IFN ITS,[
	SKIPN 51
	JRST .+3
	MOVEM A,ISYSNM
	MOVEM A,OSYSNM
	.CORE 1+LSTLOC_-10.
	JRST .-1

	.OPEN TYIC,[30,,SIXBIT /   TTYMIDAS INPUT /]	;DDT MODE, CONVERT LOWER CASE TO UPPER CASE
	.VALUE
	.OPEN TYOC,[SIXBIT /  !TTYMIDAS OUTPUT/]
	.VALUE
	MOVEI A,TSSYMB
	MOVE B,[SIXBIT /CALLS/]
	.GETSYS A,
	.VALUE
	HRRM A,SP1
	MOVEI B,TSSYMB
	MOVEI AA,2
	DPB AA,[400400,,(B)]
	ADDI B,2
	CAIE B,(A)
	JRST .-3
	MOVE A,RPA
	MOVEM A,RRL1
	SKIPE 51
	JRST HINIT
]
IFE IMLAC&KERNEL,	MOVE B,[SIXBIT /MIDAS./]
IFN KERNEL,	MOVE B,[SIXBIT /MIDASK/]
IFN IMLAC,	MOVE B,[SIXBIT /MIDASI/]
	PUSHJ P,SIXTYO
IFN EANDS,[
	MOVE B,JNAME	;GOBBLE THE JOB NAME
	CAME B,[SIXBIT /MIDASD/]
	JRST .+3
	MOVSI B,(SIXBIT /E&S/)	;TACK E&S ONTO NAME
	PUSHJ P,SIXTYO
]
	MOVE B,[.FNAM2]
	PUSHJ P,SIXTYO

		;RETURN POINT FROM FATAL ERRORS

GO2:	IFN A1PSW,[
	SETOM PRGC
	SETOM OUTC
]
IFN LISTSW,[
	SETZM LISTF
	MOVE A,[TRNN FF,MACRCH]
	MOVEM A,RRR1
]
	PUSHJ P,CMD	;GET TYPED IN COMMAND
GO21:	PUSHJ P,WINIT	;OPEN OUTPUT FILE
	PUSHJ P,OPNRD	;OPEN INPUT FILE
IFN ITS,[
	MOVE A,[UTYIC,,A]
	.RCHST A,
	JUMPN A+1,GO22	;NOT DSK ETC
]
	MOVE A+1,FNAM1	;USE ORG FILE NAME
	MOVE A+2,FNAM2

GO22:	MOVEM A+1,RFNAM1	;GIVE THESE 
	MOVEM  A+1,INFN1
	MOVEM A+2,RFNAM2	;FOR .FNAM1,,2
	MOVEM A+2,INFN2
GO3:	JSP A,INIT
	JSP A,PS1
IFN A1PSW,[
	AOS PRGC	;INDICATE END STATEMENT ENCOUNTERED
	SETOM OUTC	;" " "
]
	TRNE FF,NPSS	;IF 2 PASS ASSEMBLY,
	PUSHJ P,OPNRD	;THEN RE-OPEN INPUT FILE
IFN ITS,	JSP A,PLOD
	JSP A,PS2
	JSP A,PSYMS
	TRNN FF,NPSS	;IF 1 PASS ASSEMBLY,
	JRST GO3	;THEN TRY TO ASSEMBLE ANOTHER PROGRAM
RETN2:	PUSHJ P,.FILE
	SKIPA
	JRST BEG
IFN ITS,[
	.LOGOU		;LOGOUT IF DISOWNED, ETC.
	.VALUE [ASCIZ /:KILL /]
]
IFE ITS,	HALTF

TYI:
IFN ITS,	.IOT TYIC,A
IFE ITS,[
	MOVEM 1,JSYS1
	PBIN
	MOVE A,1
	MOVE 1,JSYS1
	PUSHJ P,LOWRCS
]

	CAIN A,32	;FOR CONTROL Z, DO .VALUE
IFN ITS,	.VALUE 0
IFE ITS,	HALTF
	CAIN A,^R
	JRST TYO
	CAIE A,15
	CAIN A,12
	JRST TYO
	POPJ P,

TAB:	MOVEI A,11
TYO:
IFN ITS,	.IOT TYOC,A
IFE ITS,[
	MOVEM 1,JSYS1
	MOVE 1,A
	PBOUT
	MOVE 1,JSYS1
]
	POPJ P,


INCHR:
RPA:	ILDB A,UREDP
RPAE:	CAME A,EOFCH
	POPJ P,
INCHR3:	HRRZ A,UREDP
	CAMN A,UTIBED
	JRST UTRLD
	SOSL ILEVEL
	JRST IFPOP
IFN A1PSW,[	PUSHJ P,OUTCHK
	MOVSI A,-LNEDT
	XCT NEDT(A)	;SKIPS IF NED CONDITION TO BE COMPLAINED ABOUT
	AOBJN A,.-1
	JUMPGE A,RETN2
]
	MOVE A,[70700,,EOFCH]
	MOVEM A,UREDP
	ERRUUO (SIXBIT /NED/)
UTRLD:	MOVE A,[UTIBUF-UTIBE,,UTIBUF]
	ADD A,IBUFOF
IFN ITS,	.IOT UTYIC,A
IFE ITS,[
	MOVEM 1,JSYS1
	MOVEM 2,JSYS2
	MOVEM 3,JSYS3
	MOVSI 2,4400
	HRRI 2,-1(A)
	MOVE 1,IJFN
	MOVNI 3,UTIBE-UTIBUF
	SIN
	EXCH 1,JSYS1
	EXCH 2,JSYS2
	EXCH 3,JSYS3
	HRRZ A,JSYS2		; UPDATE POINTER
	HLL A,JSYS3
]

	JUMPGE A,UTRLD1
	ADDI A,1
	HRRZM A,RPAT1
	HRLZ A,EOFCH
	LSH A,11.
	HLLM A,@RPAT1
UTRLD1:	MOVE A,[700,,UTIBUF-1]
	ADD A,IBUFOF
	MOVEM A,UREDP
	JRST RPA

IFPOP:	PUSH P,B
	PUSH P,A
	MOVE A,[AFNAM1+2,,AFNAM1]
	BLT A,AFNAM2+2*MAXIND-2	;RESTORE AFNAM1 AND 2
	POP P,A
	MOVE B,INPDLP
	POP B,EOFCH
	POP B,IBUFOF
	POP B,UREDP
	POP B,UTIBED
	MOVEM B,INPDLP
;	EVENTUALLY THE FOLLOWING TO MAKE .INSRT WORK UNDER MACRO EXPANSIONS
;	PUSHJ P,POPTT
	MOVE A,IBUFOF
	MOVE B,EOFCH
	LSH B,29.
	MOVEM B,UTIBE(A)
	POP P,B
IFN ITS,	.IOPOP UTYIC,
IFE ITS,[
	MOVEM 1,JSYS1
	MOVE 1,IJFN
	CLOSF
	JFCL
	MOVE 1,JSYS1
	EXCH 0,IOPDLP
	POP 0,IJFN
	EXCH 0,IOPDLP
	JRST RPA


IFN A1PSW,[	;HOLLAR "NED" IF ANY OF THE FOLLOWING:
NEDT:	SKIPL PRGC	;NO END STATEMENTS HAVE BEEN ENCOUNTERED
	SKIPGE OUTC	;OUTPUT HAS OCCURED NOT MATCHED BY AN END STATEMENT
	SKIPGE OUTN1	;OUTPUT HAS OCCURED OTHER THAN IN 1PASS MODE
	TRNN FF,PSS	;CURRENTLY IN PASS 2
LNEDT==.-NEDT	;LENGTH OF TABLE
]

TPPB:	IDPB A,UTYOP
	AOSGE UTYOCT
	POPJ P,
TPPBF:	MOVEM A,UTYOP	;ENTRY TO OUTPUT CURRENT CONTENTS OF BUFFER
	MOVE A,[UTOBUF,,-<UTOBE-UTOBUF>]
	SUB A,UTYOCT
	MOVSS A
IFN ITS,	.IOT UTYOC,A
IFE ITS,[
	MOVEM 1,JSYS1
	MOVEM 2,JSYS2
	MOVEM 3,JSYS3

	MOVE 1,OJFN
	MOVE 2,[4400,,UTOBUF-1]
	MOVNI 3,UTOBE-UTOBUF
	SUB 3,UTYOCT
	SOUT
	MOVE 1,JSYS1
	MOVE 2,JSYS2
	MOVE 3,JSYS3
]

	MOVNI A,UTOBE-UTOBUF
	MOVEM A,UTYOCT
	MOVE A,[4400,,UTOBUF-1]
	EXCH A,UTYOP
	POPJ P,

.FILE:	MOVNI A,1
	PUSHJ P,TPPB	;OUTPUT A -1 SO STINK WILL SEE EOF
	PUSHJ P,TPPBF	;OUTPUT THE BUFFER
IFN ITS,[
	.CLOSE UTYIC,
	.CLOSE UTYOC,
]
IFE ITS,[
	MOVEM 1,JSYS1
	MOVE 1,IJFN
	CLOSF
	JFCL
	MOVE 1,OJFN
	CLOSF
	JFCL
	MOVE 1,JSYS1
]
IFN IMLAC,	PUSHJ P,IMTRAN	;TRANSLATE INTO IMLAC CODE
	POPJ P,

WINIT:
IFN ITS,[
	MOVEI A,7
	HRLM A,ONAM
	.SUSET [.SSNAM,,OSYSNM]	;SET SYSTEM NAME
	.OPEN UTYOC,ONAM
	ERRUUO (SIXBIT /TSL/)
	TLZ FF,PTPF	;INITIALLY ASSUME DEVICE NOT PAPER TAPE PUNCH
	.STATUS UTYOC,A	;GET STATUS OF OUTPUT CHANNEL
	ANDI A,77	;MASK TO DEVICE CODE
	CAIN A,7	;IF PAPER TAPE PUNCH,
	TLO FF,PTPF	;THEN SET PTPF
]
IFE ITS,[
	PUSH P,C
	PUSH P,D
	PUSH P,T
	PUSH P,TT
	MOVE D,[440700,,JFNSTR]
;BUILD TOPS-20 FILE SPEC

	SKIPN TT,OSYSNM
	JRST NOSNM2
	CAMN TT,[-1]
	JRST NOSNM2
	MOVEI A,"<
	IDPB A,D

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,">
	IDPB A,D

NOSNM2:	MOVE TT,ONAM+1

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,".
	IDPB A,D

	MOVE TT,ONAM+2

	CAMN TT,[SIXBIT />/]
	MOVSI TT,(SIXBIT /MID/)

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,0
	IDPB A,D

	MOVEM 1,JSYS1
	MOVEM 2,JSYS2
	MOVEM 3,JSYS3

	MOVE 1,[400001,,0] 
	HRROI 2,JFNSTR
	GTJFN
	ERRUUO (SIXBIT /TSL/)
	MOVE 2,[440000,,300000]
	OPENF
	ERRUUO (SIXBIT /TSL/)
	MOVEM 1,OJFN
	MOVE 1,JSYS1
	MOVE 2,JSYS2
	MOVE 3,JSYS3
	POP P,TT
	POP P,T
	POP P,D
	POP P,C
]

	MOVE A,[4400,,UTOBUF-1]
	MOVEM A,UTYOP
	MOVNI A,UTOBE-UTOBUF
	MOVEM A,UTYOCT
	MOVEI A,4	;INITIALIZE INDIRECT DEVICE TABLE
	MOVEM A,INDDP
	POPJ P,

TFEED:	TLNN FF,PTPF	;IF OUTPUT DEVICE NOT PTP,
	POPJ P,		;THEN DO NOTHING
	PUSHJ P,TPPBF	;OTHERWISE OUTPUT THE BUFFER,
TFEED1:	IFN ITS,	.FEED UTYOC,	;FEED A LINE,
	TLZA FF,PTPF	;IF THIS IS EXECUTED, UTYOC DOESN'T HAVE PTP AFTER ALL
	SOJG B,TFEED1	;FEED THE SPECIFIED NUMBER OF LINES,
	POPJ P,		;AND RETURN

IFN IMLAC,	.INSRT COM:IMTRAN MIDASI

OPNRD:	IFN ITS,	.IOPDL
	MOVEI A,2
	HRLM A,DNAM
	MOVE A,DNAM+1
	MOVEM A,AFNAM1
	MOVE A,DNAM+2
	MOVEM A,AFNAM2
IFN ITS,[
	.SUSET [.SSNAM,,ISYSNM]
	.OPEN UTYIC,DNAM
	ERRUUO (SIXBIT /FNF/)
	MOVEI A,UTYIC
	.EOFC A,
	MOVEM A,EOFCH
	LSH A,29.
	MOVEM A,UTIBE
]
IFE ITS,[
	PUSH P,C
	PUSH P,D
	PUSH P,T
	PUSH P,TT
	MOVE D,[440700,,JFNSTR]
;BUILD TOPS-20 FILE SPEC

	SKIPN TT,ISYSNM
	JRST NOSNM3
	CAMN TT,[-1]
	JRST NOSNM3
	MOVEI A,"<
	IDPB A,D

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,">
	IDPB A,D

NOSNM3:	MOVE TT,DNAM+1

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,".
	IDPB A,D

	MOVE TT,DNAM+2

	CAMN TT,[SIXBIT />/]
	MOVSI TT,(SIXBIT /MID/)

	LSHC T,6
	ANDI T,77
	ADDI T,40
	IDPB T,D
	JUMPN TT,.-4

	MOVEI A,0
	IDPB A,D

	MOVEM 1,JSYS1
	MOVEM 2,JSYS2
	MOVEM 3,JSYS3

	SKIPE 1,IJFN
	CLOSF				; CLOSE JFN IF THERE
	JFCL				; IGNORE FAILURE
	MOVE 1,[100001,,0] 
	HRROI 2,JFNSTR
	GTJFN
	ERRUUO (SIXBIT /FNF/)
	MOVE 2,[440000,,200000]
	OPENF
	ERRUUO (SIXBIT /FNF/)
	MOVEM 1,IJFN
	MOVE 1,JSYS1
	MOVE 2,JSYS2
	MOVE 3,JSYS3
	POP P,TT
	POP P,T
	POP P,D
	POP P,C
]

	MOVE A,[700,,UTIBE-1]
	MOVEM A,UREDP
	MOVEI A,UTIBE
	MOVEM A,UTIBED
	CLEARM ILEVEL
	CLEARM IBUFOF
	MOVEI A,INPDL-1
	MOVEM A,INPDLP
	MOVE A,[-LTYPDL,,TTYPDL-1]
	MOVEM A,ITTYP
	POPJ P,

	IMPUR
EOFCH:	0
RPAT1:	0
IBUFOF:	0
UTIBED:	UTIBE
	PUR

;GET COMMAND
SIF==400000
COLF==200000
NAMF==100000
NAM2F==40000
DEVF==20000
SCOLF==10000
SYSNF==4000

RCHA:	ILDB A,CMPTR
	JUMPN A,CPOPJ
RCHA1:	MOVE B,[10700,,CMBUF-1]
	MOVEM B,CMPTR
RCHA2:	PUSHJ P,TYI
	CAIN A,177
	JRST RCHA3
	IDPB A,B
	CAIE A,15
	JRST RCHA2
	MOVEI A,0
	IDPB A,B
	JRST RCHA
RCHA3:	CAMN B,[10700,,CMBUF-1]
	JRST RCHA4
	LDB A,B
	ADD B,[70000,,]
	SKIPGE B
	SUB B,[XOR 1]
	PUSHJ P,TYO	;ECHO CHARACTER JUST RUBBED OUT
	JRST RCHA2

RCHA4:	PUSHJ P,CRR	;RUBBED OUT TO BEGINNING OF STRING, TYPE CRLF
	JRST RCHA1	;RESET POINTERS AND TRY AGAIN

CMD:	PUSHJ P,CRR
IFN ITS,	MOVSI A,(SIXBIT/>/)	;MAKE DEFAULT FNAM2 OF INPUT >
IFE ITS,	MOVSI A,(SIXBIT /MID/)
	MOVEM A,FNAM2
	SETZB FF,CMPTR
CMDB:	CLEARM SUBNM
	MOVE TT,[10600,,SUBNM-1]
CMDL:	PUSHJ P,RCHA
	CAIN A,^Q
	JRST CMDQ
	CAIGE A,40
	JRST CMDE
	CAIN A,":
	JRST CMDCOL
	CAIN A,";
	JRST CMDSC
	CAIN A,"_
	JRST CMDLA
	CAIN A,40
	JRST CMDS
IFN LISTSW,[
	CAIN A,"(
	JRST CMDSW
]
CMDL1:	SUBI A,40
	PUSHJ P,SYLEND
	TLNE TT,770000
	IDPB A,TT
	JRST CMDL
CMDQ:	PUSHJ P,RCHA
	JRST CMDL1
CMDSC:	TLOA FF,SCOLF
CMDCOL:	TLO FF,COLF
CMDS:	CAMN TT,[10600,,SUBNM-1]
	JRST CMDL
	TLO FF,SIF
	MOVE C,SUBNM
	JRST CMDB
SYLEND:	TLZN FF,SIF
	POPJ P,
;DROPS THRU


SYLE2:	TLZE FF,COLF
	JRST SYLDV
	TLZE FF,SCOLF
	JRST SYLSN
	TLOE FF,NAMF
	JRST NAM2
	MOVEM C,FNAM1
	POPJ P,
NAM2:	TLON FF,NAM2F
	MOVEM C,FNAM2
	POPJ P,

SYLSN:	TLO FF,SYSNF
	MOVEM C,ISYSNM
	POPJ P,
SYLDV:	TLO FF,DEVF
	LDB T,[360600,,C]
	CAIG T,'9
	CAIGE T,'0
	JRST SYLDV1
	TLNN C,7777
	MOVSI C,(SIXBIT /UT/+T)
SYLDV1:	HLRZM C,DNAM
	POPJ P,

CMDLA:	PUSHJ P,SYLE1
	HLRZS FF
	MOVE T,[DNAM,,ONAM]
	BLT T,ONAM+3
	JRST CMDB

CMDE:	PUSHJ P,SYLE1
	TLNN FF,NAMF
	JRST CMDB
CMDE1:	MOVEI T,(SIXBIT /DSK/)
	TLNN FF,DEVF
	MOVEM T,DNAM
	MOVE T,RSYSNM
	TLNN FF,SYSNF
	MOVEM T,ISYSNM
	TRNN FF,SYSNF
	MOVEM T,OSYSNM
	MOVE T,DNAM
	TRNN FF,DEVF
	MOVEM T,ONAM
	TRNE FF,NAM2F
	POPJ P,
	MOVE T,FNAM1
	EXCH T,ONAM+1
	TRNN FF,NAMF
IFE IMLAC,	MOVSI T,(SIXBIT /BIN/)
IFN IMLAC,	MOVSI T,(SIXBIT /IML/)
	MOVEM T,ONAM+2
	POPJ P,

SYLE1:	CAMN TT,[10600,,SUBNM-1]
	JRST SYLEND
	MOVE C,SUBNM
	JRST SYLE2

	IMPUR
RSYSNM:	-1
CMBUF:	BLOCK 50
AFNAM1:	0
AFNAM2:	0
BLOCK 2*MAXIND	;STORAGE FOR .INSRTS
DNAM:	0
FNAM1:	0
FNAM2:	0
ISYSNM:	-1
ONAM:	BLOCK 3
OSYSNM:	-1
SUBNM:	0
CMPTR:	0
UTIBUF:	BLOCK 40
UTIBE:	0
	BLOCK 5*<UTIBE-UTIBUF>
UTOBUF:	BLOCK 200
UTOBE:
UREDP:	0
UTYOP:	0
UTYOCT:	0

RFNAM1:	0	;REAL FILE NAME 1
RFNAM2:	0	;REAL FILE NAME 2
JNAME:	0	;STORE JOB NAME
	PUR

IFN LISTSW,[
CMDSW:	PUSHJ P,RCHA
	CAIN A,")
	JRST CMDL
IFN LISTSW,	CAIE A,"L
	JRST CMDSW
IFN LISTSW,[	SETOM LISTF	;IF SWITCHES NEEDED FOR OTHER THAN LISTING, DELETE OUTER CONDITIONAL
	MOVSI A,(JFCL)
	MOVEM A,RRR1
	MOVE A,[PUSHJ P,RCH]
	MOVEM A,RRL1
CMDSW1:	.OPEN LPTC,[SIXBIT /  !TPLWALL  PAPER /]
	JRST .+2
	JRST CMDSW
	.OPEN LPTC,[SIXBIT /  !LPTWALL  PAPER /]
	.VALUE	[ASCIZ /:KILL/]
	JRST CMDSW
	.SLEEP A,
	JRST CMDSW1
]
]
]

FEED1:	SKIPA B,[40]
FEED:	MOVEI B,5
	JRST TFEED"
PPB:	JUMPGE FF,CPOPJ
PPBA:	JRST TPPB"

CONSTANTS
	IMPUR
PATCH:	BLOCK 40
	PUR

GDTAB:	REPEAT 3,POPJ P,76
	JRST UT141
	REPEAT 5,POPJ P,76
	POPJ P,75	;TAB
	POPJ P,74	;LF
	REPEAT 2,POPJ P,76
	POPJ P,74	;CR
	REPEAT "!-16-1,POPJ P,76
	POPJ P,40	;SPACE
	POPJ P,41	;!
	POPJ P,42	;"
	POPJ P,43	;#
	ADD SYM,%$SQ(D)	;$
	ADD SYM,%%SQ(D)	;%
	POPJ P,46	;&
	POPJ P,47	;'
	POPJ P,50	;(
	POPJ P,51	;)
	POPJ P,52	;*
	POPJ P,53	;+
	POPJ P,54	;,
	POPJ P,55	;-
	POPJ P,56	;.
	POPJ P,57	;/
	REPEAT 10.,JSP CH2,RR2	;DIGITS
	POPJ P,60	;:
	POPJ P,61	;;
	POPJ P,62	;<
	POPJ P,63	;=
	POPJ P,64	;>
	POPJ P,65	;?
	POPJ P,66	;@
	IRPC Q,,ABCDEFGHIJKLMNOPQRSTUVWXYZ
	ADD SYM,%!Q!SQ(D)
TERMIN
	POPJ P,67	;[
	POPJ P,70	;\
	POPJ P,71	;]
	POPJ P,72	;^
	POPJ P,73	;_


	REPEAT 141-"_-1,POPJ P,76
	JRST UT141
	REPEAT 177-141,POPJ P,76
	IFN .-GDTAB-200,[PRINTX /GDTAB LOSES
/]

IRPC Q,,ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890$%.
%!Q!SQ:	0
	SQUOZE 0,Q/50/50/50/50/50
	SQUOZE 0,Q/50/50/50/50
	SQUOZE 0,Q/50/50/50
	SQUOZE 0,Q/50/50
	SQUOZE 0,Q/50
	SQUOZE 0,Q

TERMIN

IFN TS,[DEFINE .LOP A
.LVAL1=.OP A
TERMIN]
.LOP IDIV SMK 2

;MAC PROC TABLES
IFE SLOW,[
IFE MACGRO,[	IMPUR
MACTAB:	773767750000	;MAC CHAR STG
	PUR
]

INIT1:	HRLZI A, 2*-SMK-LCONTB-.LCN18-.LVAL1-NCONS*3-LCNGLO
	CLEARM ST(A)
	AOBJN A,.-1
SPREAD:	MOVEI AA,ISYMTB
	MOVS F,ISMTBB	;GET SWAPPED VALUE OF FIRST INSTRUCTION
SP3:	CAIL AA,EISYM1
	JRST SP1.1
	MOVE SYM,(AA)
	JUMPE SYM,SP2
	PUSHJ P,ES
	HRLZI T,SYMC
	HRLZ B,F
	MOVSI C,3INI
	PUSHJ P,VSM2
SP2:	ADDI F,1000
	AOJA AA,SP3
SP1.1:
IFN EANDS,[MOVE B,[SIXBIT /MIDASD/]	;SEE IF E&S SYMBOLS DESIRED
	CAME B,JNAME	;JNAME=MIDASD IF E&S SYMBOLS WANTED
	MOVEI AA,AFTES	;SYMBOLS NOT WANTED,SKIP THEM
]

	IMPUR

SP1:	CAIL AA,EISYMT	;MUNGED IN TS MODE
	JRST SP4
	JRST SPP

	PUR

SPP:	MOVE SYM,(AA)
	LDB T,[( 400400)SYM]
	ROT T,-4
	PUSHJ P,ES
	MOVE B,1(AA)
	MOVSI C,3INI
	CAMN T,[(GLOEXT)]
	TLO C,3LLV
	PUSHJ P,VSM2
	AOS AA
	AOJA AA,SP1

]
IFE SLOW,CONSTANTS
IFN SLOW\MACGRO,[

	IMPUR
BKBUF:		;HEADER WORD FOR THIS RELOCATABLE BLOCK	
LOC BKBUF+BSIZE+5	;RELOCATABLE BLOCK ASSEMBLED HERE
GLOTB:	
LOC .+20
PDL":	
	BLOCK LPDL+1

IFN MACGRO,[
ST:	;SYMBOL TABLE
	LOC ST+2*SMK
	3RDWRD:
	.LOP IDIV SMK 2
	LOC .+.LVAL1
]

CONBIT:	
LOC .+.LCN18+1
CONTAB:	
LOC CONTAB+LCONTB
PCNTB:	
LOC PCNTB+NCONS*3
CONGLO:
LOC .+LCNGLO
VARTAB:	BLOCK NVARS

STRSTO:	BLOCK STRL	;STRING STORAGE
DMYDEF:	BLOCK DMDEFL	;DMMY ARGS FOR MAC BEING DEFINED
DSTG:	BLOCK DSSIZ
DMYAGT:	BLOCK DMYAGL	;DMY ARG POINTER FOR MACROS BEING EXPANDED
TOPPP:
MACPDL:	BLOCK MPDLL
GCSV:	BLOCK 16	;SAVES ACCUMULATORS DURING MACRO GARBAGE COLLECTION


IFE MACGRO,[
;SPECIAL VARIABLES FOR SMIDAS

;SYMBOL TABLE POINTERS

SYMPTR:	ISYMTB

STA:	ISYMTB(A)
STAA:	ISYMTB(AA)
STC:	ISYMTB(C)
STD:	ISYMTB(D)
STT:	ISYMTB(T)
NPTR1==.-STA

ST1A:	ISYMTB+1(A)
ST1AA:	ISYMTB+1(AA)
ST1D:	ISYMTB+1(D)
ST1T:	ISYMTB+1(T)
NPTR2==.-ST1A
;3RDWORD IN FO POINTER

3RDWRD:	0(TM)
]
	

;SYMBOL LENGTHS

MAXMAC:	<MACL-2>*4-10
MMAXMC:	<MACL-2>*4
IFN MACGRO,	PUR
IFE MACGRO,[
MSMK:	0
2SMK:	0

SYMSND==3RDWRD

MACTAB:	773767750000
	PUR
SP1:
INIT1:	MOVEI AA,EISYMT	;CLOBBERED IN TS TO POINT TO END OF TS SYMBOLS
	SUBI AA,ISMTBB	;GET TOTAL NUMBER OF ENTRIES
	LSH AA,-2	;AMOUNT OF 3RD WORD NEEDED
IFN ITS,[
	.SUSET [.RMEMT,,T]	;CHECK FOR ENOUGH ROOM
	ADDI AA,@INIT1	;AA CONTAINS END OF SYMBOL TABLE
	CAIL T,(AA)	;SKIP IF NOT ENOUGH CORE
	JRST COROK
	ADDI AA,1777	;FIND AMOUNT NEEDED
	IDIVI AA,2000	;IN BLOCKS
	MOVEI T,(AA)	;SAVE IT IN T
	.CORE (AA)
	JRST .-1
]
COROK:	SUBI T,ISYMTB	;FIND TOTAL AMOUNT OF SYMBOL TABLEAGE IN WORLD
	IDIVI T,5	;1/5 IS FOR 3RD WORD, 4/5 FOR OTHER
	LSH T,2
	MOVEM T,2SMK
	LSH T,-1	;FOR MSMK
	MOVNM T,MSMK	;SAVE
	HRRZS MSMK
	LSH T,1
	ADDI T,ISYMTB
	HRRM T,3RDWRD

;HERE TO SET UP 3RD WORD ENTRIES

	MOVEI AA,ISYMTB	;SET UP POINTER
SPLOOP:	LDB T,[400400,,(AA)]	;GET CODE BITS
	MOVSI C,3INI	;INITIAL SYMBOL
	CAIN T,GLOEXT_-14.	;IF GLOBAL
	TLO C,3LLV	;SAY LOADER WILL SUPPLY VALUE
	MOVE T,AA
	SUBI T,ISYMTB
	3PUT C,T
	ADDI AA,2	;MOVE THRU
	CAIGE AA,@INIT1
	JRST SPLOOP
	JRST SP4
CONSTANTS

LOC MACTAB+MACL

	UFA==FSC-<2000,,0>
SYMBL1==(UFA)
NOVAL==1	;SAY SYMBOL HAS NO VALUE
EQUALS SQUO SQUOZE	;SAVE MEANING OF SQUOZE

;NOW REDEFINE IT FOR SPECIAL USE

DEFINE SQUOZE A,B
	SQUO A,B
	IFN NOVAL,SYMBL1,,0
	SYMBL1==SYMBL1+1000
	TERMIN
]

]

IFN MACGRO,[	IMPUR
MACTAB:	773767750000
	PUR
]
	IMPUR

UFA=FSC-(2000)	;ALLOW FOR BOOTSTRAP, EVENTUALLY FLUSH, MAYBE

ISMTBB:	UFA		;FIRST OP. CODE IN ISYMTB

ISYMTB:

SQUOZE 10,UFA	;PDP10 INSTRUCTION
SQUOZE 10,DFN	;PDP10 INSTRUCTION
SQUOZE 10,FSC
SQUOZE 10,IBP
SQUOZE 10,ILDB
SQUOZE 10,LDB
SQUOZE 10,IDPB
SQUOZE 10,DPB
SQUOZE 10,FAD
SQUOZE 10,FADL
SQUOZE 10,FADM
SQUOZE 10,FADB
SQUOZE 10,FADR
SQUOZE 10,FADRI
SQUOZE 10,FADRM
SQUOZE 10,FADRB
SQUOZE 10,FSB
SQUOZE 10,FSBL
SQUOZE 10,FSBM
SQUOZE 10,FSBB
SQUOZE 10,FSBR
SQUOZE 10,FSBRI
SQUOZE 10,FSBRM
SQUOZE 10,FSBRB
SQUOZE 10,FMP
SQUOZE 10,FMPL
SQUOZE 10,FMPM
SQUOZE 10,FMPB
SQUOZE 10,FMPR

SQUOZE 10,FMPRI
SQUOZE 10,FMPRM
SQUOZE 10,FMPRB
SQUOZE 10,FDV
SQUOZE 10,FDVL
SQUOZE 10,FDVM
SQUOZE 10,FDVB
SQUOZE 10,FDVR
SQUOZE 10,FDVRI
SQUOZE 10,FDVRM
SQUOZE 10,FDVRB
SQUOZE 10,MOVE
SQUOZE 10,MOVEI
SQUOZE 10,MOVEM
SQUOZE 10,MOVES
SQUOZE 10,MOVS
SQUOZE 10,MOVSI
SQUOZE 10,MOVSM
SQUOZE 10,MOVSS
SQUOZE 10,MOVN
SQUOZE 10,MOVNI
SQUOZE 10,MOVNM
SQUOZE 10,MOVNS
SQUOZE 10,MOVM
SQUOZE 10,MOVMI
SQUOZE 10,MOVMM
SQUOZE 10,MOVMS

SQUOZE 10,IMUL
SQUOZE 10,IMULI
SQUOZE 10,IMULM
SQUOZE 10,IMULB
SQUOZE 10,MUL
SQUOZE 10,MULI
SQUOZE 10,MULM
SQUOZE 10,MULB
SQUOZE 10,IDIV
SQUOZE 10,IDIVI
SQUOZE 10,IDIVM
SQUOZE 10,IDIVB
SQUOZE 10,DIV
SQUOZE 10,DIVI
SQUOZE 10,DIVM
SQUOZE 10,DIVB
SQUOZE 10,ASH
SQUOZE 10,ROT
SQUOZE 10,LSH
SQUOZE 10,JFFO	;PDP10 INSTRUCTION
SQUOZE 10,ASHC
SQUOZE 10,ROTC
SQUOZE 10,LSHC
IFN SLOW,SYMBL1==SYMBL1+1000
IFE SLOW,0
SQUOZE 10,EXCH
SQUOZE 10,BLT
SQUOZE 10,AOBJP
SQUOZE 10,AOBJN
SQUOZE 10,JRST
SQUOZE 10,JFCL
SQUOZE 10,XCT
IFN SLOW,SYMBL1==SYMBL1+1000
IFE SLOW,0

SQUOZE 10,PUSHJ
SQUOZE 10,PUSH
SQUOZE 10,POP
SQUOZE 10,POPJ
SQUOZE 10,JSR
SQUOZE 10,JSP
SQUOZE 10,JSA
SQUOZE 10,JRA
SQUOZE 10,ADD
SQUOZE 10,ADDI
SQUOZE 10,ADDM
SQUOZE 10,ADDB
SQUOZE 10,SUB
SQUOZE 10,SUBI
SQUOZE 10,SUBM
SQUOZE 10,SUBB
SQUOZE 10,CAI
SQUOZE 10,CAIL
SQUOZE 10,CAIE
SQUOZE 10,CAILE
SQUOZE 10,CAIA
SQUOZE 10,CAIGE
SQUOZE 10,CAIN
SQUOZE 10,CAIG

SQUOZE 10,CAM
SQUOZE 10,CAML
SQUOZE 10,CAME
SQUOZE 10,CAMLE
SQUOZE 10,CAMA
SQUOZE 10,CAMGE
SQUOZE 10,CAMN
SQUOZE 10,CAMG
SQUOZE 10,JUMP
SQUOZE 10,JUMPL
SQUOZE 10,JUMPE
SQUOZE 10,JUMPLE
SQUOZE 10,JUMPA
SQUOZE 10,JUMPGE
SQUOZE 10,JUMPN
SQUOZE 10,JUMPG
SQUOZE 10,SKIP
SQUOZE 10,SKIPL
SQUOZE 10,SKIPE
SQUOZE 10,SKIPLE
SQUOZE 10,SKIPA
SQUOZE 10,SKIPGE
SQUOZE 10,SKIPN
SQUOZE 10,SKIPG
SQUOZE 10,AOJ
SQUOZE 10,AOJL
SQUOZE 10,AOJE
SQUOZE 10,AOJLE
SQUOZE 10,AOJA
SQUOZE 10,AOJGE
SQUOZE 10,AOJN
SQUOZE 10,AOJG
SQUOZE 10,AOS
SQUOZE 10,AOSL
SQUOZE 10,AOSE

SQUOZE 10,AOSLE
SQUOZE 10,AOSA
SQUOZE 10,AOSGE
SQUOZE 10,AOSN
SQUOZE 10,AOSG
SQUOZE 10,SOJ
SQUOZE 10,SOJL
SQUOZE 10,SOJE
SQUOZE 10,SOJLE
SQUOZE 10,SOJA
SQUOZE 10,SOJGE
SQUOZE 10,SOJN
SQUOZE 10,SOJG
SQUOZE 10,SOS
SQUOZE 10,SOSL
SQUOZE 10,SOSE
SQUOZE 10,SOSLE
SQUOZE 10,SOSA
SQUOZE 10,SOSGE
SQUOZE 10,SOSN
SQUOZE 10,SOSG

SQUOZE 10,SETZ
SQUOZE 10,SETZI
SQUOZE 10,SETZM
SQUOZE 10,SETZB
SQUOZE 10,AND
SQUOZE 10,ANDI
SQUOZE 10,ANDM
SQUOZE 10,ANDB
SQUOZE 10,ANDCA
SQUOZE 10,ANDCAI
SQUOZE 10,ANDCAM
SQUOZE 10,ANDCAB
SQUOZE 10,SETM
SQUOZE 10,SETMI
SQUOZE 10,SETMM
SQUOZE 10,SETMB
SQUOZE 10,ANDCM
SQUOZE 10,ANDCMI
SQUOZE 10,ANDCMM
SQUOZE 10,ANDCMB
SQUOZE 10,SETA
SQUOZE 10,SETAI
SQUOZE 10,SETAM
SQUOZE 10,SETAB
SQUOZE 10,XOR
SQUOZE 10,XORI
SQUOZE 10,XORM
SQUOZE 10,XORB
SQUOZE 10,IOR
SQUOZE 10,IORI
SQUOZE 10,IORM
SQUOZE 10,IORB
SQUOZE 10,ANDCB
SQUOZE 10,ANDCBI
SQUOZE 10,ANDCBM
SQUOZE 10,ANDCBB
SQUOZE 10,EQV
SQUOZE 10,EQVI

SQUOZE 10,EQVM
SQUOZE 10,EQVB
SQUOZE 10,SETCA
SQUOZE 10,SETCAI
SQUOZE 10,SETCAM
SQUOZE 10,SETCAB
SQUOZE 10,ORCA
SQUOZE 10,ORCAI
SQUOZE 10,ORCAM
SQUOZE 10,ORCAB
SQUOZE 10,SETCM
SQUOZE 10,SETCMI
SQUOZE 10,SETCMM
SQUOZE 10,SETCMB

SQUOZE 10,ORCM
SQUOZE 10,ORCMI
SQUOZE 10,ORCMM
SQUOZE 10,ORCMB
SQUOZE 10,ORCB
SQUOZE 10,ORCBI
SQUOZE 10,ORCBM
SQUOZE 10,ORCBB
SQUOZE 10,SETO
SQUOZE 10,SETOI
SQUOZE 10,SETOM
SQUOZE 10,SETOB
SQUOZE 10,HLL
SQUOZE 10,HLLI
SQUOZE 10,HLLM
SQUOZE 10,HLLS
SQUOZE 10,HRL
SQUOZE 10,HRLI
SQUOZE 10,HRLM
SQUOZE 10,HRLS
SQUOZE 10,HLLZ
SQUOZE 10,HLLZI
SQUOZE 10,HLLZM
SQUOZE 10,HLLZS
SQUOZE 10,HRLZ
SQUOZE 10,HRLZI
SQUOZE 10,HRLZM
SQUOZE 10,HRLZS
SQUOZE 10,HLLO
SQUOZE 10,HLLOI
SQUOZE 10,HLLOM
SQUOZE 10,HLLOS
SQUOZE 10,HRLO
SQUOZE 10,HRLOI
SQUOZE 10,HRLOM
SQUOZE 10,HRLOS
SQUOZE 10,HLLE
SQUOZE 10,HLLEI
SQUOZE 10,HLLEM
SQUOZE 10,HLLES
SQUOZE 10,HRLE
SQUOZE 10,HRLEI
SQUOZE 10,HRLEM
SQUOZE 10,HRLES
SQUOZE 10,HRR

SQUOZE 10,HRRI
SQUOZE 10,HRRM
SQUOZE 10,HRRS
SQUOZE 10,HLR
SQUOZE 10,HLRI
SQUOZE 10,HLRM
SQUOZE 10,HLRS

SQUOZE 10,HRRZ
SQUOZE 10,HRRZI
SQUOZE 10,HRRZM
SQUOZE 10,HRRZS
SQUOZE 10,HLRZ
SQUOZE 10,HLRZI
SQUOZE 10,HLRZM
SQUOZE 10,HLRZS
SQUOZE 10,HRRO
SQUOZE 10,HRROI
SQUOZE 10,HRROM
SQUOZE 10,HRROS
SQUOZE 10,HLRO
SQUOZE 10,HLROI
SQUOZE 10,HLROM
SQUOZE 10,HLROS
SQUOZE 10,HRRE
SQUOZE 10,HRREI
SQUOZE 10,HRREM
SQUOZE 10,HRRES
SQUOZE 10,HLRE
SQUOZE 10,HLREI
SQUOZE 10,HLREM
SQUOZE 10,HLRES
SQUOZE 10,TRN
SQUOZE 10,TLN
SQUOZE 10,TRNE
SQUOZE 10,TLNE
SQUOZE 10,TRNA
SQUOZE 10,TLNA
SQUOZE 10,TRNN
SQUOZE 10,TLNN
SQUOZE 10,TDN
SQUOZE 10,TSN
SQUOZE 10,TDNE
SQUOZE 10,TSNE
SQUOZE 10,TDNA
SQUOZE 10,TSNA
SQUOZE 10,TDNN
SQUOZE 10,TSNN
SQUOZE 10,TRZ
SQUOZE 10,TLZ
SQUOZE 10,TRZE
SQUOZE 10,TLZE
SQUOZE 10,TRZA
SQUOZE 10,TLZA
SQUOZE 10,TRZN
SQUOZE 10,TLZN
SQUOZE 10,TDZ
SQUOZE 10,TSZ
SQUOZE 10,TDZE
SQUOZE 10,TSZE

SQUOZE 10,TDZA
SQUOZE 10,TSZA
SQUOZE 10,TDZN
SQUOZE 10,TSZN

SQUOZE 10,TRC
SQUOZE 10,TLC
SQUOZE 10,TRCE
SQUOZE 10,TLCE
SQUOZE 10,TRCA
SQUOZE 10,TLCA
SQUOZE 10,TRCN
SQUOZE 10,TLCN
SQUOZE 10,TDC
SQUOZE 10,TSC
SQUOZE 10,TDCE
SQUOZE 10,TSCE
SQUOZE 10,TDCA
SQUOZE 10,TSCA
SQUOZE 10,TDCN
SQUOZE 10,TSCN
SQUOZE 10,TRO
SQUOZE 10,TLO
SQUOZE 10,TROE
SQUOZE 10,TLOE
SQUOZE 10,TROA
SQUOZE 10,TLOA
SQUOZE 10,TRON
SQUOZE 10,TLON
SQUOZE 10,TDO
SQUOZE 10,TSO
SQUOZE 10,TDOE
SQUOZE 10,TSOE
SQUOZE 10,TDOA
SQUOZE 10,TSOA
SQUOZE 10,TDON
SQUOZE 10,TSON

;THESE ARE THE E&S SYMBOLS, IF MIDASD THEY ARE USED

IFN SLOW,EQUALS SQUOZE SQUO


EISYM1:
IFN EANDS,[

.INSRT MIDE&S >

]
AFTES:
SQUOZE 4,BLKI
BLKI IOINST
SQUOZE 4,DATAI
DATAI IOINST
SQUOZE 4,BLKO
BLKO IOINST
SQUOZE 4,DATAO
DATAO IOINST
SQUOZE 4,CONO
CONO IOINST
SQUOZE 4,CONI
CONI IOINST
SQUOZE 4,CONSZ
CONSZ IOINST
SQUOZE 4,CONSO
CONSO IOINST

SQUOZE 10,APR
0
SQUOZE 10,PI
4
SQUOZE 10,PTP
100
SQUOZE 10,PTR
104
SQUOZE 10,TTY
120
SQUOZE 10,LPT
124
SQUOZE 10,DIS
130
SQUOZE 10,DC
200
SQUOZE 10,UTC
210
SQUOZE 10,UTS
214
SQUOZE 10,IBM	;2311 CONTROL
704


SQUOZE 10,LDBI	;REALLY ILDB,
LDBI
SQUOZE 10,DPBI	;AND IDPB
DPBI
SQUOZE 10,CLEAR
CLEAR
SQUOZE 10,CLEARI
CLEARI
SQUOZE 10,CLEARM
CLEARM
SQUOZE 10,CLEARB
CLEARB

SQUOZE 4,END
AEND
SQUOZE 4,LOC
ALOC
SQUOZE 4,XWORD
AXWORD
SQUOZE 4,RIM1
SRIM1
SQUOZE 4,RIM10
SRIM10
SQUOZE 4,SBLK
SIMBLK
SQUOZE 4,RIM
SRIM
SQUOZE 4,TITLE
ATITLE
SQUOZE 4,SQUOZE
ASQOZ
SQUOZE 4,EXP
AEXP
SQUOZE 4,XWD
AXWORD
SQUOZE 4,.BEGIN
ASBEG
SQUOZE 4,REPEAT
AREPEAT
SQUOZE 4,.END
ASEND
SQUOZE 4,OCT
AEXP
SQUOZE 4,CONSTA
CNSTNT
SQUOZE 4,SIXBIT
ASIXBIT
SQUOZE 4,ASCII
AASCII
SQUOZE 4,RADIX
ARDIX
SQUOZE 4,BLOCK
ABLOCK
SQUOZE 4,NOSYMS
ANOSYMS
SQUOZE 4,EXPUNGE
AEXPUNG
SQUOZE 4,NULL
ANULL
SQUOZE 4,EQUALS
AEQUAL

SQUOZE 4,DEFINE
ADEFINE
SQUOZE 4,IRP
AIRP
SQUOZE 4,IRPC
AIRP(400000)
SQUOZE 4,IRPS
(200000)AIRP
SQUOZE 4,TERMIN
ATERMIN
SQUOZE 4,.QUOTE
A.QOTE
SQUOZE 4,WORD
AWORD
SQUOZE 4,RELOCA
ARELOC
SQUOZE 4,1PASS
A1PASS
SQUOZE 4,OFFSET
AOFFSET
SQUOZE 4,IFG
JUMPG A,COND
SQUOZE 4,IFGE
JUMPGE A,COND
SQUOZE 4,IFE
JUMPE A,COND
SQUOZE 4,IFLE
JUMPLE A,COND
SQUOZE 4,IFN
JUMPN A,COND

SQUOZE 4,IFSE
SKIPN SCOND
SQUOZE 4,IFSN
SKIPE SCOND
SQUOZE 4,IF1
TRNE FF,COND1
SQUOZE 4,IF2
TRNN FF,COND1
SQUOZE 4,IFP
COND5
SQUOZE 4,IFL
JUMPL A,COND
SQUOZE 4,PRINTX
APRNTX
SQUOZE 4,PRINTC
(400000)APRNTC
SQUOZE 4,VARIAB
AVARIAB

SQUOZE 4,.LIBRA
A.LIB
SQUOZE 4,.LENGTH
A.LENGTH
SQUOZE 4,.LIFS
A.LIFS
SQUOZE 4,.LIFND
A.LIFND
SQUOZE 4,.ENTRY
A.ENTRY
SQUOZE 4,.EXTERN
A.EXTERN
SQUOZE 4,.ELDC
A.ELDC
IRPS A,,E N G LE GE L
SQUOZE 4,.LIF!A
JUMP!A A.LDCV
TERMIN
SQUOZE 4,.SLDR
A.SLDR
SQUOZE 4,.OP
A.OP
SQUOZE 4,.FORMAT
A.FORMAT
SQUOZE 4,.STOP
(400000)A.STOP
SQUOZE 4,.ISTOP
A.STOP
SQUOZE 4,.RPCNT
A.RPCNT
SQUOZE 4,.
GTVLP
SQUOZE 4,.LOP
A.LOP
SQUOZE 44,$.
0
SQUOZE 44,$R.
0
SQUOZE 44,.LVAL1
0
SQUOZE 44,.LVAL2
0
SQUOZE 4,.LNKOT
A.LNKOT
SQUOZE 4,.NSTGW
(1)STGWS
SQUOZE 4,.YSTGW
STGWS
SQUOZE 4,.GSSET
A.GSSET
SQUOZE 4,.TYPE
A.TYPE
SQUOZE 4,.LIBRQ
A.LIBRQ
SQUOZE 4,.GLOBAL
A.GLOB

SQUOZE 4,.GO
A.GO
SQUOZE 4,.TAG
A.TAG
SQUOZE 4,.ASCII
A.ASCII
SQUOZE 4,ASCIZ
AASCIZ
SQUOZE 4,.BYTC
A.BYTC
SQUOZE 4,.BYTE
A.BYTE
SQUOZE 4,.WALGN
A.WALGN
SQUOZE 4,.IRPCNT
A.IRPCNT

SQUOZE 4,.FNAM1
AFN1
SQUOZE 4,.FNAM2
AFN2
SQUOZE 4,.AFNM1
AFNM1
SQUOZE 4,.AFNM2
AFNM2
IFN TS,[
SQUOZE 4,.INSRT
A.INSRT
SQUOZE 4,.IFNM1
AIFN1
SQUOZE 4,.IFNM2
AIFN2
]

SQUOZE 4,.F
A.F
SQUOZE 4,.I
A.I

IFN TSSYMS,[
SQUOZE 10,.UAI
0
SQUOZE 10,.UAO
1
SQUOZE 10,.UII
4
SQUOZE 10,.UIO
5
SQUOZE 10,.BAI
2
SQUOZE 10,.BAO
3
SQUOZE 10,.BII
6
SQUOZE 10,.BIO
7
IFN KERNEL,[
SQUOZE 10,INKERN
1
]
TSSYMB:
]
EISYMT:
IFE SLOW\MACGRO,[
LOC MACTAB+MACL
BKBUF:		;HEADER WORD FOR THIS RELOCATABLE BLOCK	
LOC BKBUF+BSIZE+5	;RELOCATABLE BLOCK ASSEMBLED HERE
GLOTB:	
LOC .+20
PDL":	
	BLOCK LPDL+1


ST:		;SYMBOL TABLE 2 WORDS/SYM FIRST SQUOZE, SECOND "VALUE" (THIRD EXISTS, SEE 3RDWRD)	
LOC ST+2*SMK	;SMK = # SYMS
3RDWRD:	;THIRD WORD OF SYM LIVES HERE, REALLY A HALFWORD, CONTAINS RANDOM FLAGS
.LOP IDIV SMK 2
LOC .+.LVAL1+1
CONBIT:	
LOC .+.LCN18+1
CONTAB:	
LOC CONTAB+LCONTB
PCNTB:	
LOC PCNTB+NCONS*3
CONGLO:
LOC .+LCNGLO
VARTAB:	BLOCK NVARS

STRSTO:	BLOCK STRL	;STRING STORAGE
DMYDEF:	BLOCK DMDEFL	;DMMY ARGS FOR MAC BEING DEFINED
DSTG:	BLOCK DSSIZ
DMYAGT:	BLOCK DMYAGL	;DMY ARG POINTER FOR MACROS BEING EXPANDED
TOPPP:
MACPDL:	BLOCK MPDLL
GCSV:	BLOCK 16	;SAVES ACCUMULATORS DURING MACRO GARBAGE COLLECTION
IFE ITS,[
IOPDLN==20.
IOPDLP:	-IOPDLN,,IOPDL
IOPDL:	BLOCK IOPDLN
IJFN:	0
OJFN:	0
JFNSTR:	BLOCK	7
JSYS1: 0
JSYS2: 0
JSYS3: 0
]
LSTLOC:	-1
]


IFN MACGRO,[LOC MACTAB+MACL
LSTLOC:	-1
]
IFN SLOW,[BLOCK 400.
LSTLOC:	-1

]
IFN PURFLG,[
PURIFY:	MOVSI A,-<HPUR+1777>/2000
	HRRI A,1
	.CALL PUR1
	.VALUE
	HRLI A,-200(A)
	.CALL PUR2
	.VALUE
	.VALUE .+1
	0
PUR1:	SETZ
	'CORBLK
	1000,,200000	;READ ONLY
	1000,,-1
	SETZ A

PUR2:	SETZ
	'CORBLK
	1000,,0
	1000,,-1
	SETZ A
]
	INFORM [HIMPUR]\HIMPUR
	INFORM [HPUR]\HPUR

IFN TS,END BEG	;MUST BE BEFORE RELOCATABLE END STATEMENT,
		;SINCE TS MIDAS NOW TRIES TO DO MULTIPLE 1PASS ASSEMBLIES FROM SAME FILE.
		;PUTTING RELOCABLE END STATEMENT FIRST CAUSES IT TO ENCOUNTER OTHER
		;END STATEMENT IN NEW ASSEMBLY CAUSING CONFUSION

END 100
